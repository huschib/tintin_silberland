#NOP { /**
 *  @file   config.tin
 *
 *  Datei wird beim Starten von tintin++ ausgefuehrt und setzt
 *  Grundeinstellungen fest.
 */}

#CONFIG {AUTO TAB}          {5000}
#CONFIG {BUFFER SIZE}       {100000}
#CONFIG {CHARSET}           {UTF-8}
#CONFIG {COMMAND COLOR}     {<038>}
#CONFIG {COMMAND ECHO}      {ON}
#CONFIG {CONNECT RETRY}     {15}
#CONFIG {COLOR PATCH}       {ON}
#CONFIG {HISTORY SIZE}      {1000}
#CONFIG {LOG}               {RAW}
#CONFIG {MOUSE TRACKING}    {ON}
#CONFIG {PACKET PATCH}      {0.5}
#CONFIG {REPEAT CHAR}       {!}
#CONFIG {REPEAT ENTER}      {ON}
#CONFIG {SCROLL LOCK}       {ON}
#CONFIG {SPEEDWALK}         {OFF}
#CONFIG {TINTIN CHAR}       {#}
#CONFIG {VERBATIM}          {OFF}
#CONFIG {VERBATIM CHAR}     {\}
#CONFIG {VERBOSE}           {OFF}
#CONFIG {WORDWRAP}          {ON}

#NOP { /**
 *  @todo   Screen Grenzlinien von Terminal Dimension abh√§ngig machen
 *          bis 140 Zeichen Breite nur Scrollregion mit zuschaltbarem TopSplit
 *          140 - 200 Zeichen linke Spalte mit Karte und Infos, Scrollregion
 *          ab 200 Zeichen linke Spalte, Scrollregion, rechte Spalte
 */}

#VAR screen
{
    {top}       {2}
    {bottom}    {-6}
    {left}      {60}
    {right}     {-60}
    {panels}    {3}
}

#ALIAS {set_screen}
{
    #SCREEN CLEAR ALL;
    #SCREEN SCROLLREGION $screen[top] $screen[left]+1 $screen[bottom]-1 $screen[right]-1;
    #SCREEN INPUTREGION -1 $screen[left]+1 -1 $screen[right]-1;
    draw_borders
}

#ALIAS {draw_borders}
{
    #DRAW LINE 1 1 1 -1;
    #IF {$screen[panels] == 1}
    {
        #DRAW LINE $screen[bottom] $screen[left] $screen[bottom] $screen[right]
    };
    #ELSEIF {$screen[panels] == 2}
    {
        #DRAW TOP TEED LINE 1 $screen[left] -1 $screen[left];
        #DRAW LEFT TEED LINE $screen[bottom] $screen[left] $screen[bottom] $screen[right]
    };
    #ELSE
    {
        #DRAW TOP TEED LINE 1 $screen[right] -1 $screen[right];
        #DRAW TOP TEED LINE 1 $screen[left] -1 $screen[left];
        #DRAW TEED LINE $screen[bottom] $screen[left] $screen[bottom] $screen[right]
    }
}

#EVENT {SCROLLED MOUSE WHEEL UP}
{
    #BUFFER UP 10
}
#EVENT {SCROLLED MOUSE WHEEL DOWN}
{
    #BUFFER DOWN 10
}

#ALIAS {session_change}
{
    set_screen;
    def_map_enter_room;
    draw_status;
    #BUFFER END
}

#EVENT {SESSION ACTIVATED}
{
    session_change
}

#EVENT {SCREEN RESIZE}
{
    #SCREEN GET COLS screen[cols];
    #SCREEN GET ROWS screen[rows];
    #IF {$screen[cols] < 140}
    {
        #VAR screen[panels] 1;
        #VAR screen[left] 1;
        #VAR screen[right] -1
    };
    #ELSEIF {$screen[cols] < 200}
    {
        #VAR screen[panels] 2;
        #VAR screen[left] 60;
        #VAR screen[right] -1
    };
    #ELSE
    {
        #VAR screen[panels] 3;
        #VAR screen[left] 60;
        #VAR screen[right] -60
    };
    session_change
}

#NOP /* einige Konfigurationen setzen */
#ALIAS {config}
{
    #IF {"%0" == ""}
    {
        #ECHO {<199> Konfiguration <099>};
        #ECHO {%s %s} {@dotfill{Gilde;20}} {@an_aus{config[gilde]}};
        #ECHO {%s %s} {@dotfill{Anti-Kampfscroll;20}} {@an_aus{config[kampfscroll]}}
    };
    #IF {"%0" == "gilde"}
    {
        #ECHO {Gilde setzen mit: config gilde <Gilde>};
        #ECHO {moegliche Gilden sind momentan:};
        #ECHO {barden, berserker, druiden, nekromanten, paladine, zechpreller}
    };
    #IF {"%1" == "gilde"}
    {
        #IF {"%2" == "{barden|berserker|druiden|nekromanten|paladine|zechpreller}"}
        {
            #VAR config[gilde] %2;
            #ECHO {Gilde '%2' gesetzt.};
            #READ gilden/%2.tin;
            update_var
        }
    };
    #IF {"%0" == "wizard"}
    {
        #ECHO {Mit diesem Wizard koennen einige Grundeinstellungen vorgenommen werden.};
        config_wizard
    };
}

#ALIAS {config_wizard} {
    #NOP { /**
     *	@brief	Wizard zur ersten Konfiguration eines Charakters
     */};
}

#ALIAS {catch_load}
{
    #EVENT {READ ERROR}
    {
        #ECHO {Es existiert keine Datei %1.}
    };
    #READ {%2};
    #UNEVENT {READ ERROR}
}