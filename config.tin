#NOP { /**
 *  @file   config.tin
 *
 *  Datei wird beim Starten von tintin++ ausgefuehrt und setzt
 *  Grundeinstellungen fest.
 */}

#CONFIG {AUTO TAB}          {5000}
#CONFIG {BUFFER SIZE}       {100000}
#CONFIG {CHARSET}           {UTF-8}
#CONFIG {COMMAND COLOR}     {<038>}
#CONFIG {COMMAND ECHO}      {ON}
#CONFIG {CONNECT RETRY}     {15}
#CONFIG {COLOR PATCH}       {ON}
#CONFIG {HISTORY SIZE}      {1000}
#CONFIG {LOG}               {RAW}
#CONFIG {MOUSE TRACKING}    {ON}
#CONFIG {PACKET PATCH}      {0.5}
#CONFIG {REPEAT CHAR}       {!}
#CONFIG {REPEAT ENTER}      {ON}
#CONFIG {SCROLL LOCK}       {ON}
#CONFIG {SPEEDWALK}         {OFF}
#CONFIG {TINTIN CHAR}       {#}
#CONFIG {VERBATIM}          {OFF}
#CONFIG {VERBATIM CHAR}     {\}
#CONFIG {VERBOSE}           {OFF}
#CONFIG {WORDWRAP}          {ON}

#VAR screen
{
    {top}       {2}
    {bottom}    {-6}
    {left}      {60}
    {right}     {-60}
    {panels}    {3}
}

#ALIAS {set_screen}
{
    #SCREEN CLEAR ALL;
    #SCREEN SCROLLREGION $screen[top] $screen[left]+1 $screen[bottom]-1 $screen[right]-1;
    #SCREEN INPUTREGION -1 $screen[left]+1 -1 $screen[right]-1;
    draw_borders
}

#ALIAS {draw_borders}
{
    #DRAW LINE 1 1 1 -1;
    #IF {$screen[panels] == 1}
    {
        #DRAW LINE $screen[bottom] $screen[left] $screen[bottom] $screen[right];
        #DRAW LINE $screen[top]-1 $screen[left] $screen[top]-1 $screen[right]
    };
    #ELSEIF {$screen[panels] == 2}
    {
        #DRAW TOP TEED LINE 1 $screen[left] -1 $screen[left];
        #DRAW LEFT TEED LINE $screen[bottom] $screen[left] $screen[bottom] $screen[right]
    };
    #ELSE
    {
        #DRAW TOP TEED LINE 1 $screen[right] -1 $screen[right];
        #DRAW TOP TEED LINE 1 $screen[left] -1 $screen[left];
        #DRAW TEED LINE $screen[bottom] $screen[left] $screen[bottom] $screen[right]
    }
}

#EVENT {SCROLLED MOUSE WHEEL UP}
{
    #BUFFER UP 10
}
#EVENT {SCROLLED MOUSE WHEEL DOWN}
{
    #BUFFER DOWN 10
}

#ALIAS {session_change}
{
    set_screen;
    def_map_enter_room;
    draw_status;
    draw_leftpanel;
    #BUFFER END
}

#EVENT {SESSION ACTIVATED}
{
    session_change
}

#EVENT {SCREEN RESIZE}
{
    screen_dimensions;
    map_set_offset;
    session_change
}

#ALIAS {screen_dimensions}
{
    #SCREEN GET COLS screen[cols];
    #SCREEN GET ROWS screen[rows];

    #IF {$screen[rows] > 63}
    {
        #VAR mapping[rows] 27
    };
    #ELSEIF {$screen[rows] > 57}
    {
        #VAR mapping[rows] 21
    };
    #ELSE
    {
        #VAR mapping[rows] 15
    };

    #IF {$screen[cols] < 140}
    {
        #VAR screen[panels] 1;
        #VAR screen[left] 1;
        #VAR screen[right] -1;
        #MATH mapping[rows] {$mapping[rows] - 6};
        #MATH screen[top] {$mapping[rows] + 2}
    };
    #ELSEIF {$screen[cols] < 200}
    {
        #VAR screen[panels] 2;
        #VAR screen[left] 60;
        #VAR screen[right] -1;
        #VAR screen[top] 2
    };
    #ELSE
    {
        #VAR screen[panels] 3;
        #VAR screen[left] 60;
        #VAR screen[right] -60;
        #VAR screen[top] 2
    }
}

#NOP /* Beim Laden ausfuehren ... */
screen_dimensions

#VAR config_menu
{
    {gilde}
    {
        {abenteurer}    {gilde abenteurer}
        {barden}        {gilde barden}
        {berserker}     {gilde berserker}
        {druiden}       {gilde druiden}
        {nekromanten}   {gilde nekromanten}
        {paladine}      {gilde paladine}
        {zechpreller}   {gilde zechpreller}
        {abenteurer_conf} {}
        {barden_conf}
        {
            {ueben}     {barden_ueben}
            {buffen}    {barden_buffen}
            {richtung}
            {
                {alestra}   {barden_richtung alestra}
                {korriax}   {barden_richtung korriax}
                {sarrian}   {barden_richtung sarrian}
                {tirena}    {barden_richtung tirena}
            }
            {pfad}
            {
                {angara}    {barden_pfad angara}
                {arcon}     {barden_pfad arcon}
                {flammara}  {barden_pfad flammara}
                {fraggin_not}   {barden_pfad fraggin}
                {grimm}     {barden_pfad grimm}
                {khartak}   {barden_pfad khartak}
                {mira_not}      {barden_pfad mira}
                {nogi}      {barden_pfad nogi}
                {rynoxa}    {barden_pfad rynoxa}
                {schlain_not}   {barden_pfad schlain}
                {xulgsch_not}   {barden_pfad xulgsch}
                {zafira_not}    {barden_pfad zafira}
            }
        }
        {berserker_conf} {}
        {druiden_conf} {}
        {nekromanten_conf} {}
        {paladine_conf} {}
        {zechpreller_conf} {}
    }
    {gruppe}
    {
        {slgruppe}      {gruppe_slgruppe}
        {offen}         {gruppe_showinfo}
        {slgruppe_conf}
        {
            {name}      {gruppe_name}
        }
    }
    {karte}
    {
        {vt}        {karte vt}
        {unicode}   {karte unicode}
    }
    {allgemein}
    {
        {kompakt}       {kampf_kompakt}
        {fingerhut}     {fingerhut}
        {ultihalsband}  {ultihalsband}
    }
}

#ALIAS {config}
{
    #NOP set_config_receive on;
    #NOP set_config_options {Gilden};
    #VAR config_path {config_menu};
    #NOP #ECHO {CONFIG - %s} {Start};
    config_show {$config_menu};
    config_start
}

#ALIAS {config_start}
{
    #EVENT {RECEIVED INPUT CHARACTER}
    {
        #CURSOR GET tmp_input;
        #NOP #ECHO {"$tmp_input"};
        #IF {"$tmp_input" == "{@config_options_regex{}}"}
        {
            #FORMAT config_path {%s[%s]} {$config_path} {*current_menu[+@config_item_number{$tmp_input}]};
            config_show {$current_menu[+@config_item_number{$tmp_input}]};
            #CURSOR CLEAR
        };
        #ELSEIF {"$tmp_input" == "z"}
        {
            #REGEXP {$config_path} {{(.+)\[[^\]]+\]$}}
            {
                #VAR config_path {&2}
            };
            config_show {${$config_path}};
            #CURSOR CLEAR
        };
        #ELSEIF {"$tmp_input" == "q"}
        {
            #ECHO {Config abgebrochen.};
            #CURSOR CLEAR;
            config_stop
        }
    }
}

#FUNCTION {config_options_regex}
{
    #IF {&current_menu[] < 10}
    {
        #FORMAT result {[0-%s]} {&current_menu[]}
    };
    #ELSEIF {&current_menu[] < 10}
    {
        #FORMAT result {[0-9a-%s]} {@config_item_number{&current_menu[]}}
    };
    #RETURN $result
}

#ALIAS {config_stop}
{
    #UNEVENT {RECEIVED INPUT CHARACTER}
}

#ALIAS {config_show}
{
    #VAR current_menu %0;
    #VAR config_path_sep $config_path;
    #REPLACE config_path_sep {{^[a-z_]+}} {};
    #REPLACE config_path_sep {][} { -> };
    #REPLACE config_path_sep {[} {};
    #REPLACE config_path_sep {]} {};
    #ECHO {CONFIG: %s} {$config_path_sep};
    #FOREACH {*current_menu[]} {menu_item}
    {
        #ECHO {<199>(%s)<099> <099>%-20n<099> <199>%s<099>}
            {@config_item_number{&current_menu[$menu_item]}}
            {$menu_item}
            {>}
    };
    #ECHO {};
    #ECHO {<199>(%s)<099> %+20s} {z} {Zurueck};
    #ECHO {<199>(%s)<099> %+20s} {q} {Beenden};
    #ECHO {}
}

#FUNCTION {config_item_number}
{
    #IF {%1 < 10}
    {
        #VAR result %1
    };
    #ELSEIF {%1 > 9}
    {
        #FORMAT tmp {%m} {%1 + 87};
        #FORMAT tmp {%X} {$tmp};
        #FORMAT result {%x} {$tmp}
    };
    #ELSEIF {"%1" == "{[a-z]}"}
    {
        #FORMAT tmp {%A} {%1};
        #FORMAT tmp {%m} {%1 - 87};
        #FORMAT tmp {%X} {$tmp};
        #FORMAT result {%x} {$tmp}
    };
    #RETURN $result
}

#ALIAS {set_config_options}
{
    #LIST config_options CREATE {%1};
    #ECHO {CONFIG - %s} {Start};
    #LOOP 1 &config_options[] loop
    {
        #ECHO {(%s) %s} {$config_options[$loop]}
    }

}

#ALIAS {gilden_report}
{
    #IF {&{config[gilde]}}
    {
        #SEND {$gilden_report_cmd}
    }
}

#ALIAS {catch_load}
{
    #EVENT {READ ERROR}
    {
        #ECHO {Es existiert keine Datei %1.}
    };
    #READ {%2};
    #UNEVENT {READ ERROR}
}
