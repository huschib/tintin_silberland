#NOP { /**
 *  @class astral
 *  @brief Region: Astralebenen und Brunnen
 */}

#CLASS astral KILL
#CLASS astral OPEN

#ECHO {<029> -> <188>%s<288> geladen<099>} {region/astral.tin}

#ACTION {{^\| Anzahl der erlegten Gegner +: +([0-9]+) +\|$}}
{
    #VAR br_gegneranzahl %2
}

#SUBSTITUTE {{(^\| Anzahl der Gegnerstufen +:) +([0-9]+) +\|$}}
    {%2@br_avg{%3;Stufen}}
#SUBSTITUTE {{(^\| Summe erkaempfter Fragmente +:) +([0-9]+) +\|$}}
    {%2@br_avg{%3;Fragmente}}

#FUNCTION br_avg
{
    #MATH avg {1.00 * %1 / $br_gegneranzahl};
    #FORMAT {result} {%+13s %-25s |} {%1} {($avg %2/Gegner)}
}

#SUBSTITUTE {{(^\| (Wasser|Luft|Eis|Feuer)elementarenergie: +\( *([0-9]+)/( *[0-9]+)\) \|)$}}
    {%2 @frags_progress{%4;%5}}

#FUNCTION frags_progress
{
    #MATH fract {100 * 1.00 * %1 / %2};
    #FORMAT {result} {%+6s%%} {$fract}
}

#ALIAS {chart}
{
    #SEND {lies hyperastralkoordinaten};
    #ACTION {{^([A-Z][a-z]+) Circolar; ([A-Z][a-z]+) ([0-9]+), ([A-Z][a-z]+) ([0-9]+), ([A-Z][a-z]+) ([0-9]+).$}}
    {
        #IF {"%%2" == "Erster"}
        {
            #VAR astral_circolar c1
        };
        #ELSEIF {"%%2" == "Zweiter"}
        {
            #VAR astral_circolar c2
        };
        #ELSEIF {"%%2" == "Dritter"}
        {
            #VAR astral_circolar c3
        };
        #ELSEIF {"%%2" == "Vierter"}
        {
            #VAR astral_circolar c4
        };
        #ELSEIF {"%%2" == "Fuenfter"}
        {
            #VAR astral_circolar c5
        };
        #ELSEIF {"%%2" == "Sechster"}
        {
            #VAR astral_circolar c6
        };
        #FORMAT {astral_name} {astral_%s%.1%l%s%.1%l%s%.1%l%s} {$astral_circolar} {%%3} {%%4} {%%5} {%%6} {%%7} {%%8};
        #MAP SET ROOMNAME $astral_name;
        #UNACTION {{^([A-Z][a-z]+) Circolar; ([A-Z][a-z]+) ([0-9]+), ([A-Z][a-z]+) ([0-9]+), ([A-Z][a-z]+) ([0-9]+).$}}
    }
}

#ALIAS {seek}
{
    #SEND {lies hyperastralkoordinaten};
    #ACTION {{^([A-Z][a-z]+) Circolar; ([A-Z][a-z]+) ([0-9]+), ([A-Z][a-z]+) ([0-9]+), ([A-Z][a-z]+) ([0-9]+).$}}
    {
        #IF {"%%2" == "Erster"}
        {
            #VAR astral_circolar c1
        };
        #ELSEIF {"%%2" == "Zweiter"}
        {
            #VAR astral_circolar c2
        };
        #ELSEIF {"%%2" == "Dritter"}
        {
            #VAR astral_circolar c3
        };
        #ELSEIF {"%%2" == "Vierter"}
        {
            #VAR astral_circolar c4
        };
        #ELSEIF {"%%2" == "Fuenfter"}
        {
            #VAR astral_circolar c5
        };
        #ELSEIF {"%%2" == "Sechster"}
        {
            #VAR astral_circolar c6
        };
        #FORMAT {astral_name} {astral_%s%.1%l%s%.1%l%s%.1%l%s} {$astral_circolar} {%%3} {%%4} {%%5} {%%6} {%%7} {%%8};
        #MAP GOTO $astral_name;
        #UNACTION {{^([A-Z][a-z]+) Circolar; ([A-Z][a-z]+) ([0-9]+), ([A-Z][a-z]+) ([0-9]+), ([A-Z][a-z]+) ([0-9]+).$}}
    }
}

#ALIAS {nexus_create}
{
    #MAP DIG %1;
    #MAP MOVE %1;
    #MAP SET ROOMNAME wasser_%2;
    #MAP LINK ob wasser_%3 BOTH;
    #MAP LINK u wasser_%4 BOTH
}

#ALIAS {nexus_link}
{
    #MAP LINK %1 wasser_%2 BOTH;
    #MAP MOVE %1
}

#ALIAS {nexus_complete}
{
    #MAP DIG %1;
    #MAP MOVE %1
}

#SUBSTITUTE {Ein giftiggruener Saeuredaemon.}
    {<258>Saeuredaemon.<099> [<128>Eis<099>]}

#SUBSTITUTE {Ein Erdbebendaemon.}
    {<258>Erdbebendaemon.<099> [<128>Eis, Luft<099>]}

#SUBSTITUTE {Ein rasender Orkandaemon.}
    {<258>Orkandaemon.<099> [<118>Luft<099>]}

#CLASS astral CLOSE

#NOP { /**
Deine Regenerationsaura laesst nach.
 */}

