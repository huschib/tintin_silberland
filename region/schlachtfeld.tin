#NOP { /**
 *  @class schlachtfeld
 *  @brief
 */}

#CLASS schlachtfeld KILL
#CLASS schlachtfeld OPEN

#NOP #ECHO {<029>-- <188>%s<288> geladen<099>} {region/schlachtfeld.tin}

#GAG {Die Ratte hopst umher.}
#GAG {Eine Ratte nagt an einem toten Koerperteil.}
#GAG {Es stinkt hier ekelhaft nach Toten.}
#GAG {Jemand stoehnt sterbend.}
#GAG {Ein Koerper zuckt!}
#GAG {Schreie, schmerzende Schreie aus dem Osten!}
#NOP /* Lehmgolem an Pfadbeginn */
#GAG {Der Lehmgolem ruehrt sich nicht vom Fleck.}
#GAG {Warum ist hier niemand mehr?}
#GAG {Der Lehmgolem verharrt regungslos.}
#GAG {Es ist hier bedrohlich still.}
#GAG {Der Lehmgolem ist dumm.}
#GAG {Ein Stueck Lehm broeckelt von dem Golem ab. Es scheint ihn nicht zu stoeren.}
#GAG {Du hoerst Todesschreie aus einiger Entfernung.}
#GAG {Schreie toenen aus dem Westen.}

#HIGHLIGHT {Ahtawqa.} {green}
#HIGHLIGHT {Ein entlassener Soeldner.} {magenta}
#HIGHLIGHT {Ein Ork.} {magenta}
#HIGHLIGHT {Ein brutaler Kampfsoldat.} {magenta}
#HIGHLIGHT {Ein Ex-%w.} {magenta}
#HIGHLIGHT {Ein %w%?Zwergenberserker.} {magenta}
#HIGHLIGHT {Ein %w%?Zwergenberserkermeister.} {magenta}
#HIGHLIGHT {Ein Zwergenberserker{(?! geht)[^.]+[A-Z][a-z]+}.} {magenta}
#HIGHLIGHT {Ein Muellschlucker.} {jade}
#HIGHLIGHT {Eine grauslige Ratte.} {violet}
#HIGHLIGHT {Ein Opkac.} {light blue}

#SUBSTITUTE {Ein entlassener Soeldner.} {@order{%0}}
#SUBSTITUTE {Ein brutaler Kampfsoldat.} {@order{%0}}
#SUBSTITUTE {Ein Ork.} {@order{%0}}
#SUBSTITUTE {Ein Ex-%w.} {@order{%0}}
#SUBSTITUTE {Ein %w%?Zwergenberserker.} {@order{%0}}
#SUBSTITUTE {Ein %w%?Zwergenberserkermeister.} {@order{%0}}
#SUBSTITUTE {Ein Zwergenberserker{(?! geht)[^.]+[A-Z][a-z]+}.} {@order{%0}}

#NOP /* NPC: Ork */
#GAG {Der Ork grunzt.}
#GAG {Der Ork bruellt Dich wild an!!}
#GAG {Der Ork schreit wild im Kampf!}
#ACTION {Der Ork beisst {.+} in die Schulter!}
{
    setze_angriff_spell schulterbiss npc;
    #LINE GAG
}
#ACTION {Der Ork beisst {.+}.}
{
    setze_angriff_spell biss npc;
    #LINE GAG
}
#ACTION {Das Blut spritzt, als des Orks Waffe in {.+} eindringt!}
{
    setze_angriff_spell {blut spritzt} npc;
    #LINE GAG
}

#NOP /* NPC: Zwergenberserker */
#GAG {Der %w%?Zwergenberserker GROOOOOAAAAAAAARRRRRRRRRTT!!!!!!!}
#GAG {Der %w%?Zwergenberserker flucht.}
#GAG {Der %w%?Zwergenberserker freut sich ueber die wilde Schlaegerei!}
#GAG {Der %w%?Zwergenberserker AAAAAAARRRRGGGHHHTTTTTTT.}
#ACTION {Der %w%?Zwergenberserker rammt {.+} mit seiner Schulter.}
{
    setze_angriff_spell rammen npc;
    #LINE GAG
}
#ACTION {Der %w%?Zwergenberserker wirft {.+} zu Boden.}
{
    setze_angriff_effekte 5 B;
    #LINE GAG
}
#ACTION {Der %w%?Zwergenberserker bruellt {.+} an.}
{
    setze_angriff_spell kampfschrei npc;
    #LINE GAG
}
#ACTION {Der %w%?Zwergenberserker zertritt {.+} unter blossen Fuessen.}
{
    #NOP setze_angriff_spell schulterbiss npc;
    #NOP #LINE GAG
}
#ACTION {Der %w%?Zwergenberserker tritt {.+} in den Bauch.}
{
    setze_angriff_spell bauchtritt npc;
    #LINE GAG
}
#ACTION {Der %w%?Zwergenberserker spaltet {.+} mit seine%. {.+}.}
{
    setze_angriff_spell spalten npc;
    #LINE GAG
}
#ACTION {Mit einem maechtigen Rueckhandhieb schleudert {.+}}
{
    setze_angriff_spell rueckhandh. npc;
    #LINE GAG;
    #IF {"%1" != "{.+\.}"}
    {
        #ACTION {{.*\.}}
        {
            #LINE GAG;
            #UNACTION {{.*\.}}
        }
    }
}
#ACTION {Das Blut spritzt, als {.+}}
{
    setze_angriff_spell {blut spritzt} npc;
    #LINE GAG;
    #IF {"%1" != "{.+\.}"}
    {
        #ACTION {{.*\.}}
        {
            #LINE GAG;
            #UNACTION {{.*\.}}
        }
    }
}

#NOP {
Der grosse Zwergenberserker gibt dem Weingeist einen harten Kopfstoss.
Des grossen Zwergenberserkers Kopf trifft nur Luft.
Der grosse Zwergenberserker verletzt den Weingeist.
#NOP }

#NOP /* NPC: Kampfsoldat */
#ACTION {Der Kampfsoldat steigt {.+} heftig auf den Fuss.}
{
    setze_angriff_spell {Fuss steigen} npc;
    #LINE GAG
}
#ACTION {Der Kampfsoldat versetzt {.+} einen schweren Schlag ins Genick.}
{
    setze_angriff_spell genickschlag npc;
    #LINE GAG
}

#NOP /* NPC: Soeldner */
#ACTION {Der ehemalige Soeldner tritt {.+} in den Bauch.}
{
    setze_angriff_spell bauchtritt npc;
    #LINE GAG
}
#ACTION {Der ehemalige Soeldner schlaegt {.+} fest in den Ruecken!}
{
    setze_angriff_spell rueckenschl. npc;
    #LINE GAG
}

#NOP /* NPC: Ex-Garnisonssoldat */
#GAG {Der {.+} schreit unerwartet laut auf! GRAAAAARGH!!}
#GAG {Der {.+} verzieht sein Gesicht ernst.}
#GAG {Der {.+} konzentriert sich auf den Kampf.}
#GAG {Der {.+} grinst: HAHA, ENDLICH!}
#ACTION {Der {.+} trifft {.+} an einer empfindlichen Stelle!}
{
    setze_angriff_spell {empf. Stelle} npc;
    #LINE GAG
}
#ACTION {Der {.+} schlaegt {.+} eine tiefe Fleischwunde in den Arm!!}
{
    setze_angriff_spell fleischwunde npc;
    #LINE GAG
}
#ACTION {Der {.+} schlaegt in einem Wutanfall wild um sich.}
{
    setze_room_spell wutanfall npc;
    #LINE GAG
}


#FUNCTION {order}
{
    #REGEXP {%0} {{Eine? .*?([A-Z][A-Za-z\-]+).*}}
    {
        #LIST feinde ADD &2
    }
    {
        #LIST feinde ADD %0
    };
    #VAR result {(&feinde[]) %0};
    #RETURN $result
}

#ALIAS {hobbit}
{
    #SWITCH {"%0"}
    {
        #CASE {"reset"}
        {
            #VAR hobbit
            {
                {soeldner}      {0}
                {ork}           {0}
                {kampfsoldat}   {0}
                {berserker}     {0}
                {ex-soldat}     {0}
                {barghest}      {0}
                {ghoul}         {0}
                {troll}         {0}
                {lehmgolem}     {0}
                {druide}        {0}
                {zechpreller}   {0}
                {nekromantin}   {0}
            }
        };
        #DEFAULT
        {
            update_var hobbit q;
            #ECHO {Das hast bisher erreicht:};
            #ECHO {%n %s} {@dotfill{*hobbit[+1];20}} {$hobbit[+1]};
            #ECHO {%n %s} {@dotfill{*hobbit[+2];20}} {$hobbit[+2]};
            #ECHO {%n %s} {@dotfill{*hobbit[+3];20}} {$hobbit[+3]};
            #ECHO {%n %s} {@dotfill{*hobbit[+4];20}} {$hobbit[+4]};
            #ECHO {%n %s} {@dotfill{*hobbit[+5];20}} {$hobbit[+5]};
            #ECHO {%n %s} {@dotfill{*hobbit[+6];20}} {$hobbit[+6]};
            #ECHO {%n %s} {@dotfill{*hobbit[+7];20}} {$hobbit[+7]};
            #ECHO {%n %s} {@dotfill{*hobbit[+8];20}} {$hobbit[+8]};
            #ECHO {%n %s} {@dotfill{*hobbit[+9];20}} {$hobbit[+9]};
            #ECHO {%n %s} {@dotfill{*hobbit[+10];20}} {$hobbit[+10]};
            #ECHO {%n %s} {@dotfill{*hobbit[+11];20}} {$hobbit[+11]};
            #ECHO {%n %s} {@dotfill{*hobbit[+12];20}} {$hobbit[+12]};
            #MATH summe {
                $hobbit[+1] + $hobbit[+2] + $hobbit[+3] + $hobbit[+4] +
                $hobbit[+5] + $hobbit[+6] + $hobbit[+7] + $hobbit[+8] +
                $hobbit[+9] + $hobbit[+10] + $hobbit[+11] + $hobbit[+12]};
            #ECHO {%s %s} {@dotfill{Insgesamt;20}} {$summe}
        }
    }
}

#ALIAS {hobb}
{
    #SEND {nimm jede bling aus $container[3]};
    #SEND {gib ahtawqa jede bling}
}

#ALIAS {ff}
{
    #SEND {leg jede bling in $container[3]}
}

#ACTION {Du gibst Ahtawqa %+.}
{
    #SWITCH {"%1"}
    {
        #CASE {"{.+Stueck.+}"}
        {
            #MATH hobbit[soeldner] {$hobbit[soeldner] + 1}
        };
        #CASE {"{.+Hauer.+}"}
        {
            #MATH hobbit[ork] {$hobbit[ork] + 1}
        };
        #CASE {"{.+Haar.+}"}
        {
            #MATH hobbit[kampfsoldat] {$hobbit[kampfsoldat] + 1}
        };
        #CASE {"{.+Fetzen.+}"}
        {
            #MATH hobbit[berserker] {$hobbit[berserker] + 1}
        };
        #CASE {"{.+(Punkt|Strich|Ring|Stern|Winkel|Kreuz|Lorbeerkrae?nz).*}"}
        {
            #MATH hobbit[ex-soldat] {$hobbit[ex-soldat] + 1}
        };
        #CASE {"{.+Barghestklaue}"}
        {
            #MATH hobbit[barghest] {$hobbit[barghest] + 1}
        };
        #CASE {"{.+Kralle}"}
        {
            #MATH hobbit[ghoul] {$hobbit[ghoul] + 1}
        };
        #CASE {"{.+Fleischklumpen}"}
        {
            #MATH hobbit[nekromantin] {$hobbit[nekromantin] + 1}
        };
        #CASE {"{.+Stamperl}"}
        {
            #MATH hobbit[zechpreller] {$hobbit[zechpreller] + 1}
        };
        #CASE {"{.+grauen Umhang}"}
        {
            #MATH hobbit[druide] {$hobbit[druide] + 1}
        };
        #CASE {"{.+Zacke}"}
        {
            #MATH hobbit[troll] {$hobbit[troll] + 1}
        };
        #CASE {"{.+Broeckchen Lehm}"}
        {
            #MATH hobbit[lehmgolem] {$hobbit[lehmgolem] + 1}
        }
    }
}

#HIGHLIGHT {Ahtawqa fluestert Dir immer wieder ein paar Kampftricks zu. Du meinst, dass} {bold}
#HIGHLIGHT {Dir das auf dem PFAD DES TODES helfen wird.} {bold}

#ALIAS {killer}
{
    #SWITCH {"%0"}
    {
        #CASE {"reset"}
        {
            #VAR killer
            {
                {Du}        {0}
                {Weingeist} {0}
            }
        };
        #DEFAULT
        {
            update_var killer q;
            #ECHO {Ihr habt bisher getoetet:};
            #ECHO {%n %s} {@dotfill{*killer[Du];20}} {$killer[Du]};
            #ECHO {%n %s} {@dotfill{*killer[Weingeist];20}} {$killer[Weingeist]};
        }
    }
}

#ALIAS {add_kill}
{
    #NOP %0;
    #SWITCH {"$angriff[killer]"}
    {
        #CASE {"Du"}
        {
            #MATH killer[Du] {$killer[Du] + 1}
        };
        #CASE {"{.+Weingeist}"}
        {
            #MATH killer[Weingeist] {$killer[Weingeist] + 1}
        }
    };
    #RETURN
}

#ACTION {Der brutale Kampfsoldat schreit noch einmal laut auf und stirbt.}
{
    add_kill kampfsoldat
}

#ACTION {Der Ork schreit auf und faellt dann tot zu Boden.}
{
    add_kill ork
}

#ACTION {Der %w%?Zwergenberserker faellt tot zu Boden.}
{
    add_kill berserker
}

#ACTION {Der Ex-%w faellt tot zu Boden.}
{
    add_kill ex-soldat
}

#ACTION {Der entlassene Soeldner bricht tot zusammen.}
{
    add_kill soeldner
}

#ALIAS {ll}
{
    #NOP /* @todo:  keine fixen charaktere, $gruppe[sammler] nutzen */;
    #{$gruppe[blingsammler]} pl;
    #DELAY 0.2 {#{$gruppe[blingsammler]} beu3 jedes bling};
    #DELAY 0.4 {#{$gruppe[sammler]} lei};
    #DELAY 0.6 {#{$gruppe[sammler]} bru}
}

#ALIAS {blingreg}
{
    nb3 jedes bling;
    reg jedes bling
}

#NOP #ALIAS {hl} {ll}

#ALIAS {pl}
{
    #NOP { /**
    *   @fn lei 
    *   @brief  mehrere Leichen pluendern
    */};

    #VAR leichenzahl 0;

    #ACTION {^{.+}$}
    {        
        #VAR leichenzahl @leichenzahl{%%0};
        #REGEXP {%%0} {{.*}.$}
        {
            #NOP #ECHO {$leichenzahl Leichen im Keller.};
            #VAR loop 0;
            #IF {$leichenzahl}
            {
                #LOOP 1 $leichenzahl loop
                {
                    #SEND {nimm jedes bling aus leiche $loop}
                }
            } {
                #ECHO {Keine Leichen zum Pluendern vorhanden.}
            };
            #UNACTION {^{.+}$}
        }
    };
    #SEND {schau alles in raum}
}

#CLASS schlachtfeld CLOSE

#NOP {
Ahtawqa sagt: Sehr gut! Toete mehr!!
Ahtawqa sagt: Weiter so, und Du wirst ein Held! Dann bekommst Du die
Belohnung!
#NOP }