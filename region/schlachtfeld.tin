#NOP { /**
 *  @class schlachtfeld
 *  @brief
 */}

#CLASS schlachtfeld KILL
#CLASS schlachtfeld OPEN

#NOP #ECHO {<029>-- <188>%s<288> geladen<099>} {region/schlachtfeld.tin}

#GAG {Die Ratte hopst umher.}
#GAG {Eine Ratte nagt an einem toten Koerperteil.}
#GAG {Es stinkt hier ekelhaft nach Toten.}
#GAG {Jemand stoehnt sterbend.}
#GAG {Ein Koerper zuckt!}
#GAG {Schreie, schmerzende Schreie aus dem Osten!}

#NOP /* Schlachtfeld */
#GAG {^Ein Windhauch.}
#GAG {^Schreie. Schmerzende Schreie aus dem Osten!}
#GAG {^Hier ist es langweilig.}
#GAG {^Du hoerst Gebruell aus dem Osten!}
#GAG {^Aus dem Burggraben steigt ein ekelhafter Gestank auf!}

#NOP /* Lehmgolem an Pfadbeginn */
#GAG {Der Lehmgolem ruehrt sich nicht vom Fleck.}
#GAG {Warum ist hier niemand mehr?}
#GAG {Der Lehmgolem verharrt regungslos.}
#GAG {Es ist hier bedrohlich still.}
#GAG {Der Lehmgolem ist dumm.}
#GAG {Ein Stueck Lehm broeckelt von dem Golem ab. Es scheint ihn nicht zu stoeren.}
#GAG {Du hoerst Todesschreie aus einiger Entfernung.}
#GAG {Schreie toenen aus dem Westen.}

#HIGHLIGHT {Ahtawqa.} {green}
#HIGHLIGHT {Ein Muellschlucker.} {jade}
#HIGHLIGHT {Eine grauslige Ratte.} {violet}
#HIGHLIGHT {Ein Opkac.} {light blue}

#SUBSTITUTE {Ein entlassener Soeldner.%s} {@order{%0}}
#SUBSTITUTE {Ein brutaler Kampfsoldat.%s} {@order{%0}}
#SUBSTITUTE {Ein Ork.%s} {@order{%0}}
#SUBSTITUTE {Ein Ex-%w.%s} {@order{%0}}
#SUBSTITUTE {Ein %w%?Zwergenberserker.%s} {@order{%0}}
#SUBSTITUTE {Ein %w%?Zwergenberserkermeister.%s} {@order{%0}}
#SUBSTITUTE {Ein Zwergenberserker{(?! geht)[^.]+[A-Z][a-z]+}.%s} {@order{%0}}
#SUBSTITUTE {Ein%? %w%?Bogenschuetz{e|in}.%s} {@order{%0}};

#NOP /* NPC: Ork */
#GAG {Der Ork grunzt.}
#GAG {Der Ork bruellt Dich wild an!!}
#GAG {Der Ork schreit wild im Kampf!}
#ACTION {Der Ork beisst %+ in die Schulter!} {
    setze_angriff_spell schulterbiss npc;
    #LINE GAG
}
#ACTION {Der Ork beisst %+.} {
    setze_angriff_spell biss npc;
    #LINE GAG
}
#ACTION {Das Blut spritzt, als des Orks Waffe in %+ eindringt!} {
    setze_angriff_spell {blut spritzt} npc;
    #LINE GAG
}

#NOP /* NPC: Zwergenberserker */
#GAG {Der %w%?Zwergenberserker GROOOOOAAAAAAAARRRRRRRRRTT!!!!!!!}
#GAG {Der %w%?Zwergenberserker flucht.}
#GAG {Der %w%?Zwergenberserker freut sich ueber die wilde Schlaegerei!}
#GAG {Der %w%?Zwergenberserker AAAAAAARRRRGGGHHHTTTTTTT.}
#ACTION {Der %w%?Zwergenberserker rammt %+ mit seiner Schulter.} {
    setze_angriff_spell rammen npc;
    #LINE GAG
}
#ACTION {Der %w%?Zwergenberserker wirft %+ zu Boden.} {
    setze_angriff_effekte 5 B;
    #LINE GAG
}
#ACTION {Der %w%?Zwergenberserker bruellt %+ an.} {
    setze_angriff_spell kampfschrei npc;
    #LINE GAG
}
#ACTION {Der %w%?Zwergenberserker zertritt %+ unter blossen Fuessen.} {
    #NOP setze_angriff_spell schulterbiss npc;
    #NOP #LINE GAG
}
#ACTION {Der %w%?Zwergenberserker tritt %+ in den Bauch.} {
    setze_angriff_spell bauchtritt npc;
    #LINE GAG
}
#ACTION {Der %w%?Zwergenberserker spaltet %+ mit seine%. %+.}
{
    setze_angriff_spell spalten npc;
    #LINE GAG
}
#ACTION {Mit einem maechtigen Rueckhandhieb schleudert %+}
{
    setze_angriff_spell rueckhandh. npc;
    #LINE GAG @line_gag_check{%1;\.};
}
#ACTION {Das Blut spritzt, als %+}
{
    setze_angriff_spell {blut spritzt} npc;
    #LINE GAG @line_gag_check{%1;\.};
}

#NOP {
Der grosse Zwergenberserker gibt dem Weingeist einen harten Kopfstoss.
Des grossen Zwergenberserkers Kopf trifft nur Luft.
Der grosse Zwergenberserker verletzt den Weingeist.
;
#NOP }

#NOP /* NPC: Kampfsoldat */
#ACTION {Der Kampfsoldat steigt %+ heftig auf den Fuss.} {
    setze_angriff_spell {Fuss steigen} npc;
    #LINE GAG
}
#ACTION {Der Kampfsoldat versetzt %+ einen schweren Schlag ins Genick.} {
    setze_angriff_spell genickschlag npc;
    #LINE GAG
}

#NOP /* NPC: Soeldner */
#ACTION {Der ehemalige Soeldner tritt %+ in den Bauch.} {
    setze_angriff_spell bauchtritt npc;
    #LINE GAG
}
#ACTION {Der ehemalige Soeldner schlaegt %+ fest in den Ruecken!} {
    setze_angriff_spell rueckenschl. npc;
    #LINE GAG
}

#NOP /* NPC: Ex-Garnisonssoldat */
#GAG {Der %+ schreit unerwartet laut auf! GRAAAAARGH!!}
#GAG {Der %+ verzieht sein Gesicht ernst.}
#GAG {Der %+ konzentriert sich auf den Kampf.}
#GAG {Der %+ grinst: HAHA, ENDLICH!}
#ACTION {Der %+ trifft %+ an einer empfindlichen Stelle!} {
    setze_angriff_spell {empf. Stelle} npc;
    #LINE GAG
}
#ACTION {Der %+ schlaegt %+ eine tiefe Fleischwunde in den Arm!!} {
    setze_angriff_spell fleischwunde npc;
    #LINE GAG
}
#ACTION {Der %+ schlaegt in einem Wutanfall wild um sich.} {
    setze_room_spell wutanfall npc;
    #LINE GAG
}

#NOP /* NPC: wuetende Bogenschuetzen */
#ACTION {D{er|ie} wuetende Bogenschuetz{e|in} trifft %+ mit einem schnellen %+} {
    setze_angriff_spell schuss npc;
    #LINE GAG @line_gag_check{%4;empfindlich\.};
} {4.9};

#ACTION {D{er|ie} wuetende Bogenschuetz{e|in} trifft %+ mit einem %+} {
    setze_angriff_spell giftpfeil npc;
    #LINE GAG @line_gag_check{%4;Giftpfeil\.};
} {5};

#NOP /* NPC: Opkac */
#ACTION {^Das Opkac schlabbert %+ ein!} {
    setze_angriff_spell schlabbern npc;
    #LINE GAG
} {5};

#ACTION {^Das Opkac spritzt Fluessigkeit durch den Raum.} {
    setze_room_spell fluessigkeit npc;
    #LINE GAG
} {5};

#NOP { /**
 *	@brief	Bling-Anzeige
 */};

#ALIAS {hobbit} {
    #SWITCH { "%0" } {
        #CASE { "reset" } {
            #CLASS ${mud}_vars ASSIGN {
                #VAR hobbit {
                    {soeldner}      {0}
                    {ork}           {0}
                    {kampfsoldat}   {0}
                    {berserker}     {0}
                    {ex-soldat}     {0}
                    {barghest}      {0}
                    {ghoul}         {0}
                    {troll}         {0}
                    {lehmgolem}     {0}
                    {druide}        {0}
                    {zechpreller}   {0}
                    {nekromantin}   {0}
                    {bogenschuetze} {0}
                }
            }
        };
        #DEFAULT {
            update_var;
            #ECHO {Das hast bisher erreicht:};
            #VAR summe 0;
            #FOREACH {*hobbit[]} {loop} {
                #ECHO {%n %s} {@dotfill{$loop;20}} {$hobbit[$loop]};
                #MATH summe { $summe + $hobbit[$loop]}
            };
            #ECHO {%s %s} {@dotfill{Insgesamt;20}} {$summe}
        }
    }
}

#ALIAS {hobb} {
    #SEND {nimm jede bling aus $container[3]};
    #SEND {gib ahtawqa jede bling}
}

#ALIAS {ff} {
    #SEND {leg jede bling in $container[3]}
}

#ACTION {Du gibst Ahtawqa %+.} {
    #SWITCH { "%1" } {
        #CASE { "{.+Stueck.+}" } {
            #MATH hobbit[soeldner] {$hobbit[soeldner] + 1}
        };
        #CASE { "{.+Hauer.+}" } {
            #MATH hobbit[ork] {$hobbit[ork] + 1}
        };
        #CASE { "{.+Haar.+}" } {
            #MATH hobbit[kampfsoldat] {$hobbit[kampfsoldat] + 1}
        };
        #CASE { "{.+Fetzen.+}" } {
            #MATH hobbit[berserker] {$hobbit[berserker] + 1}
        };
        #CASE { "{.+(Punkt|Strich|Ring|Stern|Winkel|Kreuz|Lorbeerkrae?nz).*}" } {
            #MATH hobbit[ex-soldat] {$hobbit[ex-soldat] + 1}
        };
        #CASE { "{.+Barghestklaue}" } {
            #MATH hobbit[barghest] {$hobbit[barghest] + 1}
        };
        #CASE { "{.+Kralle}" } {
            #MATH hobbit[ghoul] {$hobbit[ghoul] + 1}
        };
        #CASE { "{.+Fleischklumpen}" } {
            #MATH hobbit[nekromantin] {$hobbit[nekromantin] + 1}
        };
        #CASE { "{.+Stamperl}" } {
            #MATH hobbit[zechpreller] {$hobbit[zechpreller] + 1}
        };
        #CASE { "{.+grauen Umhang}" } {
            #MATH hobbit[druide] {$hobbit[druide] + 1}
        };
        #CASE { "{.+Zacke}" } {
            #MATH hobbit[troll] {$hobbit[troll] + 1}
        };
        #CASE { "{.+Broeckchen Lehm}" } {
            #MATH hobbit[lehmgolem] {$hobbit[lehmgolem] + 1}
        };
        #CASE { "{.+Pfeilspitze}" } {
            #MATH hobbit[bogenschuetze] {$hobbit[bogenschuetze] + 1}
        }
    }
}

#HIGHLIGHT {^Ahtawqa fluestert Dir immer wieder ein paar Kampftricks zu. Du meinst, dass} {bold}
#HIGHLIGHT {^Dir das auf dem PFAD DES TODES helfen wird.} {bold}

#ALIAS {killer} {
    #SWITCH { "%0" } {
        #CASE { "reset" }
        {
            #VAR killer {}
        };
        #DEFAULT {
            update_var;
            #ECHO {Ihr habt bisher getoetet:};
            #FOREACH {*killer[]} {item} {
                #ECHO {%n %s} {@dotfill{$item;20}} {$killer[$item]};    
            }
        }
    }
}

#ALIAS {add_kill} {
    #NOP %0;
    #IF { "$angriff[killer]" == "{.+Weingeist}" } {
        #IF { &killer[Weingeist] == 0 } {
            #VAR killer[Weingeist] 0
        };
        #MATH {killer[Weingeist]} {$killer[Weingeist] + 1};
    };
    #ELSE {
        #IF { &killer[$angriff[killer]] == 0 } {
            #VAR killer[$angriff[killer]] 0
        };
        #MATH {killer[$angriff[killer]]} {$killer[$angriff[killer]] + 1}
    }
}

#ACTION {^Der brutale Kampfsoldat schreit noch einmal laut auf und stirbt.} {
    add_kill kampfsoldat
}

#ACTION {^Der Ork schreit auf und faellt dann tot zu Boden.} {
    add_kill ork
}

#ACTION {^Der %w%?Zwergenberserker faellt tot zu Boden.} {
    add_kill berserker
}

#ACTION {^D{er|ie} %w%?Bogenschuetz{e|in} stirbt unaufgeregt.} {
    add_kill bogenschuetze
}

#ACTION {^Der Ex-%w faellt tot zu Boden.} {
    add_kill ex-soldat
}

#ACTION {^Der entlassene Soeldner bricht tot zusammen.} {
    add_kill soeldner
}

#ALIAS {blingreg} {
    nb3 jedes bling;
    reg jedes bling
}

#NOP { /**
*   @fn lei 
*   @brief  mehrere Leichen pluendern
*/};
#ALIAS {pl} {
    #VAR leichenzahl 0;

    #ACTION {^%+$} {        
        #VAR leichenzahl @leichenzahl{%%0};
        #REGEXP {%%0} {{.*}.$} {
            #NOP #ECHO {$leichenzahl Leichen im Keller.};
            #VAR loop 0;
            #IF { $leichenzahl } {
                #LOOP 1 $leichenzahl loop {
                    #SEND {nimm jedes bling aus leiche $loop}
                }
            } {
                #ECHO {Keine Leichen zum Pluendern vorhanden.}
            };
            #UNACTION {^%+$}
        }
    };
    #SEND {schau alles in raum}
}

#CLASS schlachtfeld CLOSE

#NOP {
Ahtawqa sagt: Sehr gut! Toete mehr!!
Ahtawqa sagt: Weiter so, und Du wirst ein Held! Dann bekommst Du die Belohnung!

Die wuetende Bogenschuetzin trifft Dich mit einem schnellen Schuss empfindlich.
Die wuetende Bogenschuetzin trifft Dich mit einem Giftpfeil.
;
#NOP }