#EVENT {PROGRAM START}
{
    #CLASS worlddata READ worlddata.tin;
    #IF {&{worldlist} == 0}
    {
        #CLASS worlddata OPEN;
        #LIST worldlist CLEAR;
        #CLASS worlddata CLOSE
    };
    connect;
    intro
}

#ALIAS {addworld}
{
    #IF {"%3" == ""}
    {
        #SHOWME {Syntax: addworld <name> <host> <port> [character name] [password]}
    };
    #ELSE
    {
        #VAR worldlist[%1][name] %1;
        #VAR worldlist[%1][host] %2;
        #VAR worldlist[%1][port] %3;
        #IF {"%4" != ""}
        {
            #VAR worldlist[%1][char] %4
        };
        #IF {"%5" != ""}
        {
            #VAR worldlist[%1][pass] %5
        };
        #IF {&worldlist[%1][char]}
        {
            #SHOWME {The world %1 (%2 %3) and the character %4 (%5) has been added.}
        };
        #ELSE
        {
            #SHOWME {The world %1 (%2 %3) has been added.}
        };
        #CLASS worlddata WRITE worlddata.tin;
        #CLASS %1 WRITE chars/%1.tin;
        #CLASS %1_vars WRITE chars/%1_vars.tin
    }
}

#ALIAS {deleteworld}
{
    #IF {&worldlist[%1] != 0}
    {
        #UNVAR worldlist[%1];
        #CLASS worlddata WRITE worlddata.tin;
        #SHOWME {The world %1 has been deleted.}
    };
    #ELSE
    {
        #SHOWME {The world %1 could not be found.}
    }
}

#ALIAS {connect}
{
    #IF {"%1" == ""}
    {
        #ECHO {%s} {====={ WORLD LIST }=====};
        #FOREACH {*worldlist[]} {world}
        {
            #ECHO {%-20s %s   %s   %s} {$worldlist[$world][name]} {$worldlist[$world][host]} {$worldlist[$world][port]} {$worldlist[$world][char]}
        }
    };
    #ELSEIF {&worldlist[%1] != 0}
    {
        #gts {#VAR mud {%1}};
        #SESSION $worldlist[%1][name] $worldlist[%1][host] $worldlist[%1][port]
    };
    #ELSE
    {
        #SHOWME {Could not find world %1.}
    }
}

#ALIAS {intro}
{
    #ECHO {};
    #ECHO {Du kannst mit 'addworld' eine neue Welt anlegen z.B.};
    #ECHO {  <199>addworld <Name> <Hostname> <Port> [Charakter Name] [Passwort]<099>};
    #ECHO {'Charaktername' und 'Passwort' sind optional, wuerden aber beim Einloggen};
    #ECHO {in die Welt automatisch uebertragen werden.};
    #ECHO {Mit 'deleteworld' kannst du eine Welt wieder aus dieser Liste entfernen:};
    #ECHO {  <199>deleteworld <Name><099>};
    #ECHO {Schliesslich verbindest Du Dich mit einer Welt mit 'connect':};
    #ECHO {  <199>connect <Name><099>};
    #ECHO {Nach dem Einloggen kannst du mit dem Befehl 'config' noch ein paar zusaetzliche};
    #ECHO {Einstellungen vornehmen.}
}

#EVENT {SESSION CONNECTED}
{
    #VAR mud @gts{$mud};
    #IF {&worldlist[$mud]}
    {
        #IF {&worldlist[$mud][char]}
        {
            #SEND {$worldlist[$mud][char]};
            #IF {&worldlist[$mud][pass]}
            {
                #SEND {$worldlist[$mud][pass]}
            }
        }
    };
    #READ startup.tin
}

