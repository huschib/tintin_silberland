#NOP { /**
 *  @class  items
 *  @brief  Befehle und Trigger fuer Gegenstaende
 */}

#CLASS items KILL
#CLASS items OPEN

#ECHO {<029>-- <188>%s<288> geladen<099>} {common/items.tin}

#VAR jadering 0
#VAR color_item_heal {<F796>}

#NOP { /**
 *  @brief Laenge des Countdowns fuer Halsband
 */};

#HIGHLIGHT {^Eine magische Aura huellt Dich ein, und gibt Dir wieder%+} {$color_item_heal};

#ACTION {^Eine magische Aura huellt Dich ein, und gibt Dir wieder} {
    #IF { $config[ultihalsband] == 1 } {
        halsband_func 420
    };
    #ELSE {
        halsband_func 480
    }
}

#NOP { /**
 *  @brief  Laenge des Countdowns selbststaendig durch Ultihalsband
 */};

#HIGHLIGHT {^Dein Halsband heilt Dich selbstaendig.} {$color_item_heal};

#ACTION {^Dein Halsband heilt Dich selbstaendig.} {
    halsband_func 210
}

#NOP { /**
 *  @brief  Setzt Countdown fuer Halsband
 *
 *  @param  1   Zahl
 */};

#ALIAS {halsband_func} {
    #VAR status[halsband] %0;
    #TICKER {status_halsband} {
        #MATH {status[halsband]} {$status[halsband] - 1};
        #IF { !$status[halsband] } {
            #VAR status[halsband] <199>HB!<099>;
            #UNTICKER {status_halsband}
        }
    } {1}
}

#NOP { /**
 *  @brief  Faerbung der Temperatur im Thermometer
 *
 *  Faerbt die Temperaturanzeige am Thermometer ein.
 *  Blau - kalt, Gruen - normal, Rot - heiss.
 */};

#ACTION {{Ein handliches Taschenthermometer. Es zeigt im Moment} %+ {Grad an, aber bei}} {
    #LOCAL therm_tmp {};
    #IF     {%2 > 40}   {#VAR therm_tmp <faa>};
    #ELSEIF {%2 > 35}   {#VAR therm_tmp <daa>};
    #ELSEIF {%2 > 30}   {#VAR therm_tmp <baa>};
    #ELSEIF {%2 > 25}   {#VAR therm_tmp <aba>};
    #ELSEIF {%2 > 20}   {#VAR therm_tmp <ada>};
    #ELSEIF {%2 > 15}   {#VAR therm_tmp <afa>};
    #ELSEIF {%2 > 10}   {#VAR therm_tmp <afa>};
    #ELSEIF {%2 > 5}    {#VAR therm_tmp <afc>};
    #ELSEIF {%2 > 0}    {#VAR therm_tmp <afe>};
    #ELSEIF {%2 > -5}   {#VAR therm_tmp <aef>};
    #ELSEIF {%2 > -10}  {#VAR therm_tmp <acf>};
    #ELSEIF {%2 > -15}  {#VAR therm_tmp <aaf>};
    #ELSEIF {%2 > -20}  {#VAR therm_tmp <aad>};
    #ELSE               {#VAR therm_tmp <aab>};
    #ECHO {%s %s%s<099> %s} {%1} {$therm_tmp} {%2} {%3};
    #LINE GAG
}

#NOP  #ALIAS {gring}  {ringe gerbring}
#NOP  #ALIAS {nring}  {ringe naehring}
#NOP  #ALIAS {zring}  {ringe zechring}
#NOP  #ALIAS {aring}  {ringe astralring}
#ALIAS {tas}    {aring}
#NOP  #ALIAS {ering}  {ringe metallring}
#NOP  #ALIAS {iring}  {ringe eisring}
#NOP  #ALIAS {wring}  {ringe wasserring}
#NOP  #ALIAS {lring}  {ringe luftring}
#NOP  #ALIAS {fring}  {ringe feuerring}
#NOP  #ALIAS {hring}  {ringe humusring}
#NOP  #ALIAS {mring}  {ringe meisterring}
#NOP  #ALIAS {xring}  {ringe hextonring}

#ALIAS {ehand} {handschuhe elementarhandschuhe}
#ALIAS {whand} {handschuhe goettersturm}
#ALIAS {ihand} {handschuhe faeustlinge}


#NOP { /**
 *	@brief	Waffen Sonderschlaege
 *
 *  @todo   auf Darstellungsart der Kampfmeldungen abfragen
 */};

#NOP { /**
 *  @brief  Keule: Lichtkeule
 */};

#ACTION {^Die Magie der Lichtkeule wendet sich gegen%+} {
    #LINE GAG;
    setze_angriff_effekte 3 L
}

#ACTION {^Die Lichtkeule strahl auf wie die Sonne.} {
    #LINE GAG;
    setze_angriff_effekte 3 l
}

#NOP { /**
 *  @brief  Peitsche: Tireksschwanz
 */};

#ACTION {^Deine Peitsche zischt laut durch die Luft und trifft %+ brutalst!} {
    #LINE GAG;
    setze_angriff_effekte 3 P
}

#NOP { /**
 *  @brief  Keule: Granitkeule
 */};

#ACTION {^Der Geist der Granitkeule will den Giganten vernichten!} {
    #LINE GAG;
    setze_angriff_effekte 3 G
}

#NOP { /**
 *  @brief  Dolch: Dolch der Nekromantenfee
 */};

#ACTION {^Du rammst wuetend Deinen Dolch in %+ Koerper!} {
    #LINE GAG;
    setze_angriff_effekte 3 D
}

#NOP {
Mit GEWALTIGER KRAFT stoesst Du den Dolch bis zum Griff in den Koerper des
Krieger der Xandims, drehst ihn wild umher und reisst ihn mit einem Ruck
wieder heraus!
;#NOP }

#NOP { /**
 *	@brief	Dolch: Wellenbeisser
 */};

#ACTION {^Eine gewaltige Flutwelle reisst %* zu Boden!} {
    #LINE GAG;
    setze_angriff_effekte 3 W
} {5};

#NOP { /**
 *	@brief	Dolch: giftiger Zombiedolch
 */};

#ACTION {^Gift spritzt aus der Klinge direkt auf %+!} {
    #LINE GAG;
    setze_angriff_effekte 3 G
} {5};

#NOP { /**
 *	@brief	Dolch: Mithrildolch
 */};

#ACTION {^Du schlitzt %+ den Bauch auf.} {
    #LINE GAG;
    setze_angriff_effekte 3 S
}

#NOP { /**
 *	@brief	Dolch: Yriskandurs Hoerner
 */};

#ACTION {^Yriskandurs Hoerner hinterlaesst eine zornige Lichtspur.} {
    #NOP ;
} {5};

#ACTION {^Die Macht des Guten und der Freundschaft durchfaehrt dich!} {
    #NOP ;
} {5};

#NOP { /**
 *	@brief	Sarnakmaske
 */};

#ACTION {%+ sieht Dich mit Deiner Sarnakmaske und%*} {
    setze_angriff_effekte 2 S;
    #LINE GAG @line_gag_check{%2;\.}
} {5.0}

#ACTION {^Eine schuetzende Aura bildet sich kurz um Thrak und vermindert den Schaden.} {
    setze_angriff_effekte 3 T;
    #LINE GAG;
} {5};

#NOP { /**
 *	@brief	Jadering-Meldungen fuer XP-Verlust im xplog
 *
 *  @todo   nach aktivem XP-Log abfragen
 */};

#ACTION {^Der Ring frisst gierig Deine Erfahrung und belohnt Dich!} {
    #ECHO {<bba>%0};
    #LINE GAG;
    #VAR jadering 1
}

#ACTION {^Ring und Waffe harmonieren, Erfahrung ist wertlos!} {
    #ECHO {<bba>%0};
    #LINE GAG;
    #VAR jadering 1
}

#ACTION {^Der Jadering labt sich an Deiner Erfahrung.} {
    #ECHO {<bba>%0};
    #LINE GAG;
    #VAR jadering 1
}

#HIGHLIGHT {^Der Jadering verbessert Deine Faehigkeiten nun staerker.} {<dda>}
#HIGHLIGHT {^Der Jadering verbessert Deine Faehigkeiten nun deutlich weniger.} {<Fd77>}

#SUBSTITUTE {Du spuerst etwas an Deinem Finger - der Jadering wird aktiv! Du findest einen} {@test_items{%0;Teil Deines gestorbenen Selbst wieder}}
#SUBSTITUTE {Nach diesem Fund fuehlst Du Deine Essenz wieder vollstaendig, fast so, als} {@test_items{%0;waerest Du nie gestorben}}
#FUNCTION {test_items}
{
    #LINE ONESHOT
    {
        #HIGHLIGHT {%+.$} {<dda>}
    };
    #RETURN {<dda>%1<099>}
}

#NOP { /**
 *	@brief	Sterbewaffen
 */};

#HIGHLIGHT {%+ duerstet nach Deinem Blut!$} {<baa>}

#NOP { /**
 *	@brief	Hammerseite des Kriegshammer des Sterbens wechseln
 */};

#ALIAS {ham} {
    #IF { &{hammer} == 0 } {
        #VAR hammer nichtriss
    };
    #ELSEIF { "$hammer" == "riss" } {
        #VAR hammer nichtriss
    };
    #ELSE {
        #VAR hammer riss
    };
    #SEND {schlage mit ${hammer}seite zu}
}

#NOP { /**
 *	@brief	Reisszaehne des Tireksschwanzes nachbestuecken
 *
 *  @todo   Kommando fuer das Nehmen aus Beutel generischer machen
 */};

#ALIAS {tir} {
    #LINE GAG 7;
    #LINE ONESHOT {
        #GAG {^>};
        #ACTION {Der Schwanz von Tireks ist mit %d Splitter bestueckt.} {
            #LINE GAG;
            #MATH splitter {50 - %%1};
            #IF { $splitter > 0 } {
                #ECHO {%%0};
                rues $splitter splitter;
                #SEND {bestuecke $splitter reisszaehne in tireksschwanz}
            };
            #ELSEIF { $splitter == 0 } {
                #ECHO {Der Tireksschwanz ist bereits voll bestueckt.}
            }
        }
    };
    #SEND {schau schwanz in mir};
    #SEND {schau loecher}
}

#HIGHLIGHT {^Du fuehlst Dich ploetzlich schneller!} {<dda>}

#NOP { /**
 *	@brief	Barden: Hymne
 */};

#HIGHLIGHT {^Durch den Mut der Gemeinschaft angefeuert wirst du schneller!} {$color_item_heal};
#HIGHLIGHT {^Das Licht der Gemeinschaft durchstroemt dich und deine Wunden heilen!} {$color_item_heal};
#HIGHLIGHT {^Das Licht der Gemeinschaft durchstroemt dich und dein Geist klaert sich!} {$color_item_heal};

#NOP { /**
 *	@brief	Barden: Lorica
 */};

#HIGHLIGHT {^Ein Teil der absorbierten Magie fliesst in dich!} {$color_item_heal};

#NOP { /**
 *	@brief	Barden: Inspiration
 */};

#HIGHLIGHT {^Du schoepfst neue Kraft aus den ermutigenden Worten von %+!} {$color_item_heal};

#HIGHLIGHT {%+ weckt neue Hoffnung und Kraft in dir!} {$color_item_heal};

#NOP { /**
 *	@brief	Schattensachen
 */};

#HIGHLIGHT {^Die Schatten%+ umstroem%+ Dein%+ kurz und intensiv.} {$color_item_heal};
#HIGHLIGHT {^Der Schattenstring erfrischt Deine magische Konzentration teilweise.} {$color_item_heal};

#NOP { /**
 *	@brief	Zeit seit der letzten Heilung der Schattenhandschuhe
 */};

#FUNCTION {schatten_zeit} {
    #IF { !&{schattenzeit} } {
        #FORMAT schattenzeit %T
    };
    #VAR schattenzeit_alt $schattenzeit;
    #FORMAT schattenzeit %T;
    #FORMAT result {%0 [%t]} {{%H:%M:%S}{$schattenzeit - $schattenzeit_alt - 3600}};
    #RETURN $result
}

#NOP { /**
 *	@brief	Primussachen
 */};

#HIGHLIGHT {^Die primaere Energie von einem Primusdolch heilt Dich.} {$color_item_heal};
#HIGHLIGHT {^Die primaere Macht erfuellt Dich!} {<499>$color_item_heal};
#HIGHLIGHT {^Die primaere Macht verlaesst Dich!} {<399>$color_item_heal};

#NOP { /**
 *	@brief	Astral Sachen
 */};

#HIGHLIGHT {^Die Magie des Sternenguertels heilt Dich.} {$color_item_heal};
#HIGHLIGHT {^Frisches Wasser sprudelt von der Wasserhose in Deinen Mund und heilt Dich.} {$color_item_heal};
#HIGHLIGHT {^Die Gletscherstiefel leuchten auf und heilen Dich.} {$color_item_heal};
#HIGHLIGHT {^Die vierlagigen Armschienen gluehen auf und heilen Dich!} {$color_item_heal};
#HIGHLIGHT {^Der %*Hextonring glueht auf und heilt Dich!} {$color_item_heal};
#HIGHLIGHT {^Die Golemhandschuhe huellen Dich in eine heilende Aura ein.} {$color_item_heal};
#HIGHLIGHT {Der Lavaumhang leuchtet auf und heilt Dich.} {$color_item_heal};

#NOP { /**
 *	@brief	Brunnensachen
 */};

#HIGHLIGHT {^Der Silmaril glueht auf.} {$color_item_heal};

#NOP { /**
 *	@brief	Goldener Jaguar
 */};

#HIGHLIGHT {^Die Macht des goldenen Jaguars heilt Dich!} {$color_item_heal};

#NOP { /**
 *	@brief	Rassengildensachen
 */};

#HIGHLIGHT {^%w %w blitz%w kurz auf.$} {$color_item_heal};

#NOP { /**
 *	@brief	Ansicht bei Loewenherz
 */};

#ACTION {^| Anzahl der erlegten Gegner%!s:%!s%d%!s|$} {
    #VAR loewenherz[anzahl] %1
}

#SUBSTITUTE {^| {Anzahl der Gegnerstufen\s+:}%!s%d%!s|$} {| %1@loewenherz_avg{%2;Stufen}};
#SUBSTITUTE {^| {Summe erkaempfter Fragmente\s+:}%!s%d%!s|$} {| %1@loewenherz_avg{%2;Fragmente}};

#FUNCTION loewenherz_avg {
    #MATH avg {1.00 * %1 / $loewenherz[anzahl]};
    #FORMAT {result} {%+13s %-25s |} {%1} {($avg %2/Gegner)}
}

#NOP { /**
 *	@brief	Seelenkristall
 */};

#SUBSTITUTE {^| %!{(Wasser|Luft|Eis|Feuer)elementarenergie:\s+}(%!s%d/%!s%d) |$} {%0 @frags_progress{%2;%3}};

#FUNCTION frags_progress {
    #FORMAT {result} {%+6m%%} {100 * 1.00 * %1 / %2}
}


#CLASS items CLOSE

#NOP { /**
Deine Kampfhandschuhe wehren den Angriff ab!
Die Macht von Zafiras Sturm frischt Deine Magiepunkte auf. (ab 5 Items)
Die Macht von Zafiras Flutwelle macht Dich durstiger. (ab 5 Items)
Die Macht von Zafiras Erde heilt Dich. (ab 5 Items)

Herschels einzigartiger Kriegshammer des Sterbens duerstet nach Deinem Blut!

Schwarze Nebelbande fliessen in Herschels einzigartiger Kriegshammer des
Sterbens, und er wird langlebiger!
Herschels einzigartiger Kriegshammer des Sterbens erstrahlt gestaerkt.

Herschels einzigartiger Kriegshammer des Sterbens erstrahlt in altem Glanze!
Herschels einzigartiger Kriegshammer des Sterbens normalisiert sich.

Der Ring frisst Deine Erfahrung mit immenser Gier! Beinahe haettest Du die
Kontrolle verloren.



Die Wirkung des Flaeschchens laesst nach und Du wirst wieder normal.
 */}
