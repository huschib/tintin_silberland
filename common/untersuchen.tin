#NOP { /**
 *  @class  untersuchen.tin
 *	@brief	Halbautomatisches Untersuchen von Raeumen.
 *
 *  @todo   reject/ignore Woerterbuch vervollstaendigen
 */};

#CLASS untersuchen KILL
#CLASS untersuchen OPEN

#ECHO {<029>-- <188>%s<288> geladen<099>} {common/untersuchen.tin}

#NOP { /**
 *	@brief	Liste mit ignorierten Begriffen, die zu keinem Detail fuehren.
 */}; 
#LIST reject CREATE
{
    Ein;Eine;Einer;Einen;Der;Die;Das;Den;Dem;Dessen;Er;Sie;Es;In;Im;An;Am;Auf;
    Neben;Unter;Ueber;Vor;Nach;Auch;Sowas;Vielleicht;Du;Dich;Dein;Deine;Deiner;
    Dir;Falls;Dort;Mit;Diese;So;Wenn;Wer;Wie;Was;Wessen;Wann;Darum;Jedenfalls;
    Kein;Da;Hoffentlich;Nur;Ihn;Ihnen;Ihr;Ihm;Von;Besonders;Hier;Einige;Davon;
    Offenbar;Weiter;Nicht;Warum;Allerdings;Vermutlich;Doch;Bis;Ausserdem;Eines;
    Als;Und;Oder;Ohne;Schon;Ausser;Schliesslich;Viele;Dann;Hinter;Zwischen;Aus;
    Waehrend;Deinen;Deinem;Welche;Aber;Eigentlich;Ab;Welches;Welcher;Irgendwie;
    Ja;Nein;Nunja;Nun;Fuer;Wo;Dieser;Dieses;Diesen;Zu;Um;Deines;Ihre;Immer;Wem;
    Waehrend;Man;Durch;Sonst;Ob;Trotzdem;Jetzt;Noch;Obwohl;Irgendwann;Irgendwo;
    Allmaehlich;Ganz;Hiermit;Damit;Dahinter;Davor;Tja;Darueber;Sehr;Leider;Na;
    Daneben;Beim;Bei;Dafuer;Wahrscheinlich;Zum;Dies;Ueberall;Alles;Naja;Hmm;
    Eben;Also;Genau;Besonderes;Jede;Jeder;Jedes;Wonach;Wirklich;Ach;Deswegen;
    Innerhalb;Alle;Des;Darunter;Solche;Hoechstens;Normalerweise;Dabei;Momentan;
    Seltsam;Scriptfalle;Erst;Etwa;Deshalb;Baeh;Seine;Pfui;Ungeheure;Sei;Dazu;
    Voller;Kaum;Denn;Solange;Fast;Sicher;Westlich;Oestlich;Noerdlich;Suedlich;
    Gleich;Anscheinend;Ansonsten;Direkt;Vom;Danach;Hu;Mu;La;Vo;Ri;Geh;Viel;Ohoh;
    Verschiedene;Herrlich;Grosse;Kleine;Natuerlich;Jedoch;Lauter;Unauffaellig;
    Beeindruckend;Hart;Wohin;Woher;Wozu;Manchmal;Wegen;Hin;Weit;Scheinbar;Egal;
    Ziemlich;Seltsamerweise;Wird;Je;Hm;Dadurch;Irgendwie;Ueblicherweise;Noe;Ne;
    Willst;Bist;Hauptsaechlich;Einsam;Echt;Ist;Ich;Zumindest;Sollte;Probiers;
    Meinst;Sieh;Nahe;Schrecklich;Seit;Stetig;Staendig;Kannst;Dauernd;Abnormal;
    Nachdem;Sobald;Daran;Sieht;Findest;Siehst;Ploetzlich;Daher;Uninteressant;
    Mehr;Nebenbei;Woanders;Boah;Solch;Inzwischen;Mittlerweile;Wieder;Einerseits;
    Darauf;War;Angeblich;Beinahe;Meist;Gehe;Andererseits;Schau;Derzeit;Einfach;
    Sowohl;Entweder;Musst;Teilweise;Gibts;Lass;Untersuch;Darin;Hohe;Aufgrund;He;
    Tatsaechlich;Endlich;Heisse;Wobei;sag;Vollkommen;Ui;Wieso;Jedesmal;Dass;
    Insbesondere;Denk;Zusammen;Soweit;Selber;Ok;Ah;Bislang;Einfache;Waere;Frag;
    Scheint;Wieviel;Wen;Lausch;Lausche;Riech;Rieche;Woah;Besser;Stimmt;Woran;
    Dazwischen;Waeren;Mir;Komisch;Ha;Oh;Ruhig;Geniess;Erstaunlich;Frisch;Kennst;
    Hatten;Koennte;Kann;Jaja;Richtig;Unglaublich;Welchen;Sollten;Waerst;Wirst;
    Leise;Dagegen;Dennoch;Haettest;Wahrlich;Vorsichtig;Such;Sogar;Sanft;Langsam;
    Woraus;Aengstlich;Selbst;Anstrengend;Dunkle;Lange;Unbeschreibliche;Wieviele;
    Irrsinnig;Wunderbare;Gross;Tief;Bestimmt;Uff;Gewaltige;Niedriges;Finster;
    Schauerlich;Dickfluessiger;Oerkz;Superviele;Undefinierbar;Panikartig;Heiss;
    Eiskalter;Gruselig;Grauenhaft;Ekelhafte;Manche;Blass;Kahler;Weiterhin;Purer;
    Glasklares;Groessere;Kratzige;Puuuuh;Harter;Etliche;Keineswegs;Weisst;Brrr;
    Tipp;Nanu;Inmitten;Seltsame;Panisch;Eindeutig;Gegen
}

#VAR reject_sentence 
{
    {1} {Dieses Detail wurde an einen Gnom vermietet}
    {2} {Hast Du jetzt nicht eigentlich was anderes zu tun}
    {3} {Du untersuchst aber auch wirklich alles, oder}
    {4} {Geh lieber metzeln, denn so wird das nichts}
    {5} {Gibts hier nicht, oder nicht in dieser Form}
    {6} {Sowas gibt es wirklich}
    {7} {Ist das wirklich wichtig}
    {8} {Lass das, das bringt doch so nichts}
    {9} {Sowas untersuchst Du}
    {10}{Untersuch doch nicht jeden Bloedsinn}
    {11}{Protheg war zu faul fuer dieses Detail}
}

#NOP { /**
 *	@brief	Startet den Untersuchenprozess mit einem $look[start]].
 */};
#ALIAS {look} {
	#VAR modus[untersuchen] %1;
    modus_setzen untersuchen;
    #LIST worte CLEAR;
    #LIST worte_untersucht CLEAR;
	#ACTION {{^.+$}} {
		worte_finden {%%0}
	} {9.9};
    #IF { "$modus[untersuchen]" == "{riech|lausch}" } {
        #ACTION {^>} {
            modus_setzen;
            #UNACTION {{^.+$}};
            #UNACTION {^>}
        } {1}
    };
    #DELAY {0.2} {
        sidepanel_redraw_tab $sidepanel[active]
    };
	$modus[untersuchen]
}

#ALIAS {modus_untersuchen} {
    checklist {Boden};

    #VAR terrain {};
    #MAP GET ROOMTERRAIN terrain;
    #IF {"$terrain" == "{.*hoehle|haus}"} {
        checklist {Decke};
        checklist {Waende};
        checklist {Wand}
    };
    #ELSEIF {"$terrain" == ""} {
        checklist {Decke};
        checklist {Waende};
        checklist {Wand};
        checklist {Himmel}
    };
    #ELSE {
        checklist {Himmel}
    };
    #UNACTION {{^.+$}};
    modus_setzen
}

#NOP { /**
 *	@brief  Prueft Liste nach einem Wort und haengt dieses bei Bedarf an.
 */};
#ALIAS {checklist} {
    #VAR tmp 0;
    #LIST worte FIND {%0} tmp;
    #IF { $tmp == 0 } {
        #LIST worte ADD {%0}
    }
}

#ALIAS {wort_untersuchen} {
	#ACTION {^{.+}$} {
		worte_finden %%0
	} {9.9};

    #LIST worte_untersucht ADD {%0};

    prompt_func prompt_untersuchen;

    #NOP {
	#ACTION {>} {
        sidepanel_show;
		#UNACTION {^{.+}$};
		#UNACTION {>}
	} {1};
    #NOP };
    
    #VAR tmp_cmd {$modus[untersuchen] %0};
    #ECHO {[%s]} {$tmp_cmd};
	#SEND {$tmp_cmd im Raum}
}

#ALIAS {prompt_untersuchen} {
    sidepanel_show;
    #UNACTION {^{.+}$};
    prompt_func stop
} {5};

#ALIAS {worte_finden} {
    #VAR tmp_find {%0};
    #REPLACE tmp_find {{.$}} {};
    #LIST reject_sentence FIND {$tmp_find} tmp;
    #IF { $tmp > 0 } {
        #RETURN
    };
	#REGEXP {%0} {{[A-Z][a-z]+}{.*}} {
		#IF { "&1" != "" } {
            #VAR tmp 0;
            #VAR tmp_reject 0;
			#LIST worte FIND &1 tmp;
			#LIST reject FIND &1 tmp_reject;
			#IF { $tmp == 0 && $tmp_reject == 0 } {
                #LIST worte ADD &1
            };
			worte_finden &2
		}
	}
}

#NOP { /**
 *	@brief  Einfaches Deklinieren von Substantiven in moeglichen Nominativ
 */};
#FUNCTION {deklination} {
    #IF { "%0" != "{.+[sen]}" } {   
        #RETURN %0
    };

    #IF { "%0" == "{.+ees$}" } {
        #VAR res %0;
        #REPLACE res {{.$}} {};
        #RETURN $res
    };

    #IF { "%0" == "{.+s}" } {
        #REGEXP {%0} {{.+}s$} {
            #REGEXP {&1} {{.+}e$} {
                #VAR res &&1
            } {
                #VAR res &1
            }
        };
        #RETURN $res
    };
    #REGEXP {%0} {{.+}n$} {
        #VAR res &1
    };
    #REGEXP {%0} {{.+}e$} {
        #VAR res &1
    };
    #RETURN $res
}

#CLASS untersuchen CLOSE
