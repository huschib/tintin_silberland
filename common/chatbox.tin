#CLASS chatbox KILL;
#CLASS chatbox OPEN;

#NOP { /**
 *	@brief	Chatbox rechts oben
 *
 *  @todo   Inhalt zwischen Logins speichern
 *  @todo   Inhalt mit Rechtsklick auf Button filterbar machen
 *  @todo   Buttons (Breite) dynamisch erstellen
 */}

#VAR chat_menu_tabs {
    {1} {
        {text}  {Kanaele}
        {show}  {chatbox_show 1}
        {var}   {chat_channel_list}
        {aktiv} {$line_color_active}
    }
    {2} {
        {text}  { Chats }
        {show}  {chatbox_show 2}
        {var}   {chat_tells_list}
        {aktiv} {$line_color_inactive}
    }
    {3} {
        {text}  {Gruppe}
        {show}  {chatbox_show 3}
        {var}   {chat_group_list}
        {aktiv} {$line_color_inactive}
    }
    {4} {
        {text}  {Moerder}
        {show}  {chatbox_show 4}
        {var}   {chat_moerder_list}
        {aktiv} {$line_color_inactive}
    }
    {5} {
        {text}  { Infos }
        {show}  {chat_infopanel}
        {var}   {info_list}
        {aktiv} {$line_color_inactive}
    }
};

#LIST ${chat_menu_tabs[1][var]} CREATE {};
#LIST ${chat_menu_tabs[2][var]} CREATE {};
#LIST ${chat_menu_tabs[3][var]} CREATE {};
#LIST ${chat_menu_tabs[4][var]} CREATE {};

#MATH chatbox_height {$screen[rows] + $sidepanel[top] -2};
#VAR chatbox_offset 0;

#ALIAS {draw_chat_menu} {
    #DRAW TEED LINE 1 -58 2 -58;
    #DRAW TEED LINE 2 -58 2 -56;
    #DRAW BOTTOM LEFT CORNER 2 -58 2 -58;
    #ECHO {{$chat_menu_tabs[1][aktiv]$chat_menu_tabs[1][text]<099>}{2}{-55}};
    #DRAW TEED LINE 2 -48 2 -46;
    #ECHO {{$chat_menu_tabs[2][aktiv]$chat_menu_tabs[2][text]<099>}{2}{-45}};
    #DRAW TEED LINE 2 -38 2 -36;
    #ECHO {{$chat_menu_tabs[3][aktiv]$chat_menu_tabs[3][text]<099>}{2}{-35}};
    #DRAW TEED LINE 2 -28 2 -26;
    #ECHO {{$chat_menu_tabs[4][aktiv]$chat_menu_tabs[4][text]<099>}{2}{-25}};
    #DRAW TEED LINE 2 -18 2 -16;
    #ECHO {{$chat_menu_tabs[5][aktiv]$chat_menu_tabs[5][text]<099>}{2}{-15}};
    #DRAW TEED LINE 2 -8 2 -6;
    #DRAW TEED LINE 1 -6 2 -6;
    #DRAW BOTTOM RIGHT CORNER 2 -6 2 -6;    
}

#ALIAS {chatbox_draw} {
    #IF {$screen[panels] != 3} {
        #UNBUTTON {2;-56;2;-48};
        #UNBUTTON {2;-46;2;-38};
        #UNBUTTON {2;-36;2;-28};
        #UNBUTTON {2;-26;2;-18};
        #UNBUTTON {2;-16;2;-8};
        #RETURN;
    };
    #MATH chatbox_height {$screen[rows] + $sidepanel[top] -2};

    #LIST chat_menu_tabs INDEXATE aktiv;
    #LIST chat_menu_tabs FIND {$line_color_active} index;

    chat_menu_button $index;

    #BUTTON {2;-56;2;-48} {
        #VAR chatbox_offset 0;
        chat_menu_button 1;
    } {5};
    #BUTTON {2;-46;2;-38} {
        #VAR chatbox_offset 0;
        chat_menu_button 2;
    } {5};
    #BUTTON {2;-36;2;-28} {
        #VAR chatbox_offset 0;
        chat_menu_button 3;
    } {5};
    #BUTTON {2;-26;2;-18} {
        #VAR chatbox_offset 0;
        chat_menu_button 4;
    } {5};
    #BUTTON {2;-16;2;-8} {
        chat_menu_button 5;
    } {5};} {5};

#ALIAS {chat_menu_button} {
    #FOREACH {*chat_menu_tabs[]} {loop} {
        #VAR chat_menu_tabs[$loop][aktiv] $line_color_inactive;
    };
    #VAR chat_menu_tabs[%1][aktiv] $line_color_active;
    draw_chat_menu;
    $chat_menu_tabs[%1][show];
} {5};

#ACTION {^[%!{(?!Tod|Moerder)}%+1..w:%+1..w%!{]?} %+} {
    chatbox_action 1 %1 %2 {%3};
} {5};

#ACTION {^%+1..w teilt Dir mit: %+} {
    chatbox_action 2 {von} %1 {%2};
} {5.1};

#ACTION {^Du teilst %+1..w mit: %+} {
    chatbox_action 2 {an } %1 {%2};
} {5.1};

#ACTION {^[Gruppe %+1..w:%+1..w%!{]?} %+} {
    chatbox_action 3 %1 %2 {%3};
} {5};

#ALIAS {chatbox_action} {
    #FORMAT chatbox_time {%T};
    #IF { %1 != 4
            && &{${chat_menu_tabs[%1][var]}[]} > 0
            && "$chatbox_time" == "${${chat_menu_tabs[%1][var]}[-1][time]}"
            && "%2" == "${${chat_menu_tabs[%1][var]}[-1][channel]}"
            && "%3" == "${${chat_menu_tabs[%1][var]}[-1][user]}" } {
        #VAR ${chat_menu_tabs[%1][var]}[-1][text] {${${chat_menu_tabs[%1][var]}[-1][text]} %4};
    };
    #ELSE {
        #LIST ${chat_menu_tabs[%1][var]} ADD {{{time}{$chatbox_time}{user}{%3}{channel}{%2}{text}{%4}}};
    };
    #IF { "$chat_menu_tabs[%1][aktiv]" == "$line_color_active" && $screen[panels] == 3} {
        $chat_menu_tabs[%1][show];
    };
} {5};

#ALIAS {chatbox_show} {
    #DRAW TILE 3 -59 -34 -1;
    #LIST chatbox_lines CREATE {};
    #FOREACH {${${chat_menu_tabs[%1][var]}[]}} {item} {
        #FORMAT {chatbox_time} {%t} {{%H:%M:%S}{$item[time]}};
        #FORMAT {chatbox_line} {%w} {{<$chatbox_time> <bcf>[$item[channel]:$item[user]]<099> $item[text]}{58}};
        #FOREACH {$chatbox_line[]} {loop} {
            #LIST chatbox_lines ADD {$loop}
        };
    };
    #VAR i 0;
    #FOREACH {$chatbox_lines[-${chatbox_height}-${chatbox_offset}..-1-${chatbox_offset}]} {loop} {
        #ECHO {{$loop} {3+$i} {-59}};
        #MATH i {$i + 1};
    };
} {5};

#ALIAS {chat_infopanel} {
    #NOP %0;
    #DRAW TILE 3 -59 -34 -1;
} {5};

chatbox_draw

#CLASS chatbox CLOSE;