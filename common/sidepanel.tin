#NOP { /**
 *  @class  sidepanel
 *	@brief	Buttons im rechten Panel
 */}

#CLASS sidepanel KILL
#CLASS sidepanel OPEN

#ECHO {<029> -> <188>%s<288> geladen<099>} {common/sidepanel.tin}

#VAR sidepanel
{
    {top}       {-33}
    {bottom}    {-1}
    {left}      {-59}
    {right}     {-1}
    {tabs}      {}
    {active}    {0}
}

#VAR PRESSED_BUTTON
{
    {ONE}   {PRESSED MOUSE BUTTON ONE}
    {TWO}   {PRESSED MOUSE BUTTON TWO}
    {THREE} {PRESSED MOUSE BUTTON THREE}
}

#VAR button
{
    {width}     {11}
    {height}    {3}
}

#VAR sidepanel_topmenu {}

#NOP { /**
 *  @fn     sidepanel_config
 *	@brief	Tabs im Sidepanel verwalten
 *
 *  @param  1   Befehl
 *  @param  2   Name des Tabs
 *
 *  @todo   Befehle delete und move hinzufuegen
 */}
#ALIAS {sidepanel_config}
{
    #IF {"%1" == "add"}
    {
        #LIST sidepanel_topmenu[tab] ADD { };
        #VAR sidepanel_topmenu[tab][-1][name] %2;
        #FORMAT tmp_len {%L} %2;
        #MATH sidepanel_topmenu[tab][-1][len] {$tmp_len + 3};
        #IF {&sidepanel_topmenu[tab][] > 1}
        {
            #MATH sidepanel_topmenu[tab][-1][left] {$sidepanel_topmenu[tab][-2][left] + $sidepanel_topmenu[tab][-2][len]};
        };
        #ELSE
        {
            #VAR sidepanel_topmenu[tab][-1][left] 2
        };
        #LIST sidepanel[tabs] ADD { };
        #VAR sidepanel[tabs][-1][name] %2
    }
}

#NOP { /**
 *  @fn     sidepanel_start
 *	@brief	allgemeine Startfunktion
 */}
#ALIAS {sidepanel_start}
{
    sidepanel_kill;
    
    sidepanel_config add untersuchen;
    sidepanel_config add mapping;
    sidepanel_config add gruppe;

    #IF {$screen[panels] > 2}
    {
        sidepanel_draw_menu active 1
    }
}

#NOP { /**
 *  @fn     sidepanel_show
 *	@brief	Sidepanel anzeigen wenn Layout drei Panels hat
 */}
#ALIAS {sidepanel_show}
{
    #IF {$screen[panels] != 3}
    {
        sidepanel_kill;
        #RETURN
    };

    sidepanel_draw_menu active $sidepanel[active]
}

#NOP { /**
 *  @fn     sidepanel_kill
 *	@brief	Aktives Tab und Menue entfernen
 */}
#ALIAS {sidepanel_kill}
{
    sidepanel_clear $sidepanel[active];
    #NOP #LIST sidepanel[tabs] CLEAR;
    #NOP #VAR sidepanel[active] 0;
    #NOP #DRAW TILE $sidepanel[top] $sidepanel[left] $sidepanel[bottom] $sidepanel[right];
    #NOP #VAR sidepanel_topmenu {};
    #CLASS sidepanel_topmenu CLEAR
}

#NOP { /**
 *  @fn     sidepanel_redraw_tab
 *	@brief	Tab neu zeichnen
 *
 *  @param  1   Nummer des Panels
 */}
#ALIAS {sidepanel_redraw_tab}
{
    sidepanel_clear %1;
    sidepanel_$sidepanel[tabs][%1][name]
}

#NOP { /**
 *  @fn     sidepanel_clear
 *	@brief	loescht die Flaeche des Tabs
 *
 *  @param  1   Tabnummer
 */}
#ALIAS {sidepanel_clear}
{
    #IF {$screen[panels] > 2}
    {
        #DRAW TILE $sidepanel[top]+3 $sidepanel[left] $sidepanel[bottom] $sidepanel[right]
    };
    #CLASS sidepanel_%1 CLEAR;
}

#NOP { /**
 *  @fn     sidepanel_draw_menu
 *	@brief	Menue fuer Tabs zeichnen
 *
 *  Zeichnet das Tabmenue fuer die bestehenden Tabs, dabei wird das aktive Tab besonders
 *  hervorgehoben.
 *
 *  @param  1   vorerst immer active
 *  @param  2   Tabnummer
 */}
#ALIAS {sidepanel_draw_menu}
{
    #VAR button_color_active {<G14>};
    #VAR line_color_active {<279>};
    #VAR button_color_inactive {<G04>};
    #VAR line_color_inactive {<g04>};

    #DRAW $line_color_inactive LINE $sidepanel[top]+2 $sidepanel[left] $sidepanel[top]+2 $sidepanel[right];

    #LOOP 1 &sidepanel_topmenu[tab][] loop
    {
        #DRAW $line_color_inactive BOX
            $sidepanel[top]
            $sidepanel[left]+$sidepanel_topmenu[tab][$loop][left]
            $sidepanel[top]+2
            $sidepanel[left]+$sidepanel_topmenu[tab][$loop][left]+$sidepanel_topmenu[tab][$loop][len]
            {<279> $sidepanel_topmenu[tab][$loop][name] <099>};
        #IF {$loop > 1}
        {
            #DRAW $line_color_inactive TOP TEED LINE $sidepanel[top] $sidepanel[left]+$sidepanel_topmenu[tab][$loop][left] $sidepanel[top]+2 $sidepanel[left]+$sidepanel_topmenu[tab][$loop][left]
        };


        #MATH button[left] {$sidepanel[left]+$sidepanel_topmenu[tab][$loop][left]};
        #MATH button[right] {$sidepanel[left]+$sidepanel_topmenu[tab][$loop][left]+$sidepanel_topmenu[tab][$loop][len]};
        #MATH button[top] {$sidepanel[top]};
        #MATH button[bottom] {$sidepanel[top]+2};

        #CLASS sidepanel_topmenu ASSIGN
        {
            #LINE SUB VAR
            {
                #BUTTON {$button[top];$button[left];$button[bottom];$button[right]}
                {sidepanel_draw_menu active $loop}
            }
        }
    };

    #IF {"%1" == "active"}
    {
        #DRAW $line_color_active BOX
            $sidepanel[top]
            $sidepanel[left]+$sidepanel_topmenu[tab][%2][left]
            $sidepanel[top]+2
            $sidepanel[left]+$sidepanel_topmenu[tab][%2][left]+$sidepanel_topmenu[tab][%2][len]
            {<279> $sidepanel_topmenu[tab][%2][name] <099>};

        #DRAW $line_color_active LEFT TEED LINE $sidepanel[top]+2 $sidepanel[left]-1 $sidepanel[top]+2 $sidepanel[left]+$sidepanel_topmenu[tab][%2][left]-1;
        #DRAW $line_color_active BOTTOM RIGHT CORNER $sidepanel[top]+2 $sidepanel[left]+$sidepanel_topmenu[tab][%2][left] $sidepanel[top]+2 $sidepanel[left]+$sidepanel_topmenu[tab][%2][left];
        #DRAW $line_color_active BOTTOM LEFT CORNER $sidepanel[top]+2 $sidepanel[left]+$sidepanel_topmenu[tab][%2][left]+$sidepanel_topmenu[tab][%2][len] $sidepanel[top]+2 $sidepanel[left]+$sidepanel_topmenu[tab][%2][left]+$sidepanel_topmenu[tab][%2][len];
        #DRAW $line_color_active LINE $sidepanel[top]+2 $sidepanel[left]+$sidepanel_topmenu[tab][%2][left]+$sidepanel_topmenu[tab][%2][len]+1 $sidepanel[top]+2 $sidepanel[right];
        #DRAW <209> LINE $sidepanel[top]+2 $sidepanel[left]+$sidepanel_topmenu[tab][%2][left]+1 $sidepanel[top]+2 $sidepanel[left]+$sidepanel_topmenu[tab][%2][left]+$sidepanel_topmenu[tab][%2][len]-1;

        #IF {@_an_aus{sidepanel[active]}}
        {
            sidepanel_clear $sidepanel[active]
        };
        #VAR sidepanel[active] %2;
        sidepanel_$sidepanel[tabs][%2][name]
    };

}

#NOP { /**
 *  @fn     sidepanel_untersuchen
 *	@brief	Untersuchen von Raeumen
 *
 *  Panel fuer ein Untersucheskript fuer Details in Raeumen.
 *  Moeglich sind Details, Geraeusche und Gerueche.
 */}
#ALIAS {sidepanel_untersuchen}
{
    #VAR unt[row] 0;
    #VAR unt[col] 0;
    #VAR unt[left] 0;

    #IF {&{worte} == 0}
    {
        #LIST worte CREATE
    };

    #FOREACH $worte[%*] loop
    {
        #VAR tmp_index 0;
        #LIST worte_untersucht FIND $loop tmp_index;
        #IF {$tmp_index}
        {
            #CONTINUE
        };
        #FORMAT unt[len] {%L} $loop;
        #MATH unt[col] {$unt[left] + $unt[len] + $sidepanel[left]};
        #IF {$unt[col] > 0}
        {
            #VAR unt[left] 0;
            #MATH unt[row] {$unt[row] + 2}
        };

        create_textbutton $loop 1 $unt[row] $unt[left] {wort_untersuchen $loop};
        create_textbutton $loop 1 $unt[row] $unt[left]
            {
                #LIST worte_untersucht ADD $loop
            }
            {$PRESSED_BUTTON[TWO]};
        create_textbutton $loop 1 $unt[row] $unt[left]
            {
                #LIST worte_untersucht ADD $loop;
                wort_untersuchen @deklination{$loop}
            }
            {$PRESSED_BUTTON[THREE]};

        #MATH unt[left] {$unt[left] + $unt[len] + 2}
    };

    #LIST liste_look_commands CREATE {schau;lausch;riech};
    create_button {Start}   {9} {1}
    {
        listbox {$liste_look_commands} {look}
    };

    create_button {Stop}    {9} {2}
    {
        #ECHO {Untersuchen abgebrochen.};
        modus_setzen;
        #VAR modus[untersuchen] {};
        #LIST worte CLEAR;
        sidepanel_show
    }
}

#NOP { /**
 *  @fn     sidepanel_mapping
 *	@brief	Mappinginformationen anzeigen
 *
 *  Panel fuer verschiedene Mappinginformationen
 */}
#ALIAS {sidepanel_mapping}
{
    #MAP GET ALL map_get;

    #VAR roominfo {area;color;desc;name;note;symbol;terrain;vnum;id};
    #VAR max_len 0;
    #FOREACH {$roominfo} loop
    {
        #FORMAT tmp %L $loop;
        #IF {$tmp > $max_len}
        {
            #VAR max_len $tmp
        }
    };

    #VAR i 0;
    #FOREACH {$roominfo} loop
    {
        #MATH i {$i + 1};
        #VAR tmp $map_get[$loop];
        #REPLACE {tmp} {<} {\<};
        #REPLACE {tmp} {>} {\>};
        #FORMAT tmp { %+${max_len}s : %s} {$loop} {$tmp};
        create_text {$tmp} $i 1
    }
}

#ALIAS {sidepanel_gruppe}
{
    #NOP
}

#NOP { /**
 *  @fn create_text
 *  @brief  erschafft Text im Sidepanel
 *
 *  Es wird ein Text erzeugt der relativ zum Urpsrung des Panels
 *  verschoben wird.
 *
 *  @param  1   Text
 *  @param  2   Reihen-Verschiebung innerhalb des Panels
 *  @param  3   Spalten-Verschiebung innerhalb des Panels
 */}
#ALIAS {create_text}
{
    #SHOWME {%1} {$sidepanel[top] + 3 + %2} {$sidepanel[left] + %3};
}

#NOP { /**
 *  @fn create_textbutton
 *  @brief  erschafft kleinen Button im Sidepanel
 *
 *  Es wird ein Button erzeugt der relativ zum Ursprung des Panels
 *  verschoben wird. Der Button besteht nur aus Text.
 *
 *  @param  1   Text fuer den Button
 *  @param  2   Tab in dem der Button erscheinen soll
 *  @param  3   Reihen-Verschiebung innerhalb des Panels
 *  @param  4   Spalten-Verschiebung innerhalb des Panels
 *  @param  5   auszufuehrende Funktion
 *  @oaram  6   genutzter Mausbutton (links als default)
 */}
#ALIAS {create_textbutton}
{
    #FORMAT button[text] {<g14>%s<099>} {%1};
    #FORMAT button[len] {%L} {$button[text]};

    #MATH button[top] {$sidepanel[top] + 3 + %3};
    #MATH button[left] {$sidepanel[left] + %4};
    #MATH button[right] {$button[left] + $button[len] - 1};
    #MATH button[bottom] {$button[top]};

    #FORMAT button[coord] {$button[top];$button[left];$button[bottom];$button[right]};

    #IF {"%6" != ""}
    {
        #VAR button[type] {;%6}
    };
    #ELSE
    {
        #VAR button[type] {}
    };

    #CLASS sidepanel_%2 ASSIGN
    {
        #LINE SUB VAR
        {
            #BUTTON {$button[coord]$button[type]}
            {
                #IF {"%5" == "{listbox.+}"}
                {
                    %5 {$button[coord]} {$button[text]}
                };
                #ELSE
                {
                    %5
                };
                kill_textbutton %2 {$button[coord]$button[type]}
            }
        };
    };
    #SHOWME {$button[text]} {$button[top]} {$button[left]}
}

#NOP { /**
 *  @fn     kill_textbutton
 *  @brief  entsorgt Textbutton im Sidepanel
 *
 *  Der Textbutton wird entfernt und das Panel neu gezeichnet.
 *
 *  @param  1   Panelnummer
 *  @param  2   ButtonID mit Koordinaten und Buttontype
 */}
#ALIAS {kill_textbutton}
{
    #VAR tmp_index {};

    #UNBUTTON {%2};

    sidepanel_redraw_tab %1
}

#NOP { /**
 *  @fn create_button
 *  @brief  erschafft Button im Sidepanel
 *
 *  Es wird ein Button erzeugt der relativ zum Urpsrung des Panels
 *  verschoben wird.
 *
 *  @param  1   Text fuer den Button
 *  @param  2   Tab in dem der Button erscheinen soll
 *  @param  3   Reihen-Verschiebung innerhalb des Panels
 *  @param  4   Spalten-Verschiebung innerhalb des Panels
 */}
#ALIAS {create_button}
{
    #MATH button[top] {$sidepanel[top] + $button[height] * %2};
    #MATH button[right] {$sidepanel[left] + $button[width] * %3 + 1};
    #MATH button[left] {$button[right] - $button[width] + 1};
    #MATH button[bottom] {$button[top] + $button[height] - 1};
    #FORMAT button[coord] {$button[top];$button[left];$button[bottom];$button[right]};

    #IF {$button[right] >= 0}
    {
        #RETURN
    };

    #FORMAT len {%L} {%1};
    #MATH tmp_maxlen {$button[width] - 4};
    #IF {$len > $tmp_maxlen}
    {
        #VAR len $tmp_maxlen
    };
    #MATH tmp_len {$button[width] - $len};
     
    #MATH button[pad_left] {$tmp_len / 2 - 1};
    #MATH button[pad_right] {$tmp_len / 2 + $tmp_len % 2 - 1};

    #IF {{%5} == {}}
    {
        #VAR button[number] ONE;
        #VAR button[type] {}
    };
    #ELSE
    {
        #REGEXP {%5} {{.+ ([A-Z]+)$}}
        {
            #VAR button[number] &2
        };
        #VAR button[type] {;%5}
    };

    #FORMAT button[text] {<G14><208>%+${button[pad_left]}s%.${len}s%+${button[pad_right]}s<099>} {} {%1} {};

    #CLASS sidepanel_$sidepanel[active] ASSIGN
    {
        #LINE SUB VAR
        {
            #BUTTON {$button[coord]$button[type]}
            {
                #IF {"%4" == "{listbox.+}"}
                {
                    %4 {$button[coord]} {$button[text]} {$button[number]}
                };
                #ELSE
                {
                    %4
                }
            }
        };
    };
    #REPLACE button[coord] {;} { };
    #DRAW <g14> BOX $button[coord] {$button[text]}
}

#NOP { /**
 *  @fn     listbox
 *  @brief  erschafft listbox zur Auswahl im Sidepanel
 *
 *  Es wird eine Listbox mit vorgegebenen Auswahlmoeglichkeiten erzeugt.
 *  Solange die Maustaste gehalten wird, koennen die Moeglichkeiten
 *  ausgewaehlt werden.
 *
 *  @param  1   Liste von Auswahlmoeglichkeiten
 *  @param  2   Vorangestellter Befehl
 *  @param  3   Koordinaten
 *  @param  4   Button Text vor Auswahl
 *  @param  5   Nummer der Maustaste, Standard links (ONE)
 */}
#ALIAS {listbox}
{
    #VAR tmp {%3};
    #REPLACE tmp { } {;};
    #LIST coord CREATE $tmp;
    #VAR tmp {%4};
    #MATH top {$coord[1] + 1};
    #MATH left {$coord[2] + 1};
    #NOP #MATH right {$coord[4] - 1};

    #IF {{%5} == {}}
    {
        #VAR button[number] ONE
    };
    #ELSE
    {
        #VAR button[number] %5
    };

    #REGEXP {$tmp} {{(<[A-Za-z0-9]{3}><[A-Za-z0-9]{3}>)(.+)}}
    {
        #VAR text_part {&3};
        #VAR old_color &2;
    };
    #SHOWME {<G04><g14>$text_part} {$top} {$left};

    #VAR inhalt {%1};
    #LIST inhalt SIZE size;

    #FORMAT len %L $text_part;
    #VAR max_len $len;
    #FOREACH {$inhalt[%*]} loop
    {
        #FORMAT len %L $loop;
        #IF {$len > $max_len}
        {
            #VAR max_len $len
        }
    };

    #MATH min_index {$top + 1};
    #MATH max_index {$top + $size};
    #VAR i $top;
    #MATH tmp {$left + $max_len};
    #IF {$tmp > 0}
    {
        #MATH left {0 - $max_len}
    };
    #FOREACH {$inhalt[%*]} loop
    {
        #MATH i {$i + 1};
        #FORMAT tmp {<G14><g04>%-${max_len}s<099>} {$loop};
        #SHOWME {$tmp} {$i} {$left};
    };
    #VAR old_row $min_index;
    #EVENT {MOVED MOUSE BUTTON $button[number]}
    {
        #IF {%%2 < $min_index || %%2 > $max_index}
        {
            #RETURN
        };
        #VAR new_row %%2;
        #MATH index {$new_row - $top};
        #FORMAT tmp {<G14><g04>%-${max_len}s<099>} {$inhalt[$old_index]};
        #SHOWME {$tmp} {$old_row} {$left};
        #FORMAT tmp {<G04><g14>%-${max_len}s<099>} {$inhalt[$index]};
        #SHOWME {$tmp} {$new_row} {$left};
        #VAR old_row $new_row;
        #VAR old_index $index;
    };
    #LINE SUB VAR
    {
        #EVENT {RELEASED MOUSE BUTTON $button[number]}
        {
            %2 $$inhalt[$$index];
            sidepanel_show;
            #UNEVENT {MOVED MOUSE BUTTON $button[number]};
            #UNEVENT {RELEASED MOUSE BUTTON $button[number]}
        }
    }
}

#NOP /* Beim Laden ausfuehren */

sidepanel_start

#CLASS sidepanel CLOSE
