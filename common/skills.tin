#NOP { /**
 *  @class  skills
 *	@brief	Befehle und Trigger zu Skills
 */}

#CLASS skills KILL
#CLASS skills OPEN

#ECHO {<029> -> <188>%s<288> geladen<099>} {common/skills.tin}

#HIGHLIGHT {^Deine Faehigkeit '{.+}' ist besser geworden!} {<eec>}
#HIGHLIGHT {^Du lernst aus Deinem Fehler.} {<eec>}
#HIGHLIGHT {^Du kannst Deine Fertigkeit '{.+}' nun nicht mehr weiter erhoehen.} {<eec> underscore}
#HIGHLIGHT {^Du hast Deine Fertigkeit '{.+}' verlernt!} {<eec> underscore}
#HIGHLIGHT {^Ab nun hast Du mehr Glueck!} {<eec>}

#HIGHLIGHT {Ein {?!Eis}%+1..wbaer.} {<bde>}
#HIGHLIGHT {Ein %w%s%+1..wfuchs.} {<bde>}
#HIGHLIGHT {Eine %w%sWaldspinne.} {<bde>}
#HIGHLIGHT {Ein%w %w%sWaldloew%w.} {<bde>}
#HIGHLIGHT {Eine %w%sHuegelkatze.} {<bde>}
#HIGHLIGHT {Ein %w%sGoldmarder.} {<bde>}
#HIGHLIGHT {Ein Biber.} {<bde>}
#HIGHLIGHT {Ein %wfrosch.} {<afb>}

#ACTION {^Frisch erholt erhebst Du Dich.$}
{
    #NOP { /**
     *	@brief	Zeitpunkt fuer naechstes Rasten
     */};
    #FORMAT {resttime} {%T} {}
}

#SUBSTITUTE {^Du hast Dich doch erst unlaengst ausgeruht!$} {%0 (noch @resting{} Sekunden)}

#FUNCTION {resting}
{
    #NOP { /**
     *	@brief	Sekunden bis zur naechsten Rastmoeglichkeit
     */};
    #FORMAT {time} {%T} {};
    #MATH result {300 - ($time - $resttime)}
}

#ALIAS {juwelier}
{
    #NOP { /**
     *	@brief	Edelsteine und Perlen aus Container nehmen
     *          von Juwelenmusterseite ausgehend
     */};

    #REGEXP {%0} {{[0-9]+} {.+}}
    {
        #VAR arg1 &1;
        #VAR arg2 &2
    }
    {
        #ECHO {%s} {Benutzung: juwelier <Musterseite> <Container>};
        #RETURN
    };
    #VAR linecount 3;
    #LIST material CREATE {};
    #ACTION {{[0-9]+}. Ei{n|ne} {gold|platin}en{e|er|es} {[^ ]+}{ring|kette|amulett}}
    {
        #LIST material ADD %%5
    };
    #ACTION {^{-+}$}
    {
        #MATH {linecount} {$linecount - 1};
        #IF {!$linecount} {
            #LIST material SIZE matsize;
            #VAR command nimm;
            #LOOP 1 $matsize loop
            {
                #LIST material GET $loop tmp;
                #REGEXP {$tmp} {perlen}
                {
                    #NOP ;
                }
                {
                    #SWITCH {"$tmp"} {
                        #CASE {"Lapislazuli"}
                        {
                            #FORMAT {tmp} {%ss} {$tmp}
                        };
                        #CASE {"Dioptas"}
                        {
                            #FORMAT {tmp} {%sse} {$tmp}
                        };
                        #CASE {"Jaspis"}
                        {
                            #FORMAT {tmp} {%sse} {$tmp}
                        };
                        #CASE {"Tigerauge"}
                        {
                            #FORMAT {tmp} {%sn} {$tmp}
                        };
                        #CASE {"Diamant"}
                        {
                            #FORMAT {tmp} {%sen} {$tmp}
                        };
                        #DEFAULT {
                            #FORMAT {tmp} {%se} {$tmp}
                        }
                    }
                };
                #FORMAT command {%s alle %s} {$command} {$tmp};
                #IF {$loop < $matsize}
                {
                    #FORMAT {command} {%s und} {$command}
                };
            };
            #SEND {$command aus $arg2};
            #UNACTION {{[0-9]+}. Ei{n|ne} {gold|platin}en{e|er|es} {[^ ]+}{ring|kette|amulett}};
            #UNACTION {^{-+}$}
        }
    };
    #SEND {jm $arg1}
}

add_help {juwelier} {skills}
    {Nimmt alle noetigen Materialien aus einem Container.}
    {juwelier <Musterseite> <Container>}

#NOP { /**
 *	@brief	Anzahl von gleichen Fellen gerben
 *
 *  @todo   Ersetzen von langen Fellnamen
 */};
#ALIAS {gerb} {
    #IF {"%0" == ""} {
        #ECHO {%s} {Benutzung: gerb <Anzahl> <Name>};
        #RETURN
    };

    #REGEXP {%0} {{([0-9]+) (.+)}} {
        #VAR arg1 {&2};
        #VAR arg2 {&3}
    } {
        #VAR arg1 {1};
        #VAR arg2 {%0}
    };
    #LOOP 1 $arg1 loop {
        #FORMAT sendtext {gerbe %sfell %s} {$arg2} {$loop};
        #SEND {$sendtext}
    }
}

add_help {gerb} {skills}
    {Gerbt ein oder mehrere Felle. Name ohne 'fell' angeben.}
    {gerb [Anzahl] <Name>}

#NOP { /**
 *	@brief	Pflanzen finden im Wald
 */};

#ACTION {^Du findest eine seltene Pflanze, die Du sofort pflueckst. Es handelt sich} {
    #LINE MULTISHOT 2 {
        #HIGHLIGHT {%+} {<bcb>}
    }
}

#ACTION {^Du findest hier ein seltsame Pflanze, aber da Du sie nicht kennst, wendest Du} {
    #LINE MULTISHOT 2 {
        #HIGHLIGHT {%+} {<cbb>}
    }
} {5};

#NOP { /**
 *	@brief	Schuerfen / Graben
 */};

#ALIAS {grab} {
    #LOCAL grab_richtung %1;
    #SWITCH { "$grab_richtung" } {
        #CASE { "n" } {
            #LOCAL {grab_richtung} {norden};
        };
        #CASE { "no" } {
            #LOCAL {grab_richtung} {nordosten};
        };
        #CASE { "o" } {
            #LOCAL {grab_richtung} {osten};
        };
        #CASE { "so" } {
            #LOCAL {grab_richtung} {suedosten};
        };
        #CASE { "s" } {
            #LOCAL {grab_richtung} {sueden};
        };
        #CASE { "sw" } {
            #LOCAL {grab_richtung} {suedwesten};
        };
        #CASE { "w" } {
            #LOCAL {grab_richtung} {westen};
        };
        #CASE { "nw" } {
            #LOCAL {grab_richtung} {nordwesten};
        };
        #DEFAULT {
            #SEND {grab %0};
            #RETURN;
        };
    };
    #SEND {grabe weg nach $grab_richtung frei}
} {5};

#NOP { /**
 *	@brief	Umwandlung der Anzeige der Skill-Fertigkeiten
 *
 *  #actions in eigene Klasse ausgelagert, die bei Bedarf geladen wird
 */};

#CLASS skill_fertigkeiten OPEN

#ACTION {^  %+ beherrscht Ihr %+.} {
    #ECHO {%s %s} {@dotfill{<ffd>%1<099>;40}} {@skill_fert{%2}<099>};
    #LINE GAG
} {5.0}

#ACTION {^Yuknok, das Orakel sagt: Insgesamt beherrscht Ihr diese %+.} {
    #ECHO {%s %s.} {<cff>Yuknok, das Orakel sagt:<099> Insgesamt beherrscht Ihr diese} {@skill_fert{%1}<099>};
    #LINE GAG
} {4.9}

#CLASS skill_fertigkeiten SAVE
#CLASS skill_fertigkeiten CLEAR
#CLASS skill_fertigkeiten CLOSE

#FUNCTION skill_fert {
    #LIST sfort FIND {%0} tmp;
    #IF {$tmp < 15} {
        #VAR tmp_color {<bba>}
    };
    #ELSEIF {$tmp < 19} {
        #VAR tmp_color {<bca>}
    };
    #ELSEIF {$tmp < 22} {
        #VAR tmp_color {<bdb>}
    };
    #ELSEIF {$tmp < 24} {
        #VAR tmp_color {<dea>}
    };
    #ELSEIF {$tmp < 26} {
        #VAR tmp_color {<bfa>}
    };
    #ELSE {
        #VAR tmp_color {<dfc><388>}
    };
    #RETURN ${tmp_color}%0
}

#ACTION {Du fragst Yuknok, das Orakel nach %wertigkeiten.} {
    #CLASS skill_fertigkeiten LOAD;
} {5};

#ACTION {Yuknok, das Orakel versinkt wieder in Meditation.} {
    #CLASS skill_fertigkeiten CLEAR;
} {5};

#VAR sfort {
    {1} {ueberhaupt nicht}
    {2} {gar nicht}
    {3} {fast gar nicht}
    {4} {sehr miserabel}
    {5} {ziemlich schlecht}
    {6} {schlecht}
    {7} {maessig}
    {8} {fast schon unterdurchschnittlich}
    {9} {unterdurchschnittlich}
    {10}{fast schon durchschnittlich}
    {11}{durchschnittlich}
    {12}{fast schon ueberdurchschnittlich}
    {13}{ueberdurchschnittlich}
    {14}{fast schon gut}
    {15}{gut}
    {16}{wirklich gut}
    {17}{fast schon sehr gut}
    {18}{sehr gut}
    {19}{unglaublich gut}
    {20}{phantastisch gut}
    {21}{ausgezeichnet}
    {22}{hervorragend}
    {23}{meisterhaft}
    {24}{nahezu perfekt}
    {25}{perfekt}
    {26}{absolut perfekt}
}

#NOP { /**
 *  @brief  Zeigt Stufen fuer den Skill-Fortschritt
 *
 */};

#ALIAS {sfort} {
    #VAR tmp_count {1};
    #SCREEN GET {SCROLL_COLS} {tmp_width};
    #VAR tmp_line {};
    #WHILE {$tmp_count <= &sfort[] } {
        #FORMAT {tmp_line_len} {%L} {$tmp_line};
        #FORMAT {tmp_item_len} {%L} {$sfort[$tmp_count]};
        #MATH {tmp_total_len} {$tmp_line_len + $tmp_item_len + 5};

        #VAR tmp_item @skill_fert{$sfort[$tmp_count]};

        #IF {$tmp_total_len > $tmp_width} {
            #ECHO {$tmp_line<099>};
            #VAR tmp_line {$tmp_item};
        };
        #ELSEIF {"$tmp_line" == ""} {
            #VAR tmp_line {$tmp_item};
        };
        #ELSE {
            #CAT tmp_line {     $tmp_item}
        };
        inc tmp_count
    };
    #ECHO {$tmp_line<099>}
} {5};

add_help {sfort} {skills}
    {Zeigt eine Übersicht der einzelnen Stufen im Skill-Fortschritt.}
    {sfort}



#CLASS skills CLOSE

#NOP {
Du findest eine seltene Pflanze, die Du sofort pflueckst. Es handelt sich
dabei um einen Beifuss.

Dieses Wissen hilft dir gerade nicht, da du schon etwas aehnliches im Kopf
hast.

Das Buch 'Edelsteinschätze Enthüllt' ist so zerlesen, dass es in alle
Einzelteile zerfaellt.

Deine Faehigkeiten 'Schuerfen', sind nun eine zeitlang besser.

Deine Faehigkeit(en) 'Graben', sind nun nicht mehr verbessert.

Durch die Erlebnisse des Bergmanns verstehst du nun mehr vom Bergbau.
Du lernst etwas ueber 'Graben' dazu!

Ein wohliges Gefuehl goettlicher Erhabenheit durchflutet dich. Ehre sei Dusty!

Dustys wohlwollender Blick wendet sich von dir ab.
;
#NOP }