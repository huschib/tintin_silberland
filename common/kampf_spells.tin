#NOP { /**
 *  @class  kampf_spells
 *	@brief	Abkuerzungen diverser Kampfspells
 */};

#CLASS kampf_spells KILL
#CLASS kampf_spells OPEN

#ECHO {<029>-- <188>%s<288> geladen<099>} {common/kampf_spells.tin}

#NOP /* Weingeist */
#ACTION {Ein{ ?[a-z]*} Weingeist haucht {.+}} {
    setze_angriff_spell anhauchen helfer;
    #LINE GAG;
    #IF {"%2" != "{.+entgegen!}" } {
        #ACTION {{.*entgegen!}} {
            #LINE GAG;
            #UNACTION {{.*entgegen!}}
        }
    }
}

#ACTION {{.+ haucht .+ an\. Pfui, das stinkt!}} {
    setze_angriff_spell anhauchen helfer;
    #LINE GAG
}

#ACTION {Der Weingeist feuert einen Strahl aus Alkohol auf {.+}} {
    setze_angriff_spell alkstrahl helfer;
    #LINE GAG
}

#ACTION {{BOEEEEERP! .+ ruelpst (.+)}} {
    setze_angriff_spell ruelpser helfer;
    #LINE gag;
    #IF {"%2" != "{.+stinkt!}"} {
        #ACTION {{.*stinkt!}} {
            #LINE GAG;
            #UNACTION {{.*stinkt!}}
        }
    }
}

#NOP /* Mumie */
#ACTION {Die Mumie bohrt ihre Finger in {.+}}
{
    setze_angriff_spell finger helfer;
    #LINE GAG
}

#ACTION {Die Mumie bespritzt {.+} mit Leichengift.}
{
    setze_angriff_spell leichengift helfer;
    #LINE GAG
}

#ACTION {Die Mumie wirft eine Saeurekugel nach {.+}}
{
    setze_angriff_spell saeurekugel helfer;
    #LINE GAG
}

#ACTION {{.+} wirft einen unheiligen Zauber nach {.+}.}
{
    setze_angriff_spell {unhl. Zauber} helfer;
    #LINE GAG
}

#NOP /* Vampir */
#ACTION {Der Vampir zerreisst {.+} Kehle.} {
    setze_angriff_spell kehle helfer;
    #LINE GAG
}

#ACTION {Der Vampir saugt {.+} Blut ab.}
{
    setze_angriff_spell {Blut saugen} helfer;
    #LINE GAG;
    #LINE ONESHOT
    {
        #GAG {Dein Vampir teilt das abgesaugte Blut mit Dir.}
    }
}


#NOP /* Zechpreller bei Gruppenkampf */
#ACTION {%+ Gesicht wird rot vor Konzentration.} {
    setze_angriff_spell alkoholisier;
    #LINE GAG
}

#GAG {{^.+ wird ganz gruen im Gesicht.}}

#ACTION {%+ scheint ploetzlich betrunkener.} {
    #LINE GAG;
    setze_angriff_effekte 5 K
}

#ACTION {%+ wankt aus der Reichweite der Attacke!} {
    #LINE GAG;
    setze_angriff_effekte 1 A
}

#ACTION {%+ faellt zu Boden.$}
{
    #NOP { /**
     *  @brief  Spelleffekt durch Rumkugeln
     */};

    setze_angriff_effekte 5 B;
    #LINE GAG
}

#NOP /* Druiden bei Gruppenkampf */
#ACTION {{.+} feuert einen elementaren {\w+pfeil} auf {.+} ab.}
{
    setze_angriff_spell %2;
    #LINE GAG
}

#ACTION {{Der|Die|Das} {\w+} von {.+} {wirbelt|prasselt|huellt|bricht|schuettelt} {.+} {herum|nieder|ein|herein|durch}!}
{
    setze_angriff_spell %2;
    #LINE GAG
}

#ACTION {{.+} konzentriert sich und feuert einen elementaren Windstoss auf{.*}}
{
    setze_angriff_spell windstoss spieler;
    #LINE GAG;
    #IF {"%2" != "{.+\.}"}
    {
        #ACTION {{.*\.}}
        {
            #LINE GAG;
            #UNACTION {{.*\.}}
        }
    }
}

#ACTION {{.+} feuert einen elementaren Strahl fluessigen Erzes auf{.*}}
{
    setze_angriff_spell erzstrahl spieler;
    #LINE GAG;
    #IF {"%2" != "{.+\.}"}
    {
        #ACTION {{.*\.}}
        {
            #LINE GAG;
            #UNACTION {{.*\.}}
        }
    }
}

#ACTION {{.+} wirft einen elementaren Feuerball nach{.*}}
{
    setze_angriff_spell feuerball spieler;
    #LINE GAG;
    #IF {"%2" != "{.+\.}"}
    {
        #ACTION {{.*\.}}
        {
            #LINE GAG;
            #UNACTION {{.*\.}}
        }
    }
}

#ACTION {{.+} konzentriert sich und ein Strahl elementar reinen Wassers schiesst auf{.*}}
{
    setze_angriff_spell wasserstrahl spieler;
    #LINE GAG;
    #IF {"%2" != "{.+\.}"}
    {
        #ACTION {{.*\.}}
        {
            #LINE GAG;
            #UNACTION {{.*\.}}
        }
    }
}

#ACTION {{.+} feuert eine Lanze aus elementaren Eis auf{.*}}
{
    setze_angriff_spell froststrahl spieler;
    #LINE GAG;
    #IF {"%2" != "{.+\.}"}
    {
        #ACTION {{.*\.}}
        {
            #LINE GAG;
            #UNACTION {{.*\.}}
        }
    }
}

#NOP /* Paladine */
#ACTION {{.+} ruft: DUSTY! Erhoere mich und verbanne}
{
    #LINE GAG 2
}
#ACTION {Die Macht Dustys senkt sich auf {.+} und loest{.*}}
{
    setze_angriff_spell verbannung spieler;
    #LINE GAG;
    #IF {"%2" != "{.+!}"}
    {
        #LINE ONESHOT
        {
            #GAG {{.+!}}
        }
    }
}

#GAG {%+ ruft:  Deine Hand fuehre mein Schwert gegen die Ausgeburt der Unterwelt!}
#GAG {%+ ruft: Deine goettliche Kraft lasse mich diesen Kampf bestehen!}
#GAG {%+ ruft: Dein Wille fuehre mich zum Sieg!}
#GAG {%+ ruft voller Inbrunst: FUER DUSTY!}
#HIGHLIGHT {%+ bekommt einen durchdringenden Blick!}

#ACTION {%+ nimmt %+ Schild fester in die Hand!}
{
    #LINE GAG 2
}
#ACTION {%+ pariert die Attacke%* mit %+ Schild!}
{
    #LINE GAG;
    setze_angriff_effekte 1 S
}

#GAG {%+ ruft: DUSTY STEH MIR BEI!!}
#GAG {^%+1..w attackiert %+!$}
#ACTION {Die Waffe von %+ dringt mit goettlicher Wucht in %+ ein!!}
{
    setze_angriff_spell zielattacke spieler;
    #LINE GAG
}

#ACTION {%+ attackiert mit {ihrem|seinem} Schild.}
{
    #LINE GAG;
    setze_angriff_effekte 3 S
}

#NOP { /**
 *	@brief	Schadensmeldungen von 'Kopfstoss' in Kampfausgabe integrieren
 *
 */};
#ALIAS {spezial_schaden_analyse}
{
    #VAR angreifer      @name_kuerzen{%3};
    #VAR opfer          @name_kuerzen{%4};
    setze_angriff_spell {%1};
    #VAR angriff[art]   %2;
    #VAR angriff[hoehe] %5;

    #FORMAT {%1_schadenausgabe} {%s%s|%s%s %s%s%s %.5s %s%s}
        {@kampf_pad{9;1;l;$angriff[art]}}
        {$angriff[art]}
        {$angriff[spell]}
        {@kampf_pad{12;1;r;$angriff[spell]}}
        {$angreifer}
        {@kampf_pad{39;2;m;$angreifer;$opfer}}
        {$opfer}
        {@zeige_angriff_effekte{$angriff[effekte]}}
        {@kampf_pad{11;1;l;$angriff[hoehe]}}
        {$angriff[hoehe]};

    #UNVAR angriff;
    #VAR angriff[killer] Du;
    setze_angriff_spell;
    setze_angriff_effekte;
    #VAR roomspell {};

    #UNTICKER kampf_ticker;
    #TICKER kampf_ticker {kampf_ticker} {4};

    #LINE GAG
}

#NOP { /**
 *	@brief	Kopfstoss
 *
 *  Ursula gibt dem Zwergenwaechter einen harten Kopfstoss.
 *    Ursula verletzt den Zwergenwaechter sehr schwer.     
 */};
#ALIAS {kopfstoss_analyse} {
    spezial_schaden_analyse {kopfstoss} {Schlag} %0
}

#ACTION {%+ gibt %+ einen harten Kopfstoss.$} {
    #NOP #LINE GAG @line_gag_check{%2;\.};
    #LINE GAG
} {5};

#ACTION {^  %+ streif%?t %+.} {
    kopfstoss_analyse {%1} {%2} {<dbc>streift<099>};
    #ECHO {$kopfstoss_schadenausgabe}
}

#ACTION {^  %+ verletz%!?t %+.} {
    kopfstoss_analyse {%1} {%2} {<dbc>verletzt<099>};
    #ECHO {$kopfstoss_schadenausgabe}
} {5.2}

#ACTION {^  %+ verletz%!?t %+ schwer.} {
    kopfstoss_analyse {%1} {%2} {<dbc>verl. schwer<099>};
    #ECHO {$kopfstoss_schadenausgabe}
} {5.1}

#ACTION {^  %+ verletz%!?t %+ sehr schwer.} {
    kopfstoss_analyse {%1} {%2} {<dbc>sehr schwer<099>};
    #ECHO {$kopfstoss_schadenausgabe}
} {5.0}

#ACTION {^  %+ brich%!?t %+ die Nase.} {
    kopfstoss_analyse {%1} {%2} {<daf>Nase brechen<099>};
    #ECHO {$kopfstoss_schadenausgabe}
}

#ACTION {^  %+ zerschmetter%!?t %+ Gesicht.} {
    kopfstoss_analyse {%1} {%2} {<daf>zerschmetter<099>};
    #ECHO {$kopfstoss_schadenausgabe}
}

#ACTION {^  %+ schlaeg%!?t %+ Gesicht zu Brei.} {
    kopfstoss_analyse {%1} {%2} {<158>zu Brei<099>};
    #ECHO {$kopfstoss_schadenausgabe}
}

#ACTION {^  %+ schlaeg%!?t %+ Gesicht zu Klump.} {
    kopfstoss_analyse {%1} {%2} {<158>zu Klump<099>};
    #ECHO {$kopfstoss_schadenausgabe}
}

#NOP { /**
 *	@brief	Zertreten
 */};

#ALIAS {zertreten_analyse} {
    spezial_schaden_analyse {zertreten} {Druck} %0
}

#ACTION {%+ zertritt%!* %+ unter %+.} {
    #LINE GAG;
} {5};

#ACTION {^  %+ tritt%!* neben %+ auf den Boden.} {
    zertreten_analyse {%1} {%2} {<dff>verfehlen<099>};
    #ECHO {$zertreten_schadenausgabe}
}

#ACTION {^  %+ stiefel%!?t %+ hart.} {
    zertreten_analyse {%1} {%2} {<dcf>hart<099>};
    #ECHO {$zertreten_schadenausgabe}
} {5.2}

#ACTION {^  %+ stiefel%!?t %+ sehr hart.} {
    zertreten_analyse {%1} {%3} {<dcf>sehr hart<099>};
    #ECHO {$zertreten_schadenausgabe}
} {5.1}

#ACTION {^  %+ stiefel%!?t %+ extrem hart.} {
    zertreten_analyse {%1} {%2} {<daf>extrem hart<099>};
    #ECHO {$zertreten_schadenausgabe}
} {5.1}

#ACTION {^  %+ zerdrueck%!?t %+.} {
    zertreten_analyse {%1} {%2} {<158>zerdruecken<099>};
    #ECHO {$zertreten_schadenausgabe}
}

#ACTION {^  %+ drueck%!?t %+ tief in den Boden hinein.} {
    zertreten_analyse {%1} {%2} {<158>in den Boden<099>};
    #ECHO {$zertreten_schadenausgabe}
}

#ACTION {^  %+ zermansch%!?t %+ zu Brei.} {
    zertreten_analyse {%1} {%2} {<158>zu Brei<099>};
    #ECHO {$zertreten_schadenausgabe}
}

#NOP { /**
 *	@brief	Lorica Buff durch Barden
 */};

#SUBSTITUTE {Ein regenbogenfarbenes Netz schimmert kurz um dich.} {@timer_lorica{%0}}

#FUNCTION {timer_lorica} {
    timer_start lorica;
    #RETURN {%1}
}

#ACTION {Das regenbogenfarbene Netz um dich herum verblasst.} {
    timer_end lorica
}

#NOP /* 25% */;
#NOP #ACTION {Die Wirkung von 'Schuetzendes Herz des Berges' wird bald enden.}
#ACTION {Die Wirkung von 'Lorica der Musen' wird bald enden.} {
    timer_med lorica
}

#NOP { /**
 *	@brief	Ruf der Helden Buff durch Barden
 */};

#ACTION {Mit donnerndem Crescendo endet Juergens Lied.} {
    timer_start helden
}

#ACTION {Deine heldenhafte Ausstrahlung verblasst.} {
    timer_end helden
}

#NOP /* 25% */;
#ACTION {Deine heldenhafte Ausstrahlung wird schwaecher.} {
    timer_med helden
}

#CLASS kampf_spells CLOSE
