#NOP { /**
 *	@brief	Pfad: Tiermeister
 */};

#CLASS wl_pfad_tiermeister KILL;
#CLASS wl_pfad_tiermeister OPEN;

#EVENT {VARIABLE UPDATED waldlaeufer_tiermeister} {
    #VAR waldlaeufer_pfad tiermeister;
}

#VAR waldlaeufer_tiermeister {
    {1}     {{name} {kampfgeist}    {symbol} {${color_inactive}G}}
    {2}     {{name} {ahnen}         {symbol} {${color_inactive}U}}
    {3}     {{name} {tierkraft}     {symbol} {${color_inactive}T}}
}

#ALIAS {change_status_symbol_tiermeister} {
    change_status_symbol waldlaeufer_tiermeister %1 %2 %3
} {5};

#ALIAS {set_status_symbol_tiermeister} {
    set_status_symbol waldlaeufer_tiermeister %1 %2
} {5};

#NOP { /**
 *	@brief	Kampfgeist          G
 */};

#ACTION {%+ stellt sich schuetzend vor dich.$} {
    set_status_symbol_tiermeister kampfgeist on
} {5};

#NOP { /**
 *	@brief	Kraft der Ahnen     U
 */};

#ACTION {^Ein %+ Tiergeist faehrt in %+ ein.$} {
    set_status_symbol_tiermeister ahnen on
} {5};

#NOP { /**
 *	@brief	Kraft des Tieres    T
 */};

#ACTION {^Du spuerst auf einmal eine geistige Verbindung zu %+.} {
    set_status_symbol_tiermeister tierkraft on
} {5};

#NOP { /**
 *	@brief	Tierangriff
 */};

#ACTION {^Du gibst Deinem Kampfgefaehrten das Zeichen zum Angriff.} {
    #ACTION {%+ versteht nicht.$} {
        #UNACTION {^Du zeigst auf %+};
    } {5};
    #ACTION {^Du zeigst auf %+} {
        setze_angriff_spell tierangriff;
        #UNACTION {%+ versteht nicht.$};
        #LINE GAG @line_gag_check{%1;aus\.}
    } {5};
}


#CLASS wl_pfad_tiermeister CLOSE;