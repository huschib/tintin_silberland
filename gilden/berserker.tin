#NOP
/** .tin
 *
 */

#CLASS {berserker} {kill}
#CLASS {berserker} {open}

#ECHO {<029> -> <188>%s<288> geladen<099>} {gilden/berserker.tin}

#VAR statusitems {{1}{statushp}{2}{statusmp}{3}{statusalign}{4}{statusbersispells}{5}{halsband}}

#ACTION {{-+}{\(WK\)|-+}{-+}{\(EH\)|-+}{-+}{\(RK\)|\(rk\)|-+}{-+}{\(wuetend\)|-+}{[^0-9]+}{[0-9]+}/{[0-9]+} ({[^\)]+}){[^0-9]+}{[0-9]+}/{[0-9]+} ({[^\)]+})} {
    #NOP #ECHO {t: %2, %4, %6, %8, %10, %11, %14, %15.};
    reportbody %0;
    #LINE GAG;
}

#ALIAS {reportbody} {
    #REGEXP {%0} {{-+}{\(WK\)|-+}{-+}{\(EH\)|-+}{-+}{\(RK\)|\(rk\)|-+}{-+}{\(wuetend\)|-+}{[^0-9]+}{[0-9]+}/{[0-9]+} ({[^\)]+}){[^0-9]+}{[0-9]+}/{[0-9]+} ({[^\)]+})} {
        #VAR willenskraft @berspell{&2};
        #VAR eisenhaut @berspell{&4};
        #VAR riesenkraft @berspell{&6};
        #VAR wueten @berspell{&8};
        #VAR lasthp $hp;
        #VAR hp &10;
        #VAR maxhp &11;
        #NOP #VAR diffhp &12;
        #VAR lastmp $mp;
        #VAR mp &14;
        #VAR maxmp &15;
        #NOP #VAR diffmp &16;
        #MATH {diffhp} {$hp - $lasthp};
        #MATH {diffmp} {$mp - $lastmp};
        #IF {$diffmp < 0 || $diffhp < 0} {
            #ECHO {%s lp : %s mp} {$diffhp} {$diffmp};
        };
        #IF {$diffmp > 0} {shift <060>+$diffmp<099>;};
        #IF {$diffhp > 0} {shift <020>+$diffhp<099>;};
        #IF {$diffmp < 0} {shift <140>$diffmp<099>;};
        #IF {$diffhp < 0} {shift <010>$diffhp<099>;};
        draw_status;
    };
}

#FUNCTION {berspell} {
    #NOP #ECHO {t: %0.};
    #IF {"%0" == "(WK)"} {#RETURN W;};
    #ELSEIF {"%0" == "(EH)"} {#RETURN E;};
    #ELSEIF {"%0" == "(RK)"} {#RETURN R;};
    #ELSEIF {"%0" == "(rk)"} {#RETURN r;};
    #ELSEIF {"%0" == "(wuetend)"} {#RETURN T;};
    #ELSE {#RETURN -;};
}

#FUNCTION {statusbersispells} {
    #NOP
    /**
     * setzt Block mit aktiven Spells fuer die Statuszeile
     *
     * arg1     Laenge des Resultats
     * return   formatierter String
     */;
    #VAR activespells $willenskraft$eisenhaut$riesenkraft$wueten$blutrausch;
    #FORMAT {result} {%-7s|} {$activespells};
}

#ACTION {Dein Wille wird wieder schwaecher.} {
    #VAR willenskraft -;
    #HIGHLIGHT {%0} {<ffa>};
}

#HIGHLIGHT {Deine Riesenkraft verlaesst Dich - Du brichst erschoepft zusammen.} {<ffa>}

#ACTION {Du erholst Dich von Deiner Erschoepfung.} {
    #VAR riesenkraft -;
}

#ACTION {Du wuetest wie ein Wilder und schuechterst alle Anwesenden ein.} {
    #VAR wueten T;
}

#ACTION {^Du bruellst {.+} an.} {
    #VAR angriffsspell Kampfschrei;
    #LINE GAG;
}

#ACTION {^Du bereitest Dich auf das Rammen vor.} {}

#ACTION {^Du rammst {.+} mit Deiner Schulter.} {
    #VAR angriffsspell Rammen;
    #LINE GAG;
}

#ACTION {^Du wirfst {.+} zu Boden.} {
    #ECHO {<ffa>%0};
    #LINE GAG;
}

#ACTION {^Du zertrittst {.+} unter {.+}.} {
    #VAR angriffsspell Zertreten;
    #NOP #LINE GAG;
}

#ACTION {^Du gibst {.+} einen harten Kopfstoss.} {
    #VAR angriffsspell Kopfstoss;
    #VAR opfer %1;
    #VAR angreifer Du;
    #VAR kopfstoss1 {schlaegst|brichst|zerschmetterst|verletzt};
    #VAR kopfstoss2 {schwer|sehr schwer|die Nase|Gesicht|Gesicht zu Brei};
    #ACTION {^  Du {$kopfstoss1} {.+} {$kopfstoss2}.} {
        #VAR tmp {mit einem gewaltigen Schlag}; 
        #IF {"%%1" == "brichst"} {
            #VAR schaden @schaden_ermitteln{aussergewoehnlich hart;$tmp};
        };
        #ELSEIF {"%%1" == "zerschmetterst"} {
            #VAR schaden @schaden_ermitteln{zerschmetterst;$tmp};
        };
        #ELSEIF {"%%1" == "verletzt"} {
            #VAR schaden @schaden_ermitteln{sehr hart;$tmp};
        };
        #ELSEIF {"%%2" == "Gesicht zu Brei"} {
            #VAR schaden @schaden_ermitteln{massakrierst;$tmp};
        };
        kampf_ausgabe;
        #NOP #LINE GAG;
        #UNACTION {^  Du {$kopfstoss1} {.+} {$kopfstoss2}.};
    } {1};
    #NOP #LINE GAG;
}

#CLASS {berserker} {close}

#NOP
/*
Das Blut spritzt, als Deine Streitaxt tief in eine starke Zollwache eindringt.
Dein maechtiger Axthieb wirft einen Kampfsoldat beinahe zu Boden.
Mit einem maechtigen Rueckhandhieb schleuderst Du einen Oger zu Boden.

Oswin ist noch bei bester Gesundheit.
Oswin hat erst ein paar Kratzer.
Der Stier hat schon einige Kratzer.
Der Kampfsoldat ist schon etwas geschwaecht.
Der Krieger der Xandim ist schon ziemlich geschwaecht.
Oswin fuehlte sich heute schon besser.
Der Kampfsoldat fuehlt sich etwas schlecht.
Der Krieger der Xandim fuehlt sich ziemlich schlecht.
Der Krieger der Xandim ist leicht angeschlagen.
Oswin ist ziemlich angeschlagen.
Der Krieger der Xandim ist schwer angeschlagen.
Der Stier sieht nicht mehr taufrisch aus.
Der Krieger der Xandim sieht gar nicht mehr gut aus.
Der Stier sieht schon relativ schlecht aus.
Der Krieger der Xandim macht einen etwas mitgenommenen Eindruck.
Der Stier wankt schon etwas.
Der Krieger der Xandim wankt bereits bedenklich.
Der Krieger der Xandim ist in einem ueblen Zustand.
Der Kampfsoldat ist in keiner guten Verfassung.
Der Krieger der Xandim ist in sehr schlechter Verfassung.
Der Kampfsoldat ist in extrem schlechter Verfassung.
Der Krieger der Xandim braucht bald einen Arzt.
Der Krieger der Xandim braucht sofort einen Notarzt.
Der Stier blutet aus vielen schweren Wunden.
Oswin blutet aus vielen toedlichen Wunden.
Der Krieger der Xandim steht auf der Schwelle des Todes.
Oswin ist am Verbluten und wird gleich sterben.
Der Krieger der Xandim ist eigentlich schon tot.
*/