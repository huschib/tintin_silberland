#NOP { /**
 *  @class  berserker
 *	@brief	Gilde: Berserker
 */};

#CLASS berserker KILL
#CLASS berserker OPEN

#ECHO {<029> -> <188>%s<288> geladen<099>} {gilden/berserker.tin}

#VAR color_inactive     {<g02>}
#VAR color_active       {<099>}
#VAR color_spell_ende   {<339>}
#VAR color_spell_lernen {<eec>}
#VAR gilden_report_cmd  {bstat}

#LIST status_items CREATE {status_session;status_hp;status_mp;status_ber;status_halsband}

#NOP { /**
 *	@brief	Berserkerreport
 *
 *  breportmodus rechts
 *  breportmodus max
 *  breportmodus diff
 *
 * ---- LPs: 429/431 (  +1)  MPs: 281/369 (  +1) --(WK)--(EH)--(RK)--(wuetend)--
 */};

#ACTION {---- LPs:%s%+1..d/%s%+1..d %+MPs:%s%+1..d/%s%+1..d %+--{\(WK\)|-+}--{\(EH\)|-+}--{\(RK\)|\(rk\)|-+}--{\(wuetend\)|-+}--} {
    #VAR status[hp] {%2};
    #VAR status[maxhp] {%4};
    #IF {&{status[lasthp]} == 0} {
        #VAR status[lasthp] {%2};
    };
    #MATH {status[diffhp]} { $status[hp] - $status[lasthp] };
    #VAR status[lasthp] {%2};

    #VAR status[mp] {%7};
    #VAR status[maxmp] {%9};
    #IF {&{status[lastmp]} == 0} {
        #VAR status[lastmp] {%7};
    };
    #MATH {status[diffmp]} { $status[mp] - $status[lastmp] };
    #VAR status[lastmp] {%7};

    #IF { $status[diffmp] > 0 } {
        shift_statusband <060>+$status[diffmp]<099>
    };
    #IF { $status[diffhp] > 0 } {
        shift_statusband <020>+$status[diffhp]<099>
    };
    #IF { $status[diffmp] < 0 } {
        shift_statusband <140>$status[diffmp]<099>
    };
    #IF { $status[diffhp] < 0 } {
        shift_statusband <010>$status[diffhp]<099>
    };
    #IF { $status[diffhp] != 0 || $status[diffmp] != 0 } {
        #NOP { /**
        *   @todo   Aufruf von scrolling nur Ã¼ber Funktion
        */};
        #LINE IGNORE #SHOWME {$scrolling} {-4} {61}
    };

    #IF { "%14" == "{-+}" && @check_state{$ber_spells[4]} } {
        #VAR ber_spells[4] {@swap_state{$ber_spells[4];off}};
    };

    #IF { $jadering == 1 } {
        #VAR jadering {0}
    };

    #LINE GAG;
}

#VAR ber_spells {
    {1} {<g02>W}
    {2} {<g02>E}
    {3} {<g02>R}
    {4} {<g02>w}
}

#EVENT {VARIABLE UPDATED ber_spells} {
    #VAR status_ber {@status_ber{}};
    #LIST status_items FIND status_ber tmp_status_index;
    #VAR statusline_list[$tmp_status_index] {$status_ber};
    draw_status
}

#NOP { /**
 *  @fn status_ber
 *  @brief  setzt Block mit aktiven Spells fuer die Statuszeile
 *
 *  @return     formatierter String
 */};

#FUNCTION {status_ber} {
    #LIST result COPY ber_spells;
    #LIST result COLLAPSE {};

    #FORMAT {result} {%s<099>|} {$result}
}

#NOP { /**
 *  @brief  Lernen
*/};

#HIGHLIGHT {%+ ist ein%? wuerdige%? Trainingspartner%w.} {$color_spell_lernen}

#NOP { /**
 *	@brief	Spell: Willenskraft
 */};

#ACTION {^Du holst tief Luft und fuehlst Dich gleich konzentrierter.} {
    timer_start willen;
    #VAR ber_spells[1] {@swap_state{$ber_spells[1];on}};
}

#ACTION {^Dein Wille ist nun gestaerkt.} {
    timer_start willen;
    #VAR ber_spells[1] {@swap_state{$ber_spells[1];on}};
}

#ACTION {^Du schliesst kurz die Augen und kraeftigst Deinen Willen.} {
    timer_start willen;
    #VAR ber_spells[1] {@swap_state{$ber_spells[1];on}};
}

#HIGHLIGHT {^Dein Wille wird wieder schwaecher.} {$color_spell_ende};

#ACTION {^Dein Wille wird wieder schwaecher.} {
    timer_end willen;
    #VAR ber_spells[1] {@swap_state{$ber_spells[1];off}}
}

#NOP { /**
 *	@brief	Spell: Riesenkraft
 */};

#ACTION {^Deine Muskeln schwellen an und Du fuehlst Dich stark wie ein%? Ries%+.} {
    #VAR ber_spells[3] {@swap_state{$ber_spells[3];on}};
} {5};

#ACTION {^Deine Muskeln werden ploetzlich riesengross.} {
    #VAR ber_spells[3] {@swap_state{$ber_spells[3];on}};
} {5};

#ACTION {^Du wirst stark wie ein%? Ries%+.} {
    #VAR ber_spells[3] {@swap_state{$ber_spells[3];on}};
} {5};

#ACTION {^Riesige Kraefte staerken Deine Muskeln.} {
    #VAR ber_spells[3] {@swap_state{$ber_spells[3];on}};
} {5};

#HIGHLIGHT {^Deine Riesenkraft verlaesst Dich - Du brichst erschoepft zusammen.} {$color_spell_ende}

#ACTION {^Deine Riesenkraft verlaesst Dich - Du brichst erschoepft zusammen.} {
    #REPLACE ber_spells[3] {{R$}} {r};
    #VAR ber_spells[3] {@swap_state{$ber_spells[3];on}};
} {5};

#ACTION {Du erholst Dich von Deiner Erschoepfung.} {
    #REPLACE ber_spells[3] {{r$}} {R};
    #VAR ber_spells[3] {@swap_state{$ber_spells[3];off}};
}

#NOP { /**
 *	@brief	Spell: Wueten
 */};

#ACTION {Du wuetest wie ein%? Wilde%? und schuechterst alle Anwesenden ein.} {
    #VAR ber_spells[4] {@swap_state{$ber_spells[4];on}};
    timer_start wueten 76 wueten_end_function
}

#ALIAS wueten_end_function {
    #VAR ber_spells[4] {@swap_state{$ber_spells[4];off}};
}

#NOP { /**
 *	@brief	Spell: Eisenhaut
 */};

#ACTION {^Deine Haut bekommt einen metallischen Schimmer.} {
    #VAR ber_spells[2] {@swap_state{$ber_spells[2];on}};
} {5};

#ACTION {^Deine Haut wird wieder normal.} {
    #VAR ber_spells[2] {@swap_state{$ber_spells[2];off}};
} {5};

#NOP { /**
 *	@brief	Spell: Kampfschrei
 */};

#ACTION {^Du bruellst %+ an.} {
    setze_angriff_spell kampfschrei;
    #LINE GAG;
}

#NOP { /**
 *	@brief	Spell: Rammen
 */};

#HIGHLIGHT {^Du wirfst %+ zu Boden.} {<ffa>};

#ACTION {^Du bereitest Dich auf das Rammen vor.} {
    #NOP ;
}

#ACTION {^Du rammst %+ mit Deiner Schulter.} {
    setze_angriff_spell rammen;
    #LINE GAG;
}

#NOP { /**
 *	@brief	Spell: Auge in Auge
 */};

#SUBSTITUTE {~\e[0%+40m%+ist absolut fit\e[0m%*.\e[0m}                            { [100%] %0};
#SUBSTITUTE {~\e[0%+40m%+ist bei ausgezeichneter Gesundheit\e[0m%*.\e[0m}         { [99%] %0};
#SUBSTITUTE {~\e[0%+40m%+ist bei hervorragender Gesundheit\e[0m%*.\e[0m}          { [96%] %0};
#SUBSTITUTE {~\e[0%+40m%+ist noch bei bester Gesundheit\e[0m%*.\e[0m}             { [93%] %0};

#SUBSTITUTE {~\e[0%+40m%+hat erst ein paar Kratzer\e[0m%*.\e[0m}                  { [90%] %0};
#SUBSTITUTE {~\e[0%+40m%+hat schon einige Kratzer\e[0m%*.\e[0m}                   { [87%] %0};
#SUBSTITUTE {~\e[0%+40m%+hat schon viele Kratzer\e[0m%*.\e[0m}                    { [84%] %0};

#SUBSTITUTE {~\e[0%+40m%+ist schon etwas geschwaecht\e[0m%*.\e[0m}                { [81%] %0};
#SUBSTITUTE {~\e[0%+40m%+ist schon ziemlich geschwaecht\e[0m%*.\e[0m}             { [78%] %0};

#SUBSTITUTE {~\e[0%+40m%+fuehlte sich heute schon besser\e[0m%*.\e[0m}            { [75%] %0};
#SUBSTITUTE {~\e[0%+40m%+fuehlt sich etwas schlecht\e[0m%*.\e[0m}                 { [72%] %0};
#SUBSTITUTE {~\e[0%+40m%+fuehlt sich ziemlich schlecht\e[0m%*.\e[0m}              { [69%] %0};

#SUBSTITUTE {~\e[0%+40m%+ist leicht angeschlagen\e[0m%*.\e[0m}                    { [66%] %0};
#SUBSTITUTE {~\e[0%+40m%+ist ziemlich angeschlagen\e[0m%*.\e[0m}                  { [63%] %0};
#SUBSTITUTE {~\e[0%+40m%+ist schwer angeschlagen\e[0m%*.\e[0m}                    { [60%] %0};

#SUBSTITUTE {~\e[0%+40m%+sieht nicht mehr taufrisch aus\e[0m%*.\e[0m}             { [57%] %0};
#SUBSTITUTE {~\e[0%+40m%+sieht gar nicht mehr gut aus\e[0m%*.\e[0m}               { [54%] %0};
#SUBSTITUTE {~\e[0%+40m%+sieht schon relativ schlecht aus\e[0m%*.\e[0m}           { [51%] %0};

#SUBSTITUTE {~\e[0%+40m%+macht einen etwas mitgenommenen Eindruck\e[0m%*.\e[0m}   { [48%] %0};
#SUBSTITUTE {~\e[0%+40m%+macht einen mitgenommenen Eindruck\e[0m%*.\e[0m}         { [45%] %0};
#SUBSTITUTE {~\e[0%+40m%+macht einen stark mitgenommenen Eindruck\e[0m%*.\e[0m}   { [42%] %0};

#SUBSTITUTE {~\e[0%+40m%+wankt schon etwas\e[0m%*.\e[0m}                          { [39%] %0};
#SUBSTITUTE {~\e[0%+40m%+wankt bereits bedenklich\e[0m%*.\e[0m}                   { [36%] %0};
#SUBSTITUTE {~\e[0%+40m%+ist in einem ueblen Zustand\e[0m%*.\e[0m}                { [33%] %0};

#SUBSTITUTE {~\e[0%+40m%+ist in keiner guten Verfassung\e[0m%*.\e[0m}             { [30%] %0};
#SUBSTITUTE {~\e[0%+40m%+ist in sehr schlechter Verfassung\e[0m%*.\e[0m}          { [27%] %0};
#SUBSTITUTE {~\e[0%+40m%+ist in extrem schlechter Verfassung\e[0m%*.\e[0m}        { [24%] %0};

#SUBSTITUTE {~\e[0%+40m%+braucht bald einen Arzt\e[0m%*.\e[0m}                    { [21%] %0};
#SUBSTITUTE {~\e[0%+40m%+braucht dringend einen Arzt\e[0m%*.\e[0m}                { [18%] %0};
#SUBSTITUTE {~\e[0%+40m%+braucht sofort einen Notarzt\e[0m%*.\e[0m}               { [15%] %0};

#SUBSTITUTE {~\e[0%+40m%+blutet aus vielen schweren Wunden\e[0m%*.\e[0m}          { [12%] %0};
#SUBSTITUTE {~\e[0%+40m%+blutet aus vielen toedlichen Wunden\e[0m%*.\e[0m}        { [ 9%] %0};
#SUBSTITUTE {~\e[0%+40m%+steht auf der Schwelle des Todes\e[0m%*.\e[0m}           { [ 6%] %0};
#SUBSTITUTE {~\e[0%+40m%+ist am Verbluten und wird gleich sterben\e[0m%*.\e[0m}   { [ 3%] %0};
#SUBSTITUTE {~\e[0%+40m%+ist eigentlich schon tot\e[0m%*.\e[0m}                   { [ 0%] %0};

#HIGHLIGHT {Du schaust so angestrengt, dass Du erblindest!} {<eec> underscore};
#HIGHLIGHT {Du kannst wieder sehen!} {underscore};

#NOP Dein starrer Blick laesst Der Raeuber erzittern.

#NOP { /**
 *  @brief  Kampf-Alis
 */};

#ALIAS {kampf_befehl} {
    #IF { "%2" == "{[0-9]+}" } {
        #FORMAT {target} {%l} {$feinde[%2]}
    };
    #ELSEIF { "%2" == "" } {
        #VAR target {}
    };
    #ELSE {
        #VAR target {%2}
    };
    #SEND {%1 $target}
}

#ALIAS {au} {
    kampf_befehl augeinauge %0
}

#ALIAS {sch} {
    kampf_befehl schaetz %0
}

#NOP { /**
 *	@brief	Zaehigkeit
 */};

#GAG {Rasende Wut brennt in dir, als du dich bruellend auf deine Feinde stuerzt!}

#CLASS berserker CLOSE

#NOP
/*
Das Blut spritzt, als Deine Streitaxt tief in eine starke Zollwache eindringt.
Dein maechtiger Axthieb wirft einen Kampfsoldat beinahe zu Boden.
Mit einem maechtigen Rueckhandhieb schleuderst Du einen Oger zu Boden.


*/