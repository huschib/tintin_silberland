#CLASS waldlaeufer KILL
#CLASS waldlaeufer OPEN

#ECHO {<029>-- <188>%s<288> geladen<099>} {gilden/waldlaeufer.tin}

#VAR color_inactive     {<g02>}
#VAR color_active       {<099>}
#VAR color_spell_ende   {<339>}
#VAR gilden_report_cmd  {wlstatus}

#HIGHLIGHT {^Du kannst wieder hoeren!} {underscore}
#HIGHLIGHT {^Du kannst wieder sehen!} {underscore}

#HIGHLIGHT {^Leider war Deine Kraeutersuche erfolglos.} {<dbb>}
#HIGHLIGHT {^Du verstehst die lokale Pflanzenwelt nun besser.} {<bdb>}
#HIGHLIGHT {^Du berrschst die Kraeuterkunde nun besser.} {<bdb>}
#HIGHLIGHT {^Du hast einige Kraeuter gefunden.} {<bdb>}

#HIGHLIGHT {^Ein %+ ist staerker geworden.} {<499><acf>}

#HIGHLIGHT {^Du hast nun die Vollendung um %+ erreicht!} {<dfc><388>}

#LIST status_items CREATE {status_session;status_hp;status_mp;status_align;status_waldlaeufer;status_zustand;status_tier;status_halsband}

#ALIAS {status_var_update} {
    #VAR %1 {@{%1}{}};
    #LIST status_items FIND %1 tmp_status_index;
    #VAR statusline_list[$tmp_status_index] {${%1}};
    draw_status
}

#NOP { /**
 *  @fn     status_waldlaeufer
 *  @brief  setzt Block mit aktiven Spells fuer die Statuszeile
 *
 *  @return     formatierter String
 */};

#FUNCTION {status_waldlaeufer} {
    #LIST result COPY waldlaeufer_spells;
    #LIST result COLLAPSE {};
    #FORMAT {result} {%s<099>|} {$result}
}

#EVENT {VARIABLE UPDATED waldlaeufer_spells} {
    status_var_update status_waldlaeufer
}

#VAR waldlaeufer_spells {
}

#NOP { /**
 *  @fn     status_zustand
 *  @brief  setzt Block mit aktuellem Zustand fuer die Statuszeile
 *
 *  @return     formatierter String
 */};

#FUNCTION {status_zustand} {
    #LIST result COPY waldlaeufer_zustand;
    #LIST result COLLAPSE {};
    #FORMAT {result} {%s<099>|} {$result}
}

#EVENT {VARIABLE UPDATED waldlaeufer_zustand} {
    status_var_update status_zustand
}

#VAR waldlaeufer_zustand {
}

#NOP { /**
 *  @fn     status_tier
 *  @brief  setzt Block mit aktuellem Tier-Zustand fuer die Statuszeile
 *
 *  @return     formatierter String
 */};

#FUNCTION {status_tier} {
    #IF { "$waldlaeufer_tier_status" == "" } {
        #VAR waldlaeufer_tier_status {${color_inactive}Tier${color_active}|}
    };
    #LIST result COPY waldlaeufer_tier_status;
    #LIST result COLLAPSE {};
    #FORMAT {result} {%s<099>} {$result}
}

#EVENT {VARIABLE UPDATED waldlaeufer_tier_status} {
    status_var_update status_tier
}

#VAR waldlaeufer_tier_status {
}

#NOP { /**
 *	@brief	Waldlaeufer Report
 *
 *  Aktive Optionen im Waldlaeuferreport sind:
 *  wlreport max 1
 *  wlreport spezial 1
 *  wlreport zauber 1
 *  wlreport gesinnung 1
 *  wlreport erweitert 1
 *  wlreport erfahrung 1
 *  wlreport tierleben 1
 *
 *  [~<-------->~~[________]~~{Hilfsbereit}~~{LP:122/178|MP:178/178}~[Tier: 82| ]~|Erfahrung: 4537926|~~]
 */};

#ACTION {{\[~<(.+)>~~\[(.+)\]~~.(.+).~~.LP:(\d+)\/(\d+)([(].*[)])?\|MP:(\d+)\/(\d+)([(].*[)])?.~(\[(Tier: \d+\|).\])?~\|Erfahrung: (\d+)\|~~\]}} {
    #VAR status[hp] {%5};
    #VAR status[maxhp] {%6};
    #IF {&{status[lasthp]} == 0} {
        #VAR status[lasthp] {%5};
    };
    #MATH {status[diffhp]} { $status[hp] - $status[lasthp] };
    #VAR status[lasthp] {%5};

    #VAR status[mp] {%8};
    #VAR status[maxmp] {%9};
    #IF {&{status[lastmp]} == 0} {
        #VAR status[lastmp] {%8};
    };
    #MATH {status[diffmp]} { $status[mp] - $status[lastmp] };
    #VAR status[lastmp] {%8};

    #IF { $status[diffmp] > 0 } {
        shift_statusband <060>+$status[diffmp]<099>
    };
    #IF { $status[diffhp] > 0 } {
        shift_statusband <020>+$status[diffhp]<099>
    };
    #IF { $status[diffmp] < 0 } {
        shift_statusband <140>$status[diffmp]<099>
    };
    #IF { $status[diffhp] < 0 } {
        shift_statusband <010>$status[diffhp]<099>
    };
    #IF { $status[diffhp] != 0 || $status[diffmp] != 0 } {
        #NOP { /**
        *   @todo   Aufruf von scrolling nur über Funktion
        */};
        #SHOWME {$scrolling} {-4} {61}
    };

    #IF {"$waldlaeufer_spells" != "%2"} {
        #VAR waldlaeufer_spells {%2}
    };
    #IF {"$waldlaeufer_zustand" != "%3"} {
        #VAR waldlaeufer_zustand {%3}
    };
    #IF {"$waldlaeufer_tier" != "%12"} {
        #VAR waldlaeufer_tier_status {%12}
    };

    #FORMAT {align} {%p} {%4};
    #IF { "$status[align]" != "%4" } {
        #FORMAT {status[align]} {%p} {%4}
    };

    #CLASS ${mud}_vars ASSIGN {
        #VAR xp[previous] {$xp[current]};
    };
    #VAR xp[current] {%13};
    #IF { $xp[log] } {
        #IF { $xp[previous] > $xp[current] } {
            #IF { $jadering == 1 } {
                #VAR jadering {0};
                #ECHO {Der Jadering hat Dir %m XP abgezogen} {$xp[previous]-$xp[current]};
                #MATH {xp[lost]} {$xp[lost] - $xp[previous] + $xp[current]}
            };
            #ELSE {
                #MATH {xp[death]} {$xp[death] + $xp[previous] - $xp[current]};
                #MATH {xp[deaths]} {$xp[deaths] + 1}
            }
        };
        #MATH {xp[diff]} {$xp[current] - $xp[start] + $xp[death]}
    };

    #IF { $jadering == 1 } {
        #VAR jadering {0}
    };

    #LINE GAG
} {5}

#ALIAS {spell_ende} {
    timer_end %2;
    #ECHO {%c%s%c (%t)} {$color_spell_ende} {%1} {<099>} {{%M:%S}{$timer[%2][dauer]}};
} {5};

#NOP { /**
 *	@brief	Ausweichtexte
 */};

#ACTION {^Du gehst in die Knie und tauchst unter den Angriff durch.} {
    setze_angriff_effekte 1 A;
    #LINE GAG;
} {5};

#ACTION {^Du machst einen Schritt nach vorne und rechts und springst mit einem Salto} {
    setze_angriff_effekte 1 A;
    #LINE GAG 3;
} {5};

#ACTION {^Du laesst Dich nach hinten fallen und nach einer geschickten Rueckwaertsrolle} {
    setze_angriff_effekte 1 A;
    #LINE GAG 2;
} {5};

#ACTION {^Blitzartig wirbelst Du herum und Durch die schnelle Drehung, gelingt es deinem} {
    setze_angriff_effekte 1 A;
    #LINE GAG 2;
} {5};

#ACTION {^Du springst genau nach oben, ziehst die Beine an und der Angriff verfehlt} {
    setze_angriff_effekte 1 A;
    #LINE GAG 2;
} {5};

#ACTION {^Schnell und sicher schlaegst du ein Rad zur Seite und rettest Dich so vor dem} {
    setze_angriff_effekte 1 A;
    #LINE GAG 2;
} {5};

#ACTION {^Wie ein Karnickel schlaegst Du Haken und bringst Dich in Sicherheit.} {
    setze_angriff_effekte 1 A;
    #LINE GAG;
} {5};

#ACTION {^Im letzten Moment rollst Du Dich nach rechts ab.} {
    setze_angriff_effekte 1 A;
    #LINE GAG;
} {5};

#ACTION {^Mit einem kraeftigen Sprung hechtest Du aus der Reichweite deines Feindes.} {
    setze_angriff_effekte 1 A;
    #LINE GAG;
} {5};

#ACTION {^Du beugst Dich schnell nach hinten und spuerst wie der Angriff knapp ueber} {
    setze_angriff_effekte 1 A;
    #LINE GAG 2;
} {5};

#ACTION {^Reaktionsschnell wirfst Du Dich auf den Boden und wirst somit nicht getroffen.} {
    setze_angriff_effekte 1 A;
    #LINE GAG;
} {5};

#ACTION {^Du rollst Dich nach vorne ab.} {
    setze_angriff_effekte 1 A;
    #LINE GAG;
} {5};

#ACTION {^Du laeufst an der Wand entlang und springst nach einem Wandlauf ueber den} {
    setze_angriff_effekte 1 A;
    #LINE GAG 2;
} {5};

#ACTION {^Du rollst Dich in letzter Sekunde nach links ab.} {
    setze_angriff_effekte 1 A;
    #LINE GAG;
} {5};

#ACTION {^Du springst schnell an eine Wand, um Dich gleich wieder mit eine Drehung zur} {
    setze_angriff_effekte 1 A;
    #LINE GAG 2;
} {5};

#ACTION {^Du drehst dich um, springst an die Wand und drueckst Dich nach hinten ab. Mit} {
    setze_angriff_effekte 1 A;
    #LINE GAG 2;
} {5};

#ACTION {^Schnell gehst Du hinter einem Baum in Deckung.} {
    setze_angriff_effekte 1 A;
    #LINE GAG;
} {5};

#ACTION {^Du wirbelst herum, laeufst auf einem Baum zu. Du laeufst den Baum gekonnt mit} {
    setze_angriff_effekte 1 A;
    #LINE GAG 4;
} {5};

#ACTION {^Du wirfst einige Blaetter und Zweige nach oben, durch die gekonnte Ablenkung} {
    setze_angriff_effekte 1 A;
    #LINE GAG 2;
} {5};

#ACTION {^Du springst schnell nach oben, schwingst Dich an einem Ast rauf und entgehst} {
    setze_angriff_effekte 1 A;
    #LINE GAG 2;
} {5};

#NOP { /**
 *	@brief	Spells
 */};

#ACTION {Deine Augen werden die eines Raubtiers. Du kannst nun im Dunklen sehen.} {
    timer_start auge
} {5};

#ACTION {Deine Sehkraft wird wieder normal.} {
    spell_ende {%0} auge;
    #LINE GAG
} {5};

#ACTION {Du spannst Deine Muskeln an und beginnst deinen Gegner schneller anzugreifen.} {
    timer_start schnell
} {5};

#ACTION {Dein Kreislauf hat sich wieder beruhigt.} {
    spell_ende {%0} schnell;
    #LINE GAG
} {5};

#ACTION {^Du konzentrierst Dich auf die Bewegungen Deines Schattens...} {
    timer_start pakt_1
} {5};

#ACTION {^Du verlaesst Die Welt der Schatten.} {
    #LINE GAG;
    spell_ende {%0} pakt_1;
    timer_start pakt_2
} {5};

#ACTION {Du kehrst vollstaendig aus den Schatten zurueck und wirst wieder normal.} {
    #LINE GAG;
    spell_ende {%0} pakt_2;
} {5};

#ACTION {^Du erhaelst die Haut einer %w.} {
    timer_start haut
} {5};

#ACTION {^Deine Haut wird wieder normal.} {
    spell_ende {%0} haut;
    #LINE GAG
} {5};

#NOP { /**
 *	@brief	Kampf-Spells
 */};

#ACTION {^Du trittst %+ mit ganzer Kraft.} {
    setze_angriff_spell fusstritt;
    #LINE GAG
} {5};

#ACTION {^Du springst %+ an und stichst zu.} {
    setze_angriff_spell messerstich;
    #LINE GAG
} {5};

#ACTION {^Du visierst kurz %+ an und schiesst.} {
    setze_angriff_spell deutschuss;
    #LINE GAG
} {5};

#NOP { /**
 *	@brief	Pfad: Messer
 */};

#ACTION {^Du streckst Deine Waffenhand aus, sammelst Dein Gleichgewicht in der} {
    setze_room_spell kling.wirbel;
    #LINE GAG 2
}

#NOP { /**
 *	@brief	Pfad: Bogen
 */};

#NOP { /**
 *	@brief	Pfad: Iana
 */};

#NOP { /**
 *	@brief	Pfad: Beschwoerer
 */};

#NOP { /**
 *	@brief	Pfad: Tiermeister
 */};

#ACTION {^Du gibst Deinem Kampfgefaehrten das Zeichen zum Angriff.} {
    #ACTION {%+ versteht nicht.$} {
        #UNACTION {^Du zeigst auf %+};
    } {5};
    #ACTION {^Du zeigst auf %+} {
        setze_angriff_spell tierangriff;
        #UNACTION {%+ versteht nicht.$};
        #LINE GAG @line_gag_check{%1;aus\.}
    } {5};
}

#NOP { /**
 *	@brief	Umwandlung der Anzeige der Waldlaeufer-Fertigkeiten
 *
 *  #actions in eigene Klasse ausgelagert, die bei Bedarf geladen wird
 */};

#CLASS wl_fertigkeiten OPEN

#ACTION {^ %+ kannst Du %+.} {
    #ECHO {%s %s} {@dotfill{<ffd>%1<099>;40}} {@waldlaeufer_fert{%2}<099>};
    #LINE GAG
} {5.0}

#ACTION {^ Im Durchschnitt kannst Du alle Faehigkeiten %+.} {
    #ECHO {%s %s} {@dotfill{<ffd>Durchschnitt<099>;40}} {@waldlaeufer_fert{%1}<099>};
    #LINE GAG
} {4.9}

#CLASS wl_fertigkeiten SAVE
#CLASS wl_fertigkeiten CLEAR
#CLASS wl_fertigkeiten CLOSE

#FUNCTION waldlaeufer_fert {
    #LIST wfort FIND {%0} tmp;
    #IF {$tmp < 11} {
        #VAR tmp_color {<bba>}
    };
    #ELSEIF {$tmp < 15} {
        #VAR tmp_color {<bca>}
    };
    #ELSEIF {$tmp < 18} {
        #VAR tmp_color {<bdb>}
    };
    #ELSEIF {$tmp < 21} {
        #VAR tmp_color {<dea>}
    };
    #ELSEIF {$tmp < 25} {
        #VAR tmp_color {<bfa>}
    };
    #ELSE {
        #VAR tmp_color {<dfc><388>}
    };
    #RETURN ${tmp_color}%0
}

#ALIAS {fert} {
    #CLASS wl_fertigkeiten LOAD;
    prompt_func fert_ende;
    #SEND {fert}
}

add_help {fert} {waldlaeufer}
    {Zeigt bei Raziel die Fertigkeiten in leicht veraenderter Form an}
    {fert}

#ALIAS {fert_ende} {
    #CLASS wl_fertigkeiten CLEAR;
    prompt_func stop
}

#VAR wfort {
    {1} {ueberhaupt nicht}
    {2} {eigentlich nicht}
    {3} {fast gar nicht}
    {4} {kaum}
    {5} {sehr schlecht}
    {6} {nur sehr laienhaft}
    {7} {schlecht}
    {8} {wie ein Amateur}
    {9} {unterdurchschnittlich}
    {10}{durchschnittlich}
    {11}{etwas ueberdurchschnittlich}
    {12}{ueberdurchschnittlich}
    {13}{fast ausreichend}
    {14}{ausreichend}
    {15}{recht sicher}
    {16}{relativ gut}
    {17}{gut}
    {18}{sehr gut}
    {19}{wie ein Waldlaeufer}
    {20}{fast fehlerfrei}
    {21}{wie ein Hueter}
    {22}{fehlerfrei}
    {23}{wie ein Meister}
    {24}{wie ein Lehrmeister}
    {25}{wie ein legendaerer Meister}
}

#NOP { /**
 *  @brief  Zeigt Stufen fuer den Skill-Fortschritt
 *
 */};

#ALIAS {wfort} {
    #VAR tmp_count {1};
    #SCREEN GET {SCROLL_COLS} {tmp_width};
    #VAR tmp_line {};
    #WHILE {$tmp_count <= &wfort[] } {
        #FORMAT {tmp_line_len} {%L} {$tmp_line};
        #FORMAT {tmp_item_len} {%L} {$wfort[$tmp_count]};
        #MATH {tmp_total_len} {$tmp_line_len + $tmp_item_len + 5};

        #VAR tmp_item @waldlaeufer_fert{$wfort[$tmp_count]};

        #IF {$tmp_total_len > $tmp_width} {
            #ECHO {$tmp_line<099>};
            #VAR tmp_line {$tmp_item};
        };
        #ELSEIF {"$tmp_line" == ""} {
            #VAR tmp_line {$tmp_item};
        };
        #ELSE {
            #CAT tmp_line {     $tmp_item}
        };
        inc tmp_count
    };
    #ECHO {$tmp_line<099>}
} {5};

add_help {wfort} {waldlaeufer}
    {Zeigt eine Liste der Fertigkeitsstufen an}
    {wfort}

#NOP { /**
 *	Kraeutersuche
 *
 *  @todo   vorherige Waffe speichern und nach 'stop' zuecken
 */};

#ALIAS {kraeuter_suche} {
    #IF {"%1" == "start"} {
        #ACTION {^Leider war Deine Kraeutersuche erfolglos.} {
            #SEND {kraeutersuche}
        } {5};
        #ACTION {^Du hast einige Kraeuter gefunden.} {
            #SEND {oeffne kraeuterbeutel};
            #SEND {leg alles ausser behaltenes in kraeuterbeutel};
            #NOP #SEND {schliesse kraeuterbeutel};
            #NOP #VAR prompt_skip 1;
            #NOP prompt_func kraeuter_suche_prompt;
            kraeuter_suche stop
        } {5};
        #ACTION {^Hier wurden schon vor kurzer Zeit Kraeuter gesucht.} {
            kraeuter_suche stop
        } {5};
        #NOP aa ziehe 1;
        #SEND {zueck kraeutersichel};
        #SEND {kraeutersuche}
    };
    #ELSEIF {"%1" == "stop"} {
        #UNACTION {^Leider war Deine Kraeutersuche erfolglos.};
        #UNACTION {^Hier wurden schon vor kurzer Zeit Kraeuter gesucht.};
        #UNACTION {^Du hast einige Kraeuter gefunden.};
        #NOP aa trage 1;
        #SEND {zueck dolch}
    };
    #ELSE {
        #ECHO {Anwendung: kraut start|stop}
    }
} {5};

#ALIAS {kraeuter_suche_prompt} {
    #IF {$prompt_skip == 0} {
        #UNACTION {^Du legst d{as|en|ie} %+ in den Kraeuterbeutel.};
        prompt_func stop;
        #RETURN
    };
    dec prompt_skip;
    #LIST kraeuter CLEAR;
    #ACTION {^Du legst d{as|en|ie} %+ in den Kraeuterbeutel.} {
        #LIST kraeuter ADD {%%2};
    } {5}
} {5};

#ALIAS {kraut} {
    kraeuter_suche %0
} {5};

add_help {kraut} {waldlaeufer}
    {Startet die Kraeutersuche solange bis die Suche erfolgreich ist.}
    {kraut start|stop}

#ALIAS {kmix} {
    #IF { "%0" != "" } {
        #SEND {beschwoere 'kraeutermixtur' auf %0};
        #RETURN
    };
    #IF { &{kraeuter[-1]} } {
        #SEND {beschwoere 'kraeutermixtur' auf $kraeuter[-1]};
        #LIST kraeuter DEL -1
    };
    #ELSE {
        #ECHO {Die Kraeuterliste ist leer.}
    }
} {5};

#ALIAS {ukr} {
    #ACTION {^Er ist geoeffnet und enthaelt:} {
        prompt_func kraeuterbeutel_prompt;
        #CLASS kraeuter OPEN;
        #IF { "%1" != "" } {
            set_tmp_highlight %1
        };
        #CLASS kraeuter CLOSE;
    } {5};
    #SEND {oeffne $container[kraeuter]};
    ubeu kraeuter
} {5};

add_help {ukr} {waldlaeufer}
    {Zeigt den Inhalt des Kraeuterbeutels, optional angegebene Wirkung farbig.}
    {ukr [hunger|durst|gift|ausdauer|alkohol|leben|magie|taub|blind|frosch|unterwasser|malaria]}

#ALIAS {set_tmp_highlight} {
    #LIST tmp COPY kraeuter;
    #LIST tmp INDEXATE wirkung;
    #LIST tmp FILTER {%*{%1}%*};
    #FOREACH {$tmp[%*]} {item} {
        #LINE ONESHOT {
            #HIGHLIGHT {$item[name]} {<bdb>};
        };
    };
}

#ALIAS {kraeuterbeutel_prompt} {
    #CLASS kraeuter KILL;
    prompt_func stop;
    #UNACTION {^Er ist geoeffnet und enthaelt:}
} {5};

#ALIAS {lkr} {
    #SEND {leg jedes heilkraut in $container[kraeuter]}
} {5};

add_help {lkr} {waldlaeufer}
    {Legt jedes Heilkraut in den Kraeuterbeutel}
    {lkr <kraut>}

#ALIAS {nkr} {
    #SEND {nimm %0 aus $container[kraeuter]};
}

add_help {nkr} {waldlaeufer}
    {Nimmt Kraeuter aus dem Kraeuterbeutel.}
    {nkr <kraut>}

#ALIAS {nmix} {
    #SEND {oeffne $container[kraeuter]};
    nkr mixtur
}

add_help {nmix} {waldlaeufer}
    {Nimmt eine Mixtur aus dem Kraeuterbeutel.}
    {nmix}

#ALIAS {imix} {
    nmix;
    #SEND {iss mixtur}
}

add_help {imix} {waldlaeufer}
    {Nimmt eine Mixtur aus dem Kraeuterbeutel und vertilgt sie sofort.}
    {imix}

#VAR kraeuter
{
    {1} {{name}    {Lungenkraut}       {level}  {1} {wirkung}   {unterwasser} }
    {2} {{name}    {Sonnenwurzel}      {level}  {1} {wirkung}   {krankheiten,malaria} }
    {3} {{name}    {Teichkraut}        {level}  {1} {wirkung}   {frosch} }
    {4} {{name}    {Kronstoeckl}       {level}  {2} {wirkung}   {frosch} }
    {5} {{name}    {Wanderblatt}       {level}  {1} {wirkung}   {ausdauer} }
    {6} {{name}    {Wanderkraut}       {level}  {2} {wirkung}   {ausdauer} }
    {7} {{name}    {Wanderwurzel}      {level}  {3} {wirkung}   {ausdauer} }
    {8} {{name}    {Hexenkraut}        {level}  {1} {wirkung}   {taub} }
    {9} {{name}    {Wolfsbeere}        {level}  {2} {wirkung}   {taub} }
    {10}{{name}    {Nachtblatt}        {level}  {1} {wirkung}   {blind} }
    {11}{{name}    {Nachtkraut}        {level}  {2} {wirkung}   {blind} }
    {12}{{name}    {Nachtwurzel}       {level}  {3} {wirkung}   {blind} }
    {13}{{name}    {Elfenkraut}        {level}  {1} {wirkung}   {taub,blind} }
    {14}{{name}    {Jaegerkraut}       {level}  {2} {wirkung}   {taub,blind} }
    {15}{{name}    {Dunkelbeere}       {level}  {1} {wirkung}   {alkohol} }
    {16}{{name}    {Gruenblatt}        {level}  {2} {wirkung}   {alkohol} }
    {17}{{name}    {Schattengras}      {level}  {3} {wirkung}   {alkohol} }
    {18}{{name}    {Mondwurzel}        {level}  {1} {wirkung}   {durst} }
    {19}{{name}    {Sandwurzel}        {level}  {2} {wirkung}   {durst} }
    {20}{{name}    {Spinnenmoos}       {level}  {3} {wirkung}   {durst} }
    {21}{{name}    {Himmelsmoos}       {level}  {1} {wirkung}   {durst,alkohol} }
    {22}{{name}    {Zwergengras}       {level}  {2} {wirkung}   {durst,alkohol} }
    {23}{{name}    {Waechtergras}      {level}  {3} {wirkung}   {durst,alkohol} }
    {24}{{name}    {Waldefeu}          {level}  {1} {wirkung}   {hunger} }
    {25}{{name}    {Kreuwurzel}        {level}  {2} {wirkung}   {hunger} }
    {26}{{name}    {Guertelgras}       {level}  {3} {wirkung}   {hunger} }
    {27}{{name}    {Zwergenfinger}     {level}  {4} {wirkung}   {hunger} }
    {28}{{name}    {Silberknolle}      {level}  {4} {wirkung}   {hunger,durst,alkohol} }
    {29}{{name}    {Feuerblatt}        {level}  {1} {wirkung}   {magie} }
    {30}{{name}    {Feuerkraut}        {level}  {2} {wirkung}   {magie} }
    {31}{{name}    {Feuerwurzel}       {level}  {3} {wirkung}   {magie} }
    {32}{{name}    {Tethyrblatt}       {level}  {1} {wirkung}   {leben} }
    {33}{{name}    {Tethyrkraut}       {level}  {2} {wirkung}   {leben} }
    {34}{{name}    {Tethyrwurzel}      {level}  {3} {wirkung}   {leben} }
    {35}{{name}    {Waldlotus}         {level}  {1} {wirkung}   {magie,leben} }
    {36}{{name}    {Shuriknolle}       {level}  {1} {wirkung}   {gift} }
    {37}{{name}    {Schlangenkraut}    {level}  {2} {wirkung}   {gift} }
    {38}{{name}    {Kobraknolle}       {level}  {3} {wirkung}   {gift} }
    {39}{{name}    {Kreuzblatt}        {level}  {4} {wirkung}   {gift} }
}


#NOP { /**
 *	@brief	Begleittier Funktionen und Übersicht
 */};

#ALIAS {za} {
    #CLASS $info[SESSION][NAME]_vars ASSIGN {
        #VAR waldlaeufer_tier[id] %0;
        #VAR waldlaeufer_tier[versteckt] 0;
        #VAR waldlaeufer_tier[kampf] 0;
    };
    #SEND { beschwoere 'zaehmung' auf %0}
} {5};

add_help {za} {waldlaeufer}
    {Tierbeherrschung: Zaehmen eines Tieres.}
    {za <tier>}

#ALIAS {tiver} {
    #SEND {sag $waldlaeufer_tier[id] verschwinde};

    #VAR waldlaeufer_tier[versteckt] 0;
    #VAR waldlaeufer_tier[kampf] 0;
    #VAR waldlaeufer_tier[id] {};
    #VAR waldlaeufer_tier_status {};
    update_var
} {5};

add_help {tiver} {waldlaeufer}
    {Tierbeherrschung: Endgueltiges Wegschicken des aktiven Tieres.}
    {tiver}

#ALIAS {tiv} {
    #IF { @_an_aus{waldlaeufer_tier[versteckt]} } {
        #SEND {spiele melodie der treue}
    };
    #ELSE {
        #SEND {sag $waldlaeufer_tier[id] versteck dich}
    };
    toggle_var waldlaeufer_tier[kampf];
    toggle_var waldlaeufer_tier[versteckt];

    update_var
} {5};

add_help {tiv} {waldlaeufer}
    {Tierbeherrschung: Toggle um Tier ins Versteck schicken und zu holen.}
    {tiv}

#ALIAS {tik} {
    #IF {   @_an_aus{waldlaeufer_tier[id]} == 0
            || @_an_aus{waldlaeufer_tier[versteckt]} == 1} {
        #ECHO {Kein Tier anwesend fuer Befehle.};
        #RETURN
    };

    #IF { @_an_aus{waldlaeufer_tier[kampf]} } {
        #SEND {sag $waldlaeufer_tier[id] kampfpause}
    };
    #ELSE {
        #SEND {sag $waldlaeufer_tier[id] hilf mir}
    };
    toggle_var waldlaeufer_tier[kampf];
    update_var
} {5};

add_help {tik} {waldlaeufer}
    {Tierbeherrschung: Toggle fuer Kampfunterstuetzung eines Tieres.}
    {tik}

#ALIAS {hh} {
    #SEND {sag $waldlaeufer_tier[id] hilf mir}
} {5};

add_help {hh} {waldlaeufer}
    {Tierbeherrschung: Aktives Tier zum Unzerstuetzen im Kampf auffordern.}
    {hh}

#ALIAS {tier} {
    #ECHO {%s %n} {@dotfill{Begleiter;20}} {@an_aus{waldlaeufer_tier[id]}};
    #ECHO {%s %s} {@dotfill{Versteck;20}} {@an_aus{waldlaeufer_tier[versteckt]}};
    #ECHO {%s %s} {@dotfill{Kampf;20}} {@an_aus{waldlaeufer_tier[kampf]}};
} {5};

add_help {tier} {waldlaeufer}
    {Tierbeherrschung: Zeigt den aktuellen Status deines Begleittieres an.}
    {tier}

#NOP { /**
 *	@brief	Informationen aus 'wlinfo' auslesen
 */};

#CLASS wl_info OPEN

#ACTION {Belastung:%!s[%+]} {
    #LINE GAG;
    #LIST statusinfo ADD {Last: @wlinfo_len{%1}% | }
} {5};

#ACTION {Ausdauer:%!s[%+]} {
    #LINE GAG;
    #LIST statusinfo ADD {Ausd: @wlinfo_len{%1}% | }
} {5};

#ACTION {Vermoegen:%!s%+1..d Muenzen} {
    #LINE GAG;
    #LIST statusinfo ADD {Geld: %1 | }
} {5};

#ACTION {Umgebung: %+1..w, Du bist %+.} {
    #LINE GAG;
    #LIST statusinfo ADD {Terrain: @wlinfo_terrain{%2} (%1)}
} {5};

#ACTION {^%*$} {
    #LINE GAG;
} {9};

#FUNCTION {wlinfo_len} {
    #VAR wlinfo_len 0;
    #REGEXP {%1} {{#+}} {
        #FORMAT {wlinfo_len} {%L} {&1}
    };
    #MATH {result} {$wlinfo_len * 10};
    #RETURN $result
};

#FUNCTION {wlinfo_terrain} {
    #SWITCH { "%0" } {
        #CASE { "in huegeligen Land" } {
            #VAR result {Huegel}
        };
        #CASE { "im Gebirge" } {
            #VAR result {Gebirge}
        };
        #CASE { "im Sumpf" } {
            #VAR result {Sumpf}
        };
        #CASE { "im Wald" } {
            #VAR result {Wald}
        };
        #CASE { "auf einer Ebene" } {
            #VAR result {Ebene}
        };
        #CASE { "in einem Haus" } {
            #VAR result {Haus}
        };
        #CASE { "auf einer Strasse" } {
            #VAR result {Strasse}
        };
        #CASE { "im Wasser" } {
            #VAR result {Wasser}
        };
        #CASE { "in einer Hoehle" } {
            #VAR result {Hoehle}
        };
        #CASE { "in der Stadt" } {
            #VAR result {Stadt}
        };
        #CASE { "im Dschungel" } {
            #VAR result {Dschungel}
        };
        #CASE { "in arktischen Gefilden" } {
            #VAR result {Arktis}
        };
        #DEFAULT {
            #VAR result {unbekannt}
        };
    };
    #RETURN $result
};

#CLASS wl_info SAVE
#CLASS wl_info CLEAR
#CLASS wl_info CLOSE

#ALIAS {wi} {
    #CLASS wl_info LOAD;
    #LIST statusinfo CREATE {};
    prompt_func wlinfo_ende;
    #SEND {wlinfo}
}

#ALIAS {wlinfo_ende} {
    #LIST statusinfo COLLAPSE {};
    #CLASS wl_info CLEAR;
    #SCREEN GET SCROLL_COLS tmp_width;
    #FORMAT statusinfo {%-${tmp_width}s} $statusinfo;
    draw_status;
    prompt_func stop
}

#NOP #ALIAS {wi} {wlinfo}

#NOP { /**
 *	@brief	Spellcombos
 */};

#ALIAS {toggle_spell} {
    #SEND {${%1[angriff][${%1[next]}]}};
    #IF { ${%1[next]} == 1 } {
        #VAR %1[next] 2
    };
    #ELSE {
        #VAR %1[next] 1
    }
}

#VAR {mf}
{
    {next}  {1}
    {angriff}
    {
        {1} {beschwoere 'messerstich'}
        {2} {beschwoere 'fusstritt'}
    }
}

#ALIAS {mf} {
    toggle_spell mf
}

#VAR {fallen}
{
    {next}  {1}
    {angriff}
    {
        {1} {beschwoere 'fesselfalle'}
        {2} {beschwoere 'fangfalle'}
    }
}

#ALIAS {fall} {
    toggle_spell fallen
}

#NOP { /**
 *	@brief	Zustand mit Augen des Jaegers
 */};

#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist absolut fit\e[0m.\e[0m}                            {@auge_zustand{%1;100;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist bei ausgezeichneter Gesundheit\e[0m.\e[0m}         {@auge_zustand{%1;99;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist bei hervorragender Gesundheit\e[0m.\e[0m}          {@auge_zustand{%1;96;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist noch bei bester Gesundheit\e[0m.\e[0m}             {@auge_zustand{%1;93;%0}};

#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ hat erst ein paar Kratzer\e[0m.\e[0m}                  {@auge_zustand{%1;90;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ hat schon einige Kratzer\e[0m.\e[0m}                   {@auge_zustand{%1;87;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ hat schon viele Kratzer\e[0m.\e[0m}                    {@auge_zustand{%1;84;%0}};

#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist schon etwas geschwaecht\e[0m.\e[0m}                {@auge_zustand{%1;81;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist schon ziemlich geschwaecht\e[0m.\e[0m}             {@auge_zustand{%1;78;%0}};

#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ fuehlte sich heute schon besser\e[0m.\e[0m}            {@auge_zustand{%1;75;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ fuehlt sich etwas schlecht\e[0m.\e[0m}                 {@auge_zustand{%1;72;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ fuehlt sich ziemlich schlecht\e[0m.\e[0m}              {@auge_zustand{%1;69;%0}};

#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist leicht angeschlagen\e[0m.\e[0m}                    {@auge_zustand{%1;66;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist ziemlich angeschlagen\e[0m.\e[0m}                  {@auge_zustand{%1;63;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist schwer angeschlagen\e[0m.\e[0m}                    {@auge_zustand{%1;60;%0}};

#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ sieht nicht mehr taufrisch aus\e[0m.\e[0m}             {@auge_zustand{%1;57;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ sieht gar nicht mehr gut aus\e[0m.\e[0m}               {@auge_zustand{%1;54;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ sieht schon relativ schlecht aus\e[0m.\e[0m}           {@auge_zustand{%1;51;%0}};

#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ macht einen etwas mitgenommenen Eindruck\e[0m.\e[0m}   {@auge_zustand{%1;48;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ macht einen mitgenommenen Eindruck\e[0m.\e[0m}         {@auge_zustand{%1;45;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ macht einen stark mitgenommenen Eindruck\e[0m.\e[0m}   {@auge_zustand{%1;42;%0}};

#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ wankt schon etwas\e[0m.\e[0m}                          {@auge_zustand{%1;39;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ wankt bereits bedenklich\e[0m.\e[0m}                   {@auge_zustand{%1;36;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist in einem ueblen Zustand\e[0m.\e[0m}                {@auge_zustand{%1;33;%0}};

#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist in keiner guten Verfassung\e[0m.\e[0m}             {@auge_zustand{%1;30;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist in sehr schlechter Verfassung\e[0m.\e[0m}          {@auge_zustand{%1;27;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist in extrem schlechter Verfassung\e[0m.\e[0m}        {@auge_zustand{%1;24;%0}};

#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ braucht bald einen Arzt\e[0m.\e[0m}                    {@auge_zustand{%1;21;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ braucht dringend einen Arzt\e[0m.\e[0m}                {@auge_zustand{%1;18;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ braucht sofort einen Notarzt\e[0m.\e[0m}               {@auge_zustand{%1;15;%0}};

#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ blutet aus vielen schweren Wunden\e[0m.\e[0m}          {@auge_zustand{%1;12;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ blutet aus vielen toedlichen Wunden\e[0m.\e[0m}        {@auge_zustand{%1;9;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ steht auf der Schwelle des Todes\e[0m.\e[0m}           {@auge_zustand{%1;6;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist am Verbluten und wird gleich sterben\e[0m.\e[0m}   {@auge_zustand{%1;3;%0}};
#SUBSTITUTE {~\e[0m\e[32m\e[40m%+ ist eigentlich schon tot\e[0m.\e[0m}                   {@auge_zustand{%1;0;%0}};

#FUNCTION {auge_zustand} {
    #IF {$screen[panels] == 1} {
        #FORMAT {result} { [%+2s%%] %3} {%2};
        #RETURN $result
    };
    #VAR monokel_chars feind_liste;
    #IF { &{feind_liste[]} > 0 } {
        #LIST feind_liste INDEXATE name;
    };
    #LIST feind_liste FIND {%1} tmp_index;
    #IF { $tmp_index == 0 } {
        #LIST feind_liste ADD {{{name}{%1}{zustand}{%2}}};
    };
    #ELSE {
        #VAR feind_liste[$tmp_index][zustand] {%2};
    };
    draw_zustand;
    #LINE GAG;
    #RETURN {}
} {5};

#VAR post_kampf wald_nach_kampf;

#ALIAS {wald_nach_kampf} {
    #IF { &{feind_liste} } {
        #LIST feind_liste CLEAR;
    };
    #ELSE {
        #LIST feind_liste CREATE {}
    };
    draw_zustand
}

#CLASS waldlaeufer CLOSE


#NOP { /**
Deine Glieder sind erschoepft. Deine Angriffe erfolgen wieder in normaler
Geschwindigkeit.                                                         
 */};