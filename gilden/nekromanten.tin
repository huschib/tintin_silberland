#NOP
/** Nekromanten.tin
 * Alles, was mit den Spells der Nekromanten zu tun hat.
 *
 * TODO:
 *      - Status von Beruehrung von Zum-Uul
 *      - Status von Schleier des Val-Tkro-Tthar
 *      - universales 'hp'-Alias f端r mehrere Helfer
 *      - Helfer: [B]lutgolem, [G]houl, [M]umie, [S]kelett, [V]ampir, [Z]ombie
 *                [L]eiche, [S]chwert, [V]arragoin, [T]hassalos
 */

#CLASS nekromanten KILL
#CLASS nekromanten OPEN

#ECHO {<029> -> <188>%s<288> geladen<099>} {gilden/nekromanten.tin}

#VAR gilden_report_cmd {nstatus}

#EVENT {VARIABLE UPDATED nekro_spells} {
    #VAR status_nekro {@status_nekro{}};
    #LIST status_items FIND status_nekro tmp_status_index;
    #VAR statusline_list[$tmp_status_index] {$status_nekro};
    draw_status
}

#EVENT {VARIABLE UPDATED nekro_untote} {
    #VAR status_untote {@status_untote{}};
    draw_status
}

#LIST status_items CREATE {status_session;status_hp;status_mp;status_align;status_nekro;status_halsband}

#NOP { /**
 *	beruehrung;schleier;helfer;sammler;waffen
 */};
#VAR nekro_spells {
    {1} {<g02>B}
    {2} {<g02>S}
    {3} {<g02>H}
    {4} {<g02>S}
    {5} {<g02>W}
}

#VAR nekro_untote {
    {1} {<g02>WG:}
    {2} {<g02>F}
    {3} {<g02>K}
    {4} {<g02>M}
}

#VAR color_inactive {<g02>};
#VAR color_active {<099>};

#NOP { /**
 *  @brief  Trigger auf Anzeige von Report
 *
 *  Trigger erfasst die LP/MP Anzeige bei den Nekromanten
 *
 *  Extra Einstellung noetig:   nreport max 1
 *
 *  ---------[-S][--------]--(satanisch)--- LPs: 297/297(0), MPs: 302/318(+1)
 *
 */};
#ACTION {---------[%+][%+]-%!*(%+)%!*- LPs: %!s%+1..d/%!s%+1..d(%!+), MPs: %!s%+1..d/%!s%+1..d(%!+)} {
    #VAR nekro[spells] %1;
    #VAR nekro[zustand] %2;

    #IF { "$config[fingerhut]" != "1" } {
        #VAR status[hp] {%4};
        #VAR status[maxhp] {%5};
        #IF {&{status[lasthp]} == 0} {
            #VAR status[lasthp] {%4};
        };
        #MATH {status[diffhp]} { $status[hp] - $status[lasthp] };
        #VAR status[lasthp] {%4};

        #VAR status[mp] {%6};
        #VAR status[maxmp] {%7};
        #IF {&{status[lastmp]} == 0} {
            #VAR status[lastmp] {%6};
        };
        #MATH {status[diffmp]} { $status[mp] - $status[lastmp] };
        #VAR status[lastmp] {%6};

        #IF { $status[diffmp] > 0 } {
            shift_statusband <060>+$status[diffmp]<099>
        };
        #IF { $status[diffhp] > 0 } {
            shift_statusband <020>+$status[diffhp]<099>
        };
        #IF { $status[diffmp] < 0 } {
            shift_statusband <140>$status[diffmp]<099>
        };
        #IF { $status[diffhp] < 0 } {
            shift_statusband <010>$status[diffhp]<099>
        };
        #IF { $status[diffhp] != 0 || $status[diffmp] != 0 } {
            #NOP { /**
            *   @todo   Aufruf von scrolling nur 端ber Funktion
            */};
            #SHOWME {$scrolling} {-4} {61}
        };
        #FORMAT {align} {%p} {%3};
        #IF { "$status[align]" != "%3" } {
            #FORMAT {status[align]} {%p} {%3}
        };

    };

    #LINE GAG
} {4}

#NOP { /**
 *  @fn status_nekro
 *  @brief  setzt Block mit aktiven Spells fuer die Statuszeile
 *
 *  @return     formatierter String
 *
 */};
#FUNCTION {status_nekro} {
    #LIST result COPY nekro_spells;
    #LIST result COLLAPSE {};

    #FORMAT {result} {%s<099>|} {$result}
}

#FUNCTION {status_untote} {
    #FORMAT {result} {%-2s<099>|} {  };
}

#NOP { /**
 *  @fn     hp
 *  @brief  Hilfe-Kommando f端r jeweils aktiven Untoten
 *
 *  evtl Kommando nach eintreten des untoten automatisch ausloesen
 *
 */};



#NOP { /**
 *  @brief  Keinen Spellangriff bei abgewehrtem Zauber setzen
 *
 */};
#ACTION {{.+} wehrt Deinen Zauber ab.} {
    setze_angriff_spell
}

#NOP { /**
 *  @brief  Sonderschlag f端r Nekromanten
 *
 */};
#ACTION {Die Macht des Boesen verstaerkt Deine Attacke!} {
    setze_angriff_effekte 4 M;
    #LINE GAG
}

#ACTION {^Der Geist %+ schwaecht eine Attacke fuer Dich ab!} {
    setze_angriff_effekte 1 G;
    #LINE GAG
} {5};

#NOP /* Helfer */
#ACTION {{.+} Wille ist gebrochen und {.+} wird Dir ab nun gehorchen! Harhar!} {
    #NOP /* */
}

#NOP /* Seelensammler */
#ACTION {{.+} macht sich nun auf die Suche nach Seelen.} {
    #VAR nekro_spells[4] {@swap_state{$nekro_spells[4];on}};
    #NOP #VAR nekro_spells[sammler][seelen] 0;
}

#ACTION {*** %+ hat sich gerade aufgeloest!} {
    #VAR nekro_spells[4] {@swap_state{$nekro_spells[4];off}};
    #NOP #ECHO {%1 hat $nekro_spells[sammler][seelen] Seelen gesammelt!};
}

#ACTION {* %+ hat eine Seele erbeutet!} {
    #NOP #MATH {nekro_spells[sammler][seelen]} {$nekro_spells[sammler][seelen] + 1}
}

#NOP { /**
 *	Fertigkeiten-Anzeige
 *
 */};
#ALIAS {fert} {
    #GAG {Zauber/Faehigkeit                    Guete                         Skillpunkte};
    #GAG {------------------------------------------------------------------------------};
    #ACTION {{^(.+?) \s+ (.+?) \s+ ([0-9\-]+)$}} {
        #IF { "%%4" == "%+1d" } {
            #VAR tmp_fert {<bba>[<ffd>%%4<bba>]<099>};
        };
        #ELSEIF { "%%4" == "%+2d" } {
            #VAR tmp_fert {<bba>[<fca>X<bba>]<099>};
        };
        #ELSE {
            #VAR tmp_fert {   };
        };
        #IF { "%%4" == "0" } {
            #VAR tmp_fert {<bba>[%%4]<099>};
        };
        #ECHO {%s %s%s}
            {$tmp_fert}
            {@dotfill{<ffd>%%2<099>;30}}
            {@dotfill_r{@nekro_fert{%%3}<099>;20}};
        #LINE GAG;
    } {5};

    prompt_func fert_ende;
    #SEND {fertigkeiten};
} {5};

#ALIAS {fert_ende} {
    #UNGAG {Zauber/Faehigkeit                    Guete                         Skillpunkte};
    #UNGAG {------------------------------------------------------------------------------};
    #UNACTION {{^(.+?) \s+ (.+?) \s+ ([0-9\-]+)$}};
} {5};

#FUNCTION {nekro_fert} {
    #SWITCH { "%0" } {
        #CASE { "{gut|sehr gut|wirklich toll|fast schon sehr gut|phantastisch gut}" } {
            #VAR result {<bca>%0}
        };
        #CASE { "{hervorragend|ausgezeichnet}" } {
            #VAR result {<bdb>%0}
        };
        #CASE {" {beinahe hoellisch|exzellent|meisterhaft}" } {
            #VAR result {<dea>%0}
        };
        #CASE { "{hoellisch|beinahe daemonisch|nahezu perfekt|perfekt}" } {
            #VAR result {<bfa>%0}
        };
        #CASE { "{daemonisch|absolut perfekt}" } {
            #VAR result {<dfc><388>%0}
        };
        #DEFAULT {
            #VAR result {<bba>%0}
        }
    };
    #RETURN $result
}

#NOP { /**
 *	@brief	Schadensmeldungen von 'kochendes Blut' in Kampfausgabe integrieren
 *
 */};
#ALIAS {blut_analyse}
{
    #VAR angreifer      @name_kuerzen{Du};
    #VAR opfer          @name_kuerzen{%1};
    setze_angriff_spell {koch. Blut};
    #VAR angriff[art]   Unheilig;
    #VAR angriff[hoehe] %2;

    #FORMAT {blut_schadenausgabe} {%s%s|%s%s %s%s%s %.5s %s%s}
        {@kampf_pad{9;1;l;$angriff[art]}}
        {$angriff[art]}
        {$angriff[spell]}
        {@kampf_pad{12;1;r;$angriff[spell]}}
        {$angreifer}
        {@kampf_pad{39;2;m;$angreifer;$opfer}}
        {$opfer}
        {@zeige_angriff_effekte{$angriff[effekte]}}
        {@kampf_pad{11;1;l;$angriff[hoehe]}}
        {$angriff[hoehe]};

    #UNVAR angriff;
    #VAR angriff[killer] Du;
    setze_angriff_spell;
    setze_angriff_effekte;
    #VAR roomspell {};

    #LINE GAG
}

#NOP { /**
 *	@brief	Kochendes Blut
 *
 */};
#ACTION {^Mit einem Ruck deutest Du auf %+! %+} {
    #LINE GAG @line_gag_check{%2;Harhar\!};
    timer_start spellpause 5
} {5};

#ACTION {^  %+ windet sich vor Schmerzen!} {
  blut_analyse {%1} {<dbc>windet sich<099>};
  #ECHO {$blut_schadenausgabe}
}

#ACTION {^  %+ windet sich und schreit vor Schmerz!} {
  blut_analyse {%1} {<dbc>wind/schrei<099>};
  #ECHO {$blut_schadenausgabe}
}

#ACTION {^  %+ schreit vor Schmerzen!} {
  blut_analyse {%1} {<dbc>schreit<099>};
  #ECHO {$blut_schadenausgabe}
}

#NOP { /**
 *	@brief  Fluch der Klinge
 *
 */};
#ACTION {^Deine leisen Worte bringen die Kaelte auf den schwarzen Stahl...} {
    timer_start vorbereitung 9
} {5};

#ACTION {^Die fokussierte Macht des Boesen faehrt in %+} {
    timer_start spellpause 40
} {5};

#NOP { /**
 *	@brief	Auge des Khaza-Dhunaar
 *
 */};
#ACTION {^Du hebst Deine Arme und rufst: Tur Yag'oth Zztta Khaza-Doom!} {
    #NOP timer_start vorbereitung 2
} {5};

#ACTION {^Ein Spalt zu den Niederhoellen oeffnet sich!} {
    timer_start spellpause 4
} {5};

#NOP { /**
 *	@brief	Schleier des Val-Tkro-Tthar
 *
 */};
#ACTION {^Du beginnst leise die geheimen Riten des Val-Tkro-Tthar zu singen!}
{
    timer_start vorbereitung 2
} {5};

#ACTION {^Mit einem Schlag kuehlt die Umgebung ab, und ein dunkler Schleier umgibt Dich.} {
    #VAR nekro_spells[2] {@swap_state{$nekro_spells[2];on}};
    timer_start spellpause 9
}

#ACTION {^Wie aus dem Nichts umgibt Dich ein dunkler Schleier.} {
    #VAR nekro_spells[2] {@swap_state{$nekro_spells[2];on}};
    timer_start spellpause 9
}

#ACTION {{.+} Dein dunkler Schleier verfliegt!} {
    #VAR nekro_spells[2] {@swap_state{$nekro_spells[2];off}};
}

#NOP { /**
 *	@brief	Beruehrung von Zum-Uul
 */};
#ACTION {^Mit aller Macht beschwoerst Du die Macht Zum-Uuls herbei!} {
    #VAR nekro_spells[1] {@swap_state{$nekro_spells[1];on}};
    timer_start spellpause 12
}

#NOP { /**
 *	@brief	Hauch der Verwesung
 *
 *  Ein fauliger Geruch breitet sich langsam aus.
 *
 *  Als der Hauch der Verwesung von dem Kleinkorporal verfliegt bleiben
 *  nekrotische Geschwuere, morsche Knochen und eitrige Wunden zurueck.
 *
 */};
#GAG {^Du rufst: Ika-haar Turz'waa tortem Gr'traf!};
#ACTION {%+ schreit gequaelt, als kleine Maden sich in %+} {
    setze_angriff_spell verwesung;
    #LINE GAG @line_gag_check{%2;\!}
} {5};

#ACTION {^Du murmelst leise: Nes'ser f'hic! Dne l'los Ned dam!} {
    timer_start spellpause 6
} {5};

#NOP { /**
 *	@brief	Geifer der Verdammten
 *
 *  Du formst Deine Haende zu einer Kugel.
 *
 */};
#GAG {Dein purer Hass formt in Deinen Haenden eine Kugel aus nachtschwarz}
#ACTION {^schaeumenden Geifer, die Du auf %+} {
    setze_angriff_spell geifer;
    #LINE GAG @line_gag_check{%1;\.}
} {5};

#ACTION {^Der daemonische Geifer explodiert.} {
    setze_room_spell geifer;
    #LINE GAG
} {5};

#NOP { /**
 *	@brief	Gekreische der Todesfee
 *
 *  Du reisst Deinen Mund ganz weit auf.
 *
 */};
#ACTION {^Du kreischt unirdisch hohe Toene, eine Kakophonie des Grauens!} {
    setze_room_spell gekreische;
    #LINE GAG
} {5};

#NOP { /**
 *	@brief	Strahlen der Dunkelheit
 *
 */};
#ACTION {^Aus Deinen Augen schiessen %+ dunkle Strahlen der Dunkelheit auf%+} {
    setze_room_spell dunkelstrahl;
    #LINE GAG @line_gag_check{%2;vernichten\!}
} {5};

#NOP { /**
 *	@brief	Biss des Vampirs
 *
 *  HARHAR! Mit deinem Biss hast du das Herzblut deines Feindes getrunken!
 *  Unheilige Kraft durchstroemt dich.
 *
 *  Verflucht noch einmal! Die unheilige Kraft entweicht aus deinen Adern!
 *
 */};
#ACTION {Du beisst %+ in die Halsschlagader und saugst%+} {
    setze_angriff_spell vampirbiss;
    #LINE GAG @line_gag_check{%2;\.}
} {5};

#NOP { /**
 *	@brief	Mysterium von Yok-njarg-Zirm
 *
 *  Du versenkst Deinen Geist in die Nebel der Ungewissheit ...
 *
 */};
#ACTION {^Du beschwoerst das Mysterium von Yok-njarg-Zirm...} {
    timer_start spellpause 20
} {5};

#NOP { /**
 *	@brief	Pforte ins Totenreich
 *
 *  Du hebst beschwoerend Deine Arme in die Hoehe ...
 *
 */};
#ACTION {^Du loest die Verbindung zwischen Geist und Koerper und sendest den Geist in das}
{
    timer_start vorbereitung 4
} {5};

#ACTION {^Du oeffnest eine Pforte ins Totenreich. Aus den unendlichen Tiefen des}
{
    timer_start spellpause 7
} {5};

#NOP { /**
 *	@brief	Ruf der Ghoule
 *
 */};
#ACTION {^Du denkst an Ghoule und Leichenfresser.} {
    timer_start vorbereitung 3
} {5};

#ACTION {^Mit Deinem Geist lenkst Du die Leichenduefte in die finstersten Gruefte und} {
    timer_start spellpause 7
} {5};

#NOP { /**
 *	@brief	Unterwerfung der Toten
 *
 */};
#ACTION {^Du rufst: Iktam Yy'rlem port Hla'tur! Shihaya'zzad ter Ll'artez!} {
    timer_start vorbereitung 3
} {5};

#ACTION {%+ Wille ist gebrochen und %+ wird Dir ab nun%*} {
    timer_start spellpause 14
} {5};

#NOP { /**
 *	@brief	Blutpakt der Verdammten
 *
 */};
#ACTION {^Du spuerst das Blut der Lebenden pulsieren...} {
    timer_start vorbereitung 5
} {5};

#ACTION {%+ scheint nun mehr geschuetzt zu sein und sich} {
    timer_start spellpause 14
} {5};

#NOP { /**
 *	@brief	Dunkler Pakt
 *
 */};
#ACTION {^Du bereitest Dich auf den dunklen Pakt vor.} {
    timer_start vorbereitung 5
} {5};

#ACTION {%+ sieht viel maechtiger und staerker aus!} {
    timer_start spellpause 14
} {5};

#NOP { /**
 *	@brief	Anrufung des Kharr-zzteth
 *
 */};
#ACTION {^Du reckst die Arme zum Himmel auf und beschwoerst die Macht der totalen} {
    timer_start vorbereitung 5
} {5};

#ACTION {^Du rufst: Nekratos ay'hala! Shiyaya'zzad 'na Astiphel! Nya al H'rrad!} {
    timer_start spellpause 8
} {5};

#CLASS nekromanten CLOSE

#NOP {
hilfe gilde nekromanten Auge_des_Khaza-dhunaar      5   1   2   4
hilfe gilde nekromanten Beruehrung_von_Zum-uul      10  10
hilfe gilde nekromanten Biss_des_Vampirs            10  5   10
hilfe gilde nekromanten Blutpakt_der_Verdammten     10  5   10
hilfe gilde nekromanten Dunkler_Pakt                10  4   8
hilfe gilde nekromanten Empathie_der_Schatten       10  10
hilfe gilde nekromanten Geifer_der_Verdammten       10  5
hilfe gilde nekromanten Gekreische_der_Todesfee     10  8
hilfe gilde nekromanten Hauch_der_Verwesung         10  8
hilfe gilde nekromanten Kochendes_Blut              10  10
hilfe gilde nekromanten Schleier_des_Val-tkro-tthar 10  10
hilfe gilde nekromanten Schrecken_der_Finsternis    10
hilfe gilde nekromanten Strahlen_der_Dunkelheit     10  10
hilfe gilde nekromanten Unterwerfung_der_Toten      10  1   3   5   8   10

Untote:

* Ein Vampir hat eine Seele erbeutet!
*** Ein Vampir hat sich gerade aufgeloest!
Der Vampir loest sich in seine stinkenden Einzelteile auf.

Eine daemonische Dunkelheit geht ploetzlich von einem Silberwolf aus!

Ein Wachsilberwolf torkelt geblendet umher!

Der Silberwolf wird mitten ins Herz getroffen und ist nun anfaelliger gegen
nekromantische Rituale!

Der Silberwolf ist nun nicht mehr anfaellig gegen nekromantische Rituale.

Mit einem Ruck deutest Du auf einen Grizzly! Bei den Niederhoellen! Bald wird
er gar sein! Harhar!
}