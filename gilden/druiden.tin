#NOP Report f√ºr Druidenzirkel
/**
 *  TODO:
 *  - an Druiden anpassen
 * z.B. -----------[----------]--(frech)------- LPs: 277/277(0), MPs: 154/285(+1)
 *      -+\[[^\]]+\]-+\([ a-z]+\)-+ LPs: [0-9]+/[0-9]+\([\-+0-9]+\), MPs: [0-9]+/[0-9]+\([\-+0-9]+\)
 */

#CLASS druiden KILL
#CLASS druiden OPEN

#ECHO {<029>-- <188>%s<288> geladen<099>} {gilden/druiden.tin}

#VAR color_inactive     {<g02>}
#VAR color_active       {<099>}
#VAR color_spell_ende   {<339>}
#VAR gilden_report_cmd  {dstatus}

#LIST status_items CREATE {status_session;status_hp;status_mp;status_align;status_druid;status_elementar;status_halsband}

#NOP { /**
 *  @fn status_druid
 *  @brief  setzt Block mit aktiven Spells fuer die Statuszeile
 *
 *  @return     formatierter String
 */};
#FUNCTION {status_druid} {
    #LIST result COPY druid_spells;
    #LIST result COLLAPSE {};
    #FORMAT {result} {%s<099>|} {$result}
}

#NOP { /**
 *  @fn status_elementar
 *  @brief  setzt Block mit aktivem Begleiter fuer die Statuszeile
 *
 *  @return     formatierter String
 */};
#FUNCTION {status_elementar} {
    #LIST result COPY druid_elementar;
    #LIST result COLLAPSE {};
    #FORMAT {result} {%s<099>|} {$result}
}

#EVENT {VARIABLE UPDATED druid_spells} {
    #VAR status_druid {@status_druid{}};
    #LIST status_items FIND status_druid tmp_status_index;
    #VAR statusline_list[$tmp_status_index] {$status_druid};
    draw_status
}

#EVENT {VARIABLE UPDATED druid_elementar} {
    #VAR status_elementar {@status_elementar{}};
    #LIST status_items FIND status_elementar tmp_status_index;
    #VAR statusline_list[$tmp_status_index] {$status_elementar};
    draw_status
}

#VAR druid_spells {
    {1} {<g02>A}
    {2} {<g02>P}
    {3} {<g02>H}
    {4} {<g02>S}
    {5} {<g02>H}
    {6} {<g02>T}
    {7} {<g02>N}
}

#VAR druid_elementar {
    {1} {<g02>E}
    {2} {<g02>K}
    {3} {<g02>V}
}

#ACTION {%!*[%!*]-(%!*)%!* LPs: %+1..d/%+1..d(%*%+1..d), MPs: %+1..d/%+1..d(%*%+1..d)} {
    #VAR check_diff {0};
    #LINE GAG
} {5}

#NOP /* Aendern, da auch Elementare betroffen sind.*/
#ACTION {^Du konzentrierst Dich auf das Element {Luft|Feuer|Erz|Eis|Wasser} und {beruehrst|blaest|formst|machst|erstarrst}} {
    #SWITCH { "%1" } {
        #CASE { "Eis" } {
            #REPLACE druid_spells[1] {%.$} {I}
        };
        #CASE { "Erz" } {
            #REPLACE druid_spells[1] {%.$} {E}
        };
        #CASE { "Feuer" } {
            #REPLACE druid_spells[1] {%.$} {F}
        };
        #CASE { "Luft" } {
            #REPLACE druid_spells[1] {%.$} {L}
        };
        #CASE { "Wasser" } {
            #REPLACE druid_spells[1] {%.$} {W}
        }
    };
    #VAR druid_spells[1] {@swap_state{$druid_spells[1];on}}
}

#ACTION {^Deine Schadensaura '%+' wird nun langsam schwaecher.} {
    #FORMAT {druid_spells[1]} {%l} {$druid_spells[1]}
}

#ACTION {^Deine Aura '%+' laesst langsam nach und verursacht nun keinen Schaden} {
    #REPLACE druid_spells[1] {%+%.$} {&1};
    #FORMAT {druid_spells[1]} {%si} {$druid_spells[1]}
}

#ACTION {^Deine Schadensaura '%+' erlischt nun vollends.} {
    #REPLACE druid_spells[1] {%+%.$} {&1A};
    #VAR druid_spells[1] {@swap_state{$druid_spells[1];off}}
}

#ACTION {Dein{e?} {Erdbeben|Feuersturm|Hagelschlag|Sturmflut|Windhose} {schuettelt|prasselt|bricht|huellt|wirbelt}{ ?(auf|ueber)?} {.+} {durch|nieder|herein|ein|herum}!}
{
    #NOP #ECHO {t: %s.} {%2};
    setze_angriff_spell %2;
    #LINE GAG
}

#ACTION {{.+}faellt zu Boden.}
{
    #NOP
    /**
     *  Problem: Wird nach der Treffermeldung ausgegeben, kann momentan nicht mit
     *  der Treffermeldung zusammmengefasst werden. (-> Lookahead)
     */;
    #NOP #VAR effekt5 <ffa>B<099>;
    #LINE GAG
}

#ALIAS {spell_ende} {
    timer_end %2;
    #ECHO {%c%s%c (%t)} {$color_spell_ende} {%1} {<099>} {{%M:%S}{$timer[%2][dauer]}};
} {5};

#ACTION {^Durch Deine Konzentration formen Deine Gedanken einen Panzer aus Eis, Luft,} {
    #VAR druid_spells[2] {@swap_state{$druid_spells[2];on}};
    timer_start panzer
}

#ACTION {^Dein Elementarpanzer loest sich in die einzelnen Elemente auf.} {
    #VAR druid_spells[2] {@swap_state{$druid_spells[2];off}};
    spell_ende {%0} panzer;
    #LINE GAG
}

#ACTION {^Du reibst Deine Haende aneinander...} {
    #VAR druid_spells[3] {@swap_state{$druid_spells[3];on}};
    timer_start hand
}

#ACTION {^Deine Haende werden wieder normal.} {
    #VAR druid_spells[3] {@swap_state{$druid_spells[3];off}};
    spell_ende {%0} hand;
    #LINE GAG
}

#ACTION {^Dein Erzelementar verfluessigt sich und bildet einen Schutzpanzer um Dich!} {
    #VAR druid_spells[4] {@swap_state{$druid_spells[4];on}};
    timer_start schutz
}

#ACTION {^Der Schutz des Erzelementars erlischt!} {
    #VAR druid_spells[4] {@swap_state{$druid_spells[4];off}};
    spell_ende {%0} schutz;
    #LINE GAG
}

#ACTION {^Das Feuerelementar bildet eine feurige Aura um Dich.} {
    #VAR druid_spells[6] {@swap_state{$druid_spells[6];on}};
    timer_start temp
}

#ACTION {^Das Eiselementar bildet einen eisblauen Schimmer um Dich.} {
    #VAR druid_spells[6] {@swap_state{$druid_spells[6];on}};
    timer_start temp
}

#ACTION {^Dein elementarer Temperaturschutz erlischt!} {
    #VAR druid_spells[6] {@swap_state{$druid_spells[6];off}};
    spell_ende {%0} temp;
    #LINE GAG
}

#ACTION {^Das Feuerelementar bildet eine feuerrote Schutzhuelle um Dich.} {
    #VAR druid_spells[5] {@swap_state{$druid_spells[5];on}};
    timer_start huelle
}

#ACTION {^Das Eiselementar bildet eine eisblaue Schutzhuelle um Dich.} {
    #VAR druid_spells[5] {@swap_state{$druid_spells[5];on}};
    timer_start huelle
}

#ACTION {^Deine Schutzhuelle faellt zusammen.} {
    #VAR druid_spells[5] {@swap_state{$druid_spells[5];off}};
    spell_ende {%0} huelle;
    #LINE GAG
}

#ACTION {^Ein%!* %+1..welementar gehorcht Dir nun!} {
    #SWITCH { "%1" }     {
        #CASE { "Feuer" } {
            #REPLACE druid_elementar[1] {%.$} {F}
        };
        #CASE { "Wasser" } {
            #REPLACE druid_elementar[1] {%.$} {W}
        };
        #CASE { "Erz" } {
            #REPLACE druid_elementar[1] {%.$} {E}
        };
        #CASE { "Luft" } {
            #REPLACE druid_elementar[1] {%.$} {L}
        };
        #CASE { "Eis" } {
            #REPLACE druid_elementar[1] {%.$} {I}
        };
        #CASE { "Humus" } {
            #REPLACE druid_elementar[1] {%.$} {H}
        };
    };
    #VAR druid_elementar[1] {@swap_state{$druid_elementar[1];on}}
}

#ACTION {^Das%!* %+1..welementar sagt: Meister, ich werde Euch jetzt im Kampf %+!} {
    #NOP #VAR kampf[helfer] %2;
    #IF { "%2" == "unterstuetzen" } {
        #VAR druid_elementar[2] {@swap_state{$druid_elementar[2];on}};
    };
    #ELSEIF { "%2" == "verteidigen" } {
        #VAR druid_elementar[3] {@swap_state{$druid_elementar[3];on}};
    }
} {5.0}

#ACTION {^Das%!* %+1..welementar sagt: Meister, ich werde Euch jetzt} {
    #LINE ONESHOT {
        #ACTION {Das%!* %+1..welementar sagt: %!*{unterstuetzen|verteidigen}!} {
            #IF { "%%2" == "unterstuetzen" } {
                #VAR druid_elementar[2] {@swap_state{$druid_elementar[2];on}};
            };
            #ELSEIF { "%%2" == "verteidigen" } {
                #VAR druid_elementar[3] {@swap_state{$druid_elementar[3];on}};
            };
            #NOP #VAR kampf[helfer] %2;
        };
    };
} {5.1}

#NOP { /**
 *  @brief  Ein Elementar loest sich auf.
 *
 *  @todo   Soll nur nach Befehl triggern.
 *  @todo   als kampfhelfer austragen
 */};
#ACTION {^Das%!* %+1..welementar {loest sich auf|zerfliesst|verschwindet im Boden}.} {
    #REPLACE druid_elementar[1] {%.$} {E};
    #VAR druid_elementar[1] {@swap_state{$druid_elementar[1];off}};
    #VAR druid_elementar[2] {@swap_state{$druid_elementar[2];off}};
    #VAR druid_elementar[3] {@swap_state{$druid_elementar[3];off}}
}

#ACTION {^Ploetzlich bricht die Erde auf, und ein %*Erzelementar entsteht} {
    setnospell 12
}

#ACTION {^Wie aus dem Nichts entsteht ein %*Wasserelementar.} {
    setnospell 14
}

#ACTION {^Ploetzlich manifestiert sich eine Gestalt aus reinstem Feuer,} {
    setnospell 10
}

#ACTION {^Ploetzlich faellt die Temperatur rapide ab und} {
    setnospell 12
}

#ACTION {^Wie aus dem Nichts erscheint ein kleiner Feuerwusel, der Dir von nun an folgt} {
    setnospell 2;

}

#ALIAS {setnospell}
{
    #VAR nospell %0;
    #TICKER {nospell}
    {
        #FORMAT nospell {%+2m} {$nospell - 1};
        #IF {!$nospell}
        {
            #VAR nospell { -};
            draw_status;
            #UNTICKER {nospell}
        };
        #ELSE
        {
            draw_status
        }
    } {1}
}

#ALIAS {defokus} {
    deaktiviere fokus;
    #VAR fokus {}
}

#ACTION {^Du feuerst einen elementaren %+pfeil auf %!+ ab.$} {
    setze_angriff_spell %1pfeil;
    #LINE GAG
}

#ACTION {^Du wirfst einen elementaren Feuerball nach %+} {
    setze_angriff_spell feuerball;
    #LINE GAG
}

#ACTION {^Du staerkst Deine Gedanken, laesst sie hart wie Fels werden. Aus Deinen}
{
    setze_angriff_spell erzstrahl;
    #LINE GAG;
    #GAG {{^.+$}};
    #ACTION {{^.*zu!$}}
    {
        #LINE GAG;
        #UNGAG {{^.+$}};
        #UNACTION {{^.*zu!$}}
    }
}

#ACTION {^Du konzentrierst Deine Gedanken auf Deine Haende, in denen sich augenblicklich}
{
    setze_angriff_spell windstoss;
    #LINE GAG;
    #GAG {{^.+$}};
    #ACTION {{^.*zuschiessen!$}}
    {
        #LINE GAG;
        #UNGAG {{^.+$}};
        #UNACTION {{^.*zuschiessen!$}}
    }
}

#ACTION {^In Deinen zur Halbschale geoeffneten Haenden manifestiert sich elementar}
{
    setze_angriff_spell wasserstrahl;
    #LINE GAG;
    #GAG {{^.+$}};
    #ACTION {{^.*zuzuschiessen!$}}
    {
        #LINE GAG;
        #UNGAG {{^.+$}};
        #UNACTION {{^.*zuzuschiessen!$}}
    }
}

#ACTION {^Deine Haende werden ploetzlich von einer Frostschicht umgeben, als Du das}
{
    setze_angriff_spell froststrahl;
    #LINE GAG;
    #GAG {{^.+$}};
    #ACTION {{^.*wirfst!$}}
    {
        #LINE GAG;
        #UNGAG {{^.+$}};
        #UNACTION {{^.*wirfst!$}}
    }
}

#ACTION {^Du konzentrierst Dich darauf, %* zu beherrschen.}
{
    #LOCAL aktive_session @ats{$sessionname};
    #IF {{$aktive_session} != {$sessionname}}
    {
        #ats {#ECHO {Eldar scheint sich auf %1 zu konzentrieren.}}
    }
}

#NOP { /**
 *	@todo   unabhaengig von charnamen, nur innerhalb Gruppe
 */};
#ACTION {^Du musst Dich erst wieder konzentrieren.}
{
    #ECHO {%c%s} {bold yellow} {%0};
    #LOCAL aktive_session @ats{$sessionname};
    #IF {{$aktive_session} != {$sessionname}}
    {
        #LOCAL text {%0};
        #REPLACE text {Du musst Dich} {Eldar muss sich};
        #ats {#ECHO {%c%s} {yellow} {$text}}
    };
    #LINE GAG
}

#ACTION {%* erholt sich von Deinem {Eis|Erz|Feuer|Luft|Wasser}pfeil.}
{
    #ECHO {%c%s} {bold yellow} {%0};
    #LOCAL aktive_session @ats{$sessionname};
    #IF {{$aktive_session} != {$sessionname}}
    {
        #LOCAL text {%0};
        #REPLACE text {Deinem} {Eldars};
        #ats {#ECHO {%c%s} {bold yellow} {$text}}
    };
    #LINE GAG
}

#NOP { /**
 *	@todo   unabhaengig von charnamen, nur innerhalb Gruppe
 */};
#ACTION {%+ wehrt Deinen Zauber ab.$} {
    setze_angriff_spell;
    #LOCAL aktive_session @ats{$sessionname};
    #IF {{$aktive_session} != {$sessionname}}
    {
        #ats
        {
            #ECHO {%cEldars Zauber wurde abgewehrt.} {yellow};
            setze_angriff_spell
        }
    }
}

#ACTION {^Anvar sagt: %+ beherrschst Du %+.} {
    #ECHO {%s %s} {@dotfill{@druid_fert{%1}<099>;40}} {@druid_abil{%2}<099>};
    #LINE GAG
} {5.1}

#ACTION {^Anvar sagt: Die Fertigkeit %+ beherrschst Du %+.} {
    #ECHO {%s %s} {@dotfill{@druid_fert{%1}<099>;40}} {@druid_abil{%2}<099>};
    #LINE GAG
} {5.0}

#ACTION {^Anvar sagt: Den %+ beherrschst Du %+.} {
    #FORMAT {tmp_fert} {%n} {%1};
    #ECHO {%s %s} {@dotfill{@druid_fert{$tmp_fert}<099>;40}} {@druid_abil{%2}<099>};
    #LINE GAG
} {5.0}

#ACTION {^Anvar sagt: Insgesamt betrachtet bist Du %+.} {
    #ECHO {%s %s} {@dotfill{<ffd>Insgesamt<099>;40}} {@druid_abil{%1}<099>};
    #LINE GAG
}

#FUNCTION {druid_abil} {
    #SWITCH { "%0" } {
        #CASE { "phantastisch gut" } {
            #VAR result <bca>%0
        };
        #CASE { "{ausgezeichnet|hervorragend|exzellent}" } {
            #VAR result <bdb>%0
        };
        #CASE { "{beinahe meisterhaft|meisterhaft}" } {
            #VAR result <dea>%0
        };
        #CASE { "{nahezu perfekt|perfekt}" } {
            #VAR result <bfa>%0
        };
        #CASE { "absolut perfekt" } {
            #VAR result <dfc><388>%0
        };
        #DEFAULT {
            #VAR result <bba>%0
        }
    };
    #RETURN $result
}

#FUNCTION {druid_fert}
{
    #IF { "%0" == "{Behandle|Humuselementar|Laehme|Schaetz|Erkenne}" } {
        #VAR result <228>%0
    };
    #ELSEIF { "%0" == "Feuer%+1..w" } {
        #VAR result <218>%0
    };
    #ELSEIF { "%0" == "{Eis|Frost|Hagel}%+1..w" } {
        #VAR result <258>%0
    };
    #ELSEIF { "%0" == "{Luft|Wind}%+1..w" } {
        #VAR result <268>%0
    };
    #ELSEIF { "%0" == "Er{d|z}%+1..w" } {
        #VAR result <238>%0
    };
    #ELSEIF { "%0" == "{Sturm|Wasser}%+1..w" } {
        #VAR result <248>%0
    };
    #ELSE {
        #VAR result <199>%0
    };
    #RETURN $result
}

#ACTION {^Das %+elementar %+ eine Attacke fuer Dich ab!$} {
    setze_angriff_effekte 1 E;
    #LINE GAG
}

#CLASS druiden CLOSE

#NOP { /**
 *  Du erkennst eine Schwaeche deines Gegners. Rasch formt das elementare Wasser
 *  in dir eine Struktur, um diese auszunutzen.
 *
 *  Ein Steinbrocken manifestiert sich aus Sand und beginnt um Eldar zu schweben.
 *
 *	Der Steinblock faengt den Treffer ab.
 *
 *  Der Oger-Arbeiter verletzt sich an den Eiszapfen deines Elementarpanzers.
 *
 *  Die Kraft des Sturms faehrt in deine Gleider. Wie Blitz, Donner und Sturm
 *  faehrst du auf deine Feinde nieder!
 */};