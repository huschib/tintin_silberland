#NOP
/** paladine.tin
 * TODO:
 * - Waffen und Schilde in Variablen speichern und auf diese in Spells zugreifen
 */

#CLASS {paladine} {kill}
#CLASS {paladine} {open}

#ECHO {<029> -> <188>%s<288> geladen<099>} {gilden/paladine.tin}

#VAR gilden_report_cmd  {pstatus}
#VAR color_spell_ende   {<339>}
#VAR color_inactive     {<g02>}
#VAR color_active       {<099>}
#VAR color_learn        {lime}
#VAR color_not_learn    {red}

#HIGHLIGHT {^Paladin! Du lernst aus Deinem Fehler dazu!} {$color_learn}
#HIGHLIGHT {^Auch wenn Du versagt hast, lernst Du aus Deinem Fehler, Paladin!} {$color_learn}
#HIGHLIGHT {^Du dankst Dusty fuer den Fehlschlag und lernst fuer die Zukunft dazu, Paladin!} {$color_learn}
#HIGHLIGHT {^Dein Fehler verhilft Dir '%1' weiter zu lernen, Paladin!} {$color_learn}
#HIGHLIGHT {^Trotz Deines Fehlschlages lernst Du aus Deinem Fehler dazu, Paladin!} {$color_learn}

#HIGHLIGHT {^Du lernst nichts mehr, Paladin! Du musst erst besser werden!} {$color_not_learn}

#HIGHLIGHT {^Dein Heiligenschein verblasst.} {$color_spell_ende}
#HIGHLIGHT {^Der Waffensegen von %+ erlischt!} {$color_spell_ende}
#HIGHLIGHT {^Der Ruestungssegen erlischt!} {$color_spell_ende}
#HIGHLIGHT {^Deine Schildparade laesst{( langsam)*} nach{(, Paladin)*}!} {$color_spell_ende}

#LIST status_items CREATE {status_session;status_hp;status_mp;status_align;status_pala;status_zustand;status_halsband}

#ALIAS {status_var_update} {
    #VAR %1 {@{%1}{}};
    #LIST status_items FIND %1 tmp_status_index;
    #VAR statusline_list[$tmp_status_index] {${%1}};
    draw_status    
}

#EVENT {VARIABLE UPDATED pala_spells} {
    status_var_update status_pala
}

#EVENT {VARIABLE UPDATED pala_zustand} {
    status_var_update status_zustand
}

#VAR pala_spells {
    {1} {<g02>D}
    {2} {<g02>S}
    {3} {<g02>H}
    {4} {<g02>R}
    {5} {<g02>W}
    {6} {<g02>T}
    {7} {<g02>G}
    {8} {<g02>S}
    {9} {<g02>E}
}

#NOP { /**
 *	@brief	Preport Level 1
 */};
#ACTION {^%.-%.-%.-%.-%.-%.-%.-%.-%.-[%+]--(%+)%+ LPs: %+1..d(%+), MPs: %+1..d(%+)} {

    #NOP {
    pala_check_aktiv %1 1;
    pala_check_aktiv %2 2;
    pala_check_aktiv %3 3;
    pala_check_aktiv %4 4;
    pala_check_aktiv %5 5;
    pala_check_aktiv %6 6;
    pala_check_aktiv %7 7;
    pala_check_aktiv %8 8;
    pala_check_aktiv %9 9;
    #NOP };
    #FORMAT {align} {%p} {%11};
    #IF {"$status[align]" != "%11"}
    {
        #FORMAT {status[align]} {%p} {%11}
    };
    #VAR pala_zustand %10;

    #IF {!@_an_aus{config[report]}} {
        #LINE GAG
    }
}

#ALIAS {pala_check_aktiv}
{
    #IF {"%1" == "x"}
    {
        #VAR pala_spells[%2][aktiv] 0
    };
    #ELSE
    {
        #VAR pala_spells[%2][aktiv] 1
    }
}

#NOP { /**
 *  @fn status_pala
 *  @brief  setzt Block mit aktiven Spells fuer die Statuszeile
 *
 *  @return     formatierter String
 */};
#FUNCTION {status_pala} {
    #LIST result COPY pala_spells;
    #LIST result COLLAPSE {};
    #FORMAT {result} {%s<099>|} {$result}
}

#NOP { /**
 *  @fn     status_zustand
 *  @brief  setzt Block mit Zustaenden fuer die Statuszeile
 *
 *  @return formatierter String
 */};
#FUNCTION {status_zustand} {
    #FORMAT result {%+9s|} {$pala_zustand}
}

#ALIAS {setnospell}
{
    #NOP {
    #FORMAT nospell {%+2m} {%0};
    #TICKER {nospell} {
        #FORMAT nospell {%+2m} {$nospell - 1};
        #IF {!$nospell} {
            #VAR nospell {  };
            draw_status;
            #UNTICKER {nospell};
        } {
            draw_status;
        };
    } {1};
    #NOP }
}

#NOP /* Paladinfertigkeiten */

#ACTION {^Ayla sagt: %+1..w%+1..s - %+.} {
    #ECHO {%s %s} {@dotfill{<ffd>%1<099>;40}} {@pala_fert{%3}<099>};
    #LINE GAG
}

#ACTION {^Ayla sagt: Das Kaempfen mit dem Schwert beherrschst Du %+.} {
    #ECHO {%s %s} {@dotfill{<ffd>Schwertkampf<099>;40}} {@pala_fert{%1}<099>};
    #LINE GAG
}

#ACTION {^Ayla sagt: In Summe gesehen bist Du %+.} {
    #ECHO {%s %s} {@dotfill{<ffd>In Summe<099>;40}} {@pala_fert{%1}<099>};
    #LINE GAG
}

#FUNCTION pala_fert {
    #SWITCH { "%0" } {
        #CASE { "{gut|sehr gut|wie ein Burgwaechter|fast schon sehr gut|phantastisch gut}" } {
            #VAR result <bca>%0
        };
        #CASE { "{wie ein hochdekorierter Veteran|wie ein hochbegabter Meister|hervorragend|ausgezeichnet}" } {
            #VAR result <bdb>%0
        };
        #CASE { "{beinahe engelgleich|engelgleich|meisterhaft}" } {
            #VAR result <dea>%0
        };
        #CASE { "{legendaer|beinahe goettlich|nahezu perfekt|perfekt}" } {
            #VAR result <bfa>%0
        };
        #CASE { "{goettlich|absolut perfekt}" } {
            #VAR result <dfc><388>%0
        };
        #DEFAULT {
            #VAR result <bba>%0
        }
    };
    #RETURN $result
}

#ACTION {^Dusty segnet Deine Attacke!} {
    setze_angriff_effekte 2 D;
    #LINE GAG
}

#ACTION {^Du reckst Dein Schild gen Himmel!} {
    #VAR pala_spells[2] {@swap_state{$pala_spells[2];on}}
}

#ACTION {^Deine Schildparade laesst{( langsam)*} nach{(, Paladin)*}!}
{
    #VAR pala_spells[2] {@swap_state{$pala_spells[2];off}}
}

#ACTION {^Du parierst die Attacke{( ein wenig)*} mit Deinem Schild!} {
    setze_angriff_effekte 1 S;
    #LINE GAG
}

#ACTION {^Ueber Deinem Haupt materialisiert sich ein goldener Heiligenschein!} {
    #VAR pala_spells[3] {@swap_state{$pala_spells[3];on}}
    #LINE GAG 2
}

#ACTION {^Dein Heiligenschein verblasst.} {
    #VAR pala_spells[3] {@swap_state{$pala_spells[3];off}}
}

#ACTION {^Du rufst: Herrin Dusty, in Deinem Namen weihe ich diese Ruestung,} {
    #VAR pala_spells[4] {@swap_state{$pala_spells[4];on}}
}

#ACTION {^Der Ruestungssegen erlischt!} {
    #VAR pala_spells[4] {@swap_state{$pala_spells[4];off}}
}

#ACTION {^Du rufst: Herrin Dusty, in Deinem Namen weihe ich Dir diese Waffe,} {
    #VAR pala_spells[5] {@swap_state{$pala_spells[5];on}}
}

#ACTION {^Der Waffensegen von %+ erlischt!} {
    #VAR pala_spells[5] {@swap_state{$pala_spells[5];off}}
}

#NOP /* Spell: Behandle */

#ALIAS {b} {
    ww_body;
    #VAR tmp garnok;
    #IF {%0}
    {
        #VAR tmp %0
    };
    #SEND {behandle $tmp};
    setnospell 14;
    ww_body
}

#ACTION {Du kniest nieder und betest zu Dusty!} {
    setnospell 8
}

#NOP /* Spell: Heiligengebet */

#ALIAS {hg} {
    ww_body waffe;
    #SEND {heiligengebet %0};
    ww_body waffe
}

#ALIAS {hgl} {
    hg leiche %0
}

#NOP /* Spell: Wundenblick */

#ALIAS {wb}
{
    #LIST wunden_blick CREATE {};
    #VAR wb_maxlen 0;
    #GAG {^{-+}$};
    #ACTION {%+1..s%+%s{ist|fuehlt|macht|machte|spuert|wird} {bei|noch|sich|schon|einen|in|die|bald|so|den} %+ {Gesundheit|drauf|gut|Eindruck|Zustand|schwinden|nahen|Ende|ueberleben}.}
    {
        #LIST wunden_blick ADD { };
        #FORMAT tmp {%L} {%%2};
        #IF {$tmp > $wb_maxlen}
        {
            #VAR wb_maxlen $tmp
        };
        #LIST wunden_blick[-1] ADD {%%2};
        #LIST wunden_blick[-1] ADD {%%6};
        #LINE GAG
    };
    prompt_func wb_ende;
    #SEND {wundenblick}
}

#ALIAS {wb_ende}
{
    #LOOP 1 &wunden_blick[] loop
    {
        #ECHO { %-${wb_maxlen}s  %s} {$wunden_blick[$loop][1]} {@tobar{$wunden_blick[$loop][2]}};
    };
    #UNGAG {^{-+}$};
    #UNACTION {%+1..s%+%s{ist|fuehlt|macht|machte|spuert|wird} {bei|noch|sich|schon|einen|in|die|bald|so|den} %+ {Gesundheit|drauf|gut|Eindruck|Zustand|schwinden|nahen|Ende|ueberleben}.};
    prompt_func stop
}

#FUNCTION {tobar}
{
    #NOP /* Zustaende vom Monokel */;

    #VAR res {};
    #SWITCH {"%0"}
    {
        #CASE {"bester"}                        {#VAR res @bar{100;20}};
        #CASE {"ausgezeichneter"}               {#VAR res @bar{100;20}};
        #CASE {"hervorragender"}                {#VAR res @bar{95;20}};
        #CASE {"sehr guter"}                    {#VAR res @bar{90;20}};
        #CASE {"guter"}                         {#VAR res @bar{85;20}};
        #CASE {"ordentlicher"}                  {#VAR res @bar{80;20}};
        #CASE {"recht gut"}                     {#VAR res @bar{75;20}};
        #CASE {"nicht mehr so"}                 {#VAR res @bar{70;20}};
        #CASE {"einen besseren"}                {#VAR res @bar{65;20}};
        #CASE {"einen deutlich besseren"}       {#VAR res @bar{60;20}};
        #CASE {"einen wesentlich besseren"}     {#VAR res @bar{55;20}};
        #CASE {"ziemlich angeschlagenen"}       {#VAR res @bar{50;20}};
        #CASE {"sehr angeschlagenen"}           {#VAR res @bar{45;20}};
        #CASE {"einem ziemlich schlechten"}     {#VAR res @bar{40;20}};
        #CASE {"einem sehr schlechten"}         {#VAR res @bar{35;20}};
        #CASE {"einem ueblen"}                  {#VAR res @bar{30;20}};
        #CASE {"einem sehr ueblen"}             {#VAR res @bar{25;20}};
        #CASE {"einem aeusserst bedenklichen"}  {#VAR res @bar{20;20}};
        #CASE {"Lebensenergie"}                 {#VAR res @bar{15;20}};
        #CASE {"das Ende"}                      {#VAR res @bar{10;20}};
        #CASE {"gut wie am"}                    {#VAR res @bar{5;20}};
        #CASE {"naechsten Treffer nicht"}       {#VAR res @bar{0;20}};
        #CASE {"von dieser"}                    {#VAR res @bar{0;20}}
    };
    #RETURN $res
}

#FUNCTION {bar}
{
    #NOP { /**
     *  @fn bar
     *  @brief Gibt einen gefuellten Balken mit Prozentangabe zurueck
     *
     *  @param  1   int, Fuellgrad in Prozentanteilen
     *  @param  2   int, Laenge des Balken in Spalten
     *  @return     str
     */};
    
    #MATH halfbar {(%2 - 4) / 2};
    #MATH fill {%2 * %1 / 100};
    #MATH lefthalfbar {%2 - $halfbar};
    
    #NOP /* zusaetzliches Prozentzeichen im Text kompensieren */;
    #IF {$fill >= $lefthalfbar}
    {
        #MATH fill {$fill + 1}
    };
    
    #FORMAT {percenttext} {%+3s%%} {%1};
    #FORMAT {bartext} {%+${halfbar}s%s%+${halfbar}s} { } {$percenttext} { };
    #FORMAT tmp_len {%L} $bartext;
    #IF {$tmp_len < $fill}
    {
        #VAR fill $tmp_len
    };
    #REGEXP {$bartext} {^{.{$fill}}{.*}}
    {
        #FORMAT bar {<091>%s<AAA>%s<099>} {&1} {&2};
    };
    #RETURN [$bar]
}

#NOP /* Spell: Waffensegen */

#ALIAS {ws}
{
    #IF {@_an_aus{waffen_schild[waffe][name]}}
    {
        #VAR tmp {$waffen_schild[waffe][name]}
    };
    #ELSE
    {
        #ECHO {Du hast noch keine Waffe gesetzt.};
        #RETURN
    };
    
    #IF {%0}
    {
        #VAR tmp {%0}
    };

    #SEND {waffensegen $tmp}
}

#NOP /* Spell: Gottheilung */

#ALIAS {frog} {
    gottheilung_body verfroschung %0
}

#ALIAS {taub} {
    gottheilung_body taubheit %0
}

#ALIAS {blind} {
    gottheilung_body blindheit %0
}

#ALIAS {gift} {
    gottheilung_body vergiftung %0
}

#ALIAS {sfluch} {
    gottheilung_body sprachfluch %0
}

#ALIAS {gottheilung_body} {
    #LINE MULTISHOT {4}     {
        prompt_func gag
    };
    ww_body;
    #VAR tmp mich;
    #IF { %2 } {
        #VAR tmp %2
    };
    #SEND {gottheilung $tmp von %1};
    ww_body
}

#CLASS {paladine} {close}

#NOP
/**
Du reckst Deinen Schwertarm gen Himmel und betest voller Inbrunst zu Dusty:

Du rufst: Herrin Dusty, durchdringe uns und fuelle uns mit Deinem Geist, dring
Du rufst: ein in unsere Glieder und gebe ihnen Kraft, dring ein in unsere
Du rufst: Herzen und gebe ihnen Mut, dring ein in unsere Seelen und gebe ihnen
Du rufst: Zorn! Mein Leben sei Dein, und den Leib des Feindes will ich Dir
Du rufst: opfern. Rotes Blut, heiliges Blut - Blut wasche rein - so sei es!

Du rufst: Es segne uns Dusty, die Herrin des Krieges! Es bewahre uns Dusty,
Du rufst: die Beherrscherin des Sturmes! Es erleuchte uns Dusty, die herrliche
Du rufst: Loewin des Kampfes! Es staerke uns die Kraft des gleissendes Stahls,
Du rufst: und unser Wille, der als ehernes Schild vor uns steht! Dein Wille, o
Du rufst: Herrin, sei unser Befehl!

Du rufst: Dir zu Ehren kaempfe und streite ich! Dir zu Ehren nur in Deinem
Du rufst: Namen! Dir zu Ehren ich leb', Dir zu Ehren ich sterb', Dir zu Ehren
Du rufst: bis in Ewigkeit!

Ayla sagt: Paladin, Du kannst hier folgende Zauber lernen: Erblicke(1),
Ayla sagt: Schaetz(1), Heiligenschein(2), Schildparade(2), Verbannung(3),
Ayla sagt: Behandle(3), Wundenblick(4), Gottheilung(4), Lokation(5),
Ayla sagt: Heiligengebet(5), Ruestungssegen(6), Waffensegen(7), Reparatur(7),
Ayla sagt: Meditation(8), Goetterschutz(9), Zielattacke(10), Entfluche(11),
Ayla sagt: Gottsegen(11), Talisman(12), Spiegelschild(13), Massenheilung(14),
Ayla sagt: Eifer(15).

Dusty segnet Deine Attacke!

Heiligenschein
Du kniest nieder und betest zu Dusty!


Fertigkeiten:
wie ein Druide - naemlich ueberhaupt nicht
fast ueberhaupt nicht
miserabel wie ein Vagabund
wie ein blutiger Anfaenger
wie ein junger Knappe
schlecht
maessig
mittelmaessig
unterdurchschnittlich wie ein Soeldner
durchschnittlich
ueberdurchschnittlich
gut
sehr gut
wie ein Burgwaechter
wie ein hochdekorierter Veteran
wie ein hochbegabter Meister
beinahe engelgleich
engelgleich
 */