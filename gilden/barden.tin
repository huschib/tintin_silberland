#NOP { /**
 *  @class  Bardengilde
 *  @brief
 */}

#CLASS barden KILL
#CLASS barden OPEN

#ECHO {<029>-- <188>%s<288> geladen<099>} {gilden/barden.tin}

#VAR color_spell_ende {<339>}
#VAR color_spell_warn {<ddb>}
#VAR gilden_report_cmd {bstatus}

#IF { @check_config{barden_buffen} } {
    #CLASS barden_buffen READ {gilden/barden_buffen.tin}
}

#HIGHLIGHT {^Du lernst aus deinen Misstoenen!} {lime}
#HIGHLIGHT {^Dank deiner Kunstfertigkeit ist das Lied gerade noch gelungen!} {lime}
#HIGHLIGHT {^Du verkoerperst das Heldentum von %+ jetzt etwas besser.} {lime}

#HIGHLIGHT {^Du kannst wieder hoeren!} {underscore}
#HIGHLIGHT {^Du kannst wieder sehen!} {underscore}
#HIGHLIGHT {^Du bist nicht mehr heiser.} {underscore}

#HIGHLIGHT {^Die Wirkung Deiner '%+' schwindet!} {$color_spell_ende}
#HIGHLIGHT {^Die Wirkung von '%+' wird bald enden.} {$color_spell_warn}
#HIGHLIGHT {^Die Wirkung der Zaubermelodie verfliegt...} {$color_spell_ende}
#HIGHLIGHT {^Deine Schutzmeldodie verklingt!} {$color_spell_ende}
#HIGHLIGHT {^Deine Aura loest sich auf!} {yellow}
#HIGHLIGHT {^Die Melodie von '%+' wird leiser.} {$color_spell_warn}
#HIGHLIGHT {^Du wirst bald aufhoeren '%+' zu singen.} {$color_spell_warn}
#HIGHLIGHT {^Du beendest das Lied '%+'.} {yellow}
#NOP 'Die Strasse gleitet fort' - @todo nach beenden in ausgangsraum zurueckkehren
#NOP #HIGHLIGHT {Du nimmst die Welt nun durch die Melodie deines Liedes wahr:} {yellow}
#HIGHLIGHT {^Die Melodie des Liedes loest sich auf...} {yellow}

#HIGHLIGHT {^Das magische Regenbogennetz um dich wird etwas schwaecher.} {$color_spell_warn}
#HIGHLIGHT {^Das regenbogenfarbene Netz um dich herum verblasst.} {$color_spell_ende}

#HIGHLIGHT {^Dein wuetender Blick wird ruhiger.} {$color_spell_warn}
#HIGHLIGHT {^Die intensiven Gefuehle, die du ausstrahlst, loesen sich auf.} {$color_spell_ende}

#HIGHLIGHT {^Dein Zauberbann ueber %+ wird bald enden!} {$color_spell_warn}

#HIGHLIGHT {%+ schuettelt die Schlaefrigkeit ab.} {yellow}

#HIGHLIGHT {^Deine heldenhafte Ausstrahlung wird schwaecher.} {$color_spell_warn}
#HIGHLIGHT {^Deine heldenhafte Ausstrahlung verblasst.} {$color_spell_ende}

#HIGHLIGHT {^Du fuehlst Dich erschoepft.} {underscore <dbb>}
#NOP #HIGHLIGHT {Du fuehlst dich von der Muse gekuesst.} {$color_spell_warn}

#HIGHLIGHT {Die Erinnerung an deine Freundschaft mit Yriskandur staerkt dich.} {<g06>}

#FUNCTION {test_hero} {
    #LINE ONESHOT {
        #HIGHLIGHT {%+!$} {<add>}
    };
    #RETURN {<add>%1<099>}
}

#SUBSTITUTE {^Du triffst die magieneutralisierenden Toene Angaras und blockierst die Magie} {@test_hero{%0;deiner Feinde}}
#HIGHLIGHT {%+ durchbricht deinen Magiebann!} {underscore <add>}

#HIGHLIGHT {%+ vollfuehrt einen zusaetzlichen Angriff gegen %+!} {<add>} {4.9}
#NOP #SUBSTITUTE {%+ vollfuehrt einen zusaetzlichen Angriff gegen %+} {@test_hero{%0;%2}}
#SUBSTITUTE {^Die Kampfeswut des Helden Khartak durchfaehrt dich und du stuerzt dich in den} {@test_hero{%0;Kampf}}

#HIGHLIGHT {^Die riesenhafte Staerke Grimms durchfaehrt dich und du schlaegst %+ zu Boden!} {<add>} {4.9}
#SUBSTITUTE {^Die riesenhafte Staerke Grimms durchfaehrt dich und du schlaegst%*} {@test_hero{%0;Boden}}

#HIGHLIGHT {^Du singst die Worte der Flamme und steckst deine Feinde in Brand!} {<add>}

#NOP {
Ein brutaler Kampfsoldat brennt nicht mehr.
;#NOP }

#HIGHLIGHT {^Licht sammelt sich auf Hoehe deines Herzens und verstroemt Hoffung und Kraft!} {<add>}
#HIGHLIGHT {^Die Hoffnung von Rynoxas Licht schwindet.} {<add>}
#SUBSTITUTE {^Du absorbierst Energie aus dem Licht der Umgebung.} {@test_rynoxa{%0}}

#FUNCTION {test_rynoxa} {
    #NOP timer_end licht;
    #NOP timer_start licht;
    #RETURN <F7aa>%1<099>
}

#HIGHLIGHT {^Deine Inspiration befluegelt ueberirdische Virtuositaet!} {bold}
#NOP #GAG {Du spruehst vor Inspiration!}
#GAG {^Voll Inspiration rufst du deinen Verbuendeten ermutigende Worte zu!}
#GAG {%+ starrt dich fasziniert an.}
#GAG {^Du inspirierst deine Verbuendeten zu rascheren Attacken!}

#FUNCTION {test_insp}
{
    #IF {%2 > $insp_len}
    {
        #VAR insp_len %2;
        #VAR insp_color {<bdb>}
    };
    #ELSEIF {%2 < $insp_len}
    {
        #VAR insp_len %2;
        #VAR insp_color {<dbb>};
        #IF { %2 == 0 } {
            #VAR insp_color {<dbb><388>};
        }
    };

    #VAR status_inspiration {@status_inspiration{$insp_len}};
    #LIST status_items FIND status_inspiration tmp_status_index;
    #VAR statusline_list[$tmp_status_index] {$status_inspiration};
    draw_status;

    #RETURN ${insp_color}%1<099>
}

#SUBSTITUTE {^Deine kreative Energie ist erschoepft.} {@test_insp{%0;0}}
#SUBSTITUTE {^Langsam drehen sich kreative Gedanken in deinem Kopf.} {@test_insp{%0;1}}
#SUBSTITUTE {^Melodien und Verse fuellen deinen Kopf.} {@test_insp{%0;2}}
#SUBSTITUTE {^Die Welt um dich herum inspriert immer neue kreative Gedanken!} {@test_insp{%0;3}}
#SUBSTITUTE {^Du spruehst regelrecht vor kreativer Energie!} {@test_insp{%0;4}}
#SUBSTITUTE {^Du fuehlst dich so inspriert wie nie zuvor! Kreativitaet pulsiert durch deine} {@test_insp{%0;5}}
#SUBSTITUTE {^Adern.$} {@test_insp{%0;5}}
#SUBSTITUTE {^Alles um dich ist ein Strudel aus Farben und Melodien.} {@test_insp{%0;6}}
#SUBSTITUTE {^Jeder Lufthauch, jeder Stein und jedes Wort sind reinster Quell der} {@test_insp{%0;7}}
#SUBSTITUTE {^Inspiration!$} {@test_insp{%0;7}}
#SUBSTITUTE {^Alles um dich verschwimmt in einem Strudel aus kreativen Gedanken.} {@test_insp{%0;8}}
#SUBSTITUTE {^Milliarden von Ideen fuer neue Kunst durchflutet dich.} {@test_insp{%0;9}}
#SUBSTITUTE {^Du hoerst den transzendenten Gesang der Musen.} {@test_insp{%0;10}}

#HIGHLIGHT {^Der Gesang der Musen verlaesst deinen Geist. Deine Inspiration kehrt auf} {$color_spell_ende}

#ACTION {^normales Niveau zurueck.$} {
    timer_end lied;
    #ECHO {${color_spell_ende}%0<099> (%t)} {{%M:%S}{$timer[lied][dauer]}};
    #LINE GAG
} {5};

#LIST status_items CREATE {status_session;status_hp;status_mp;status_align;status_waffe;status_bard;status_inspiration;status_halsband}

#EVENT {VARIABLE UPDATED bard} {
    #VAR status_bard {@status_bard{}};
    #LIST status_items FIND status_bard tmp_status_index;
    #VAR statusline_list[$tmp_status_index] {$status_bard};
    draw_status
}

#EVENT {VARIABLE UPDATED bard[inspiration]} {
    #VAR status_inspiration {@status_inspiration{}};
    #LIST status_items FIND status_inspiration tmp_status_index;
    #VAR statusline_list[$tmp_status_index] {$status_inspiration};
    draw_status
}

#NOP { /**
 *  @brief  Trigger auf Anzeige von Report
 *
 *  Trigger erfasst die LP/MP Anzeige bei den Barden
 *  - LP: 255/282 (-  3) MP: 171/206 (+  0) --------[--------]-[-T-------][#----]
 */};
#ACTION {- LP: %!s%+1..d/%!s%+1..d (%+) MP: %!s%+1..d/%!s%+1..d (%+) -%!.%+%!.-[%+]-[%+][%+]} {
    check_diff bard[schutz] {%7};
    check_diff bard[spells] {%8};
    check_diff bard[zustand] {%9};
    check_diff bard[inspiration] {%10};

    #VAR check_diff {0};
    #IF {!@_an_aus{config[report]}} {
        #LINE GAG
    }
} {4}

#NOP { /**
 *  @fn status_zech
 *  @brief  setzt Block mit aktiven Spells fuer die Statuszeile
 *
 *  @return     formatierter String
 */};
#FUNCTION {status_bard} {
    #FORMAT {result} {%+4s|%s|%s|} {$bard[schutz]} {$bard[spells]} {$bard[zustand]}
}

#FUNCTION {status_inspiration} {
    #IF { "%1" == "" } {
        #VAR insp_len {0};
        #REGEXP {$bard[inspiration]} {{#+}} {
            #FORMAT {insp_len} {%L} {&1}
        };
        #REGEXP {$bard[inspiration]} {{\*+}} {
            #FORMAT {insp_len} {%L} {&1};
            #MATH {insp_len} {$insp_len + 5}
        };
       
    };
    #ELSE {
        #VAR insp_len {%1};
    };

    #FORMAT {result} {%s|} {<188>${insp_len}<fec>i<099>}
}


#FUNCTION {status_waffe} {
    #FORMAT {result} {%.3s|} {$waff[aktiv]}
}

#NOP { /**
 *  | 81%|LERz54SH|
 *
 *  |  0%|  0|LERZSHBL|
 */};

#NOP { /**
 *  @brief  Effekt durch 'Zwergisches Ahnenlied'
 */};
#ACTION {^Der Angriff gleitet an %+ Schallpanzer ab.} {
    #LINE GAG;
    setze_angriff_effekte 4 A
}

#NOP { /**
 *  @brief  Effekt durch 'Hymne der Hoffnung'
 */};
#ACTION {^Eure Gemeinschaft faengt den Angriff auf.} {
    #LINE GAG;
    setze_angriff_effekte 5 H
}

#ACTION {^Regenbogenfarbenes Licht flackert um dich herum auf und schwaecht den Angriff} {
    #LINE GAG;
    setze_angriff_effekte 2 m;
    #LINE ONESHOT {
        #GAG {^ab.$}
    }
}

#ACTION {Regenbogenfarbenes Licht flackert um dich herum auf und absorbiert den} {
    #LINE GAG;
    setze_angriff_effekte 2 M;
    #LINE ONESHOT {
        #GAG {^Angriff.$}
    }
}

#ACTION {Du singst ein schrill ansteigendes Vibrato!} {
    setze_angriff_spell verderben;
    #LINE GAG
}

#ACTION {Du strahlst Furcht aus!} {
    setze_angriff_spell aengstigen;
    #LINE GAG
}

#ACTION {Dein Blick laesst deine Feinde in wilde Raserei verfallen!} {
    #NOP setze_angriff_spell raserei;
    #NOP #LINE GAG
}

#HIGHLIGHT {^Da dein Opfer nicht mehr hier ist beendest du deinen Vortrag.} {yellow}

#ACTION {^Da dein Opfer nicht mehr hier ist beendest du deinen Vortrag.} {
    #CLASS spell_rabe CLEAR
}

#ACTION {^Mit einem Schlag entlaedst du den Wahnsinn in %+} {
    setze_angriff_spell rabe;
    #LINE GAG;
    #CLASS spell_rabe CLEAR
}

#ALIAS {r} {
    m;
    #LINE MULTISHOT 5 {
        #GAG {^>}
    };
    waffen trommel;
    #IF { "%0" == "{[0-9]+}" } {
        #FORMAT {target} {%l} {$feinde[%0]}
    };
    #ELSEIF { "%0" == "" } {
        #VAR target {}
    };
    #ELSE {
        #VAR target {%0}
    };
    #SEND {spiele 'der besuch des raben' auf $target};
    #IF { $gruppe[aktiv] == 1 } {
        #{$gruppe[partner]} toete $target
    };
    #CLASS spell_rabe LOAD;
    #NOP waffen waffe
}

#CLASS spell_rabe OPEN

#GAG {Du sprichst: Riss das Fenster auf jetzunder,}
#GAG {Du sprichst: und herein stolziert' - o Wunder!}
#GAG {Du sprichst: Ein gewalt'ger, hochbejahrter Rabe}
#GAG {Du sprichst: schwirrend zu mir her;}
#GAG {Du sprichst: Flog mit maecht'gen Fluegelstreichen,}
#GAG {Du sprichst: ohne Gruss und Dankeszeichen,}
#GAG {Du sprichst: Stolz und stattlich sonder Gleichen,}
#GAG {Du sprichst: nach der Tuere hoch und hehr -}
#GAG {Du sprichst: Flog nach einer Pallasbueste,}
#GAG {Du sprichst: Setzte sich und sonst Nichts mehr.}
#GAG {Du sprichst: Und ich waehnte, durch die Lueft}
#GAG {Du sprichst: wallten suesse Weihrauchduefte,}
#GAG {Du sprichst: Ausgestreut durch unsichtbare Seraphshaende um mich her.}
#GAG {Du sprichst: Nimm, o nimm die suesse Spende}
#GAG {Du sprichst: und vergiss der Trauer schwer!"}
#GAG {Du sprichst: Sprach der Rabe: "Nimmermehr!"}
#GAG {Du sprichst: "Lethe," rief ich, "suesse Spende}
#GAG {Du sprichst: schickt Dir Gott durch Engelshaende,}
#GAG {Du sprichst: Dass sich von Lenoren wende Deine Trauer tief und schwer!}
#GAG {Du sprichst: Jene Sel'ge, die Lenoren}
#GAG {Du sprichst: nennt der Engel heilig Heer!"}
#GAG {Du sprichst: "Gramprophet!" rief ich voll Zweifel,}
#GAG {Du sprichst: "ob Du Vogel oder Teufel!}
#GAG {Du sprichst: Kuende mir, ob ich Lenoren,}
#GAG {Du sprichst: die hienieden ich verloren,}
#GAG {Du sprichst: Wieder find' an Edens Toren - sie, die throhnt im Engelsheer}
#GAG {Du sprichst: Bei dem ew'gen Himmel droben, bei dem Gott, den ich verehr'}
#HIGHLIGHT {Mit finsterer Stimme rufst du: "Nimmermehr!" und deutest auf %+.} {<dda>} {4.9}
#SUBSTITUTE {Mit finsterer Stimme rufst du: "Nimmermehr!" und deutest auf%*} {@test_rabe{%0}}
#FUNCTION {test_rabe} {
    #LINE ONESHOT {
        #HIGHLIGHT {%+.$} {<dda>}
    };
    #RETURN {<dda>%1<099>}
}

#CLASS spell_rabe SAVE
#CLASS spell_rabe CLEAR
#CLASS spell_rabe CLOSE

#ALIAS {a} {
    m;
    #SEND {aengstige %0}
}

#ALIAS {ahn} {
    waffen trommel;
    #SEND {spiele 'zwergisches ahnenlied' auf %0}
}

#ALIAS {borst} {
    waffen trommel;
    #NOP #SEND {sag Reparatur kommt!};
    #SEND {spiele 'das geborstene schwert'}
}

#ALIAS {e} {
    waffen trommel;
    #SEND {spiele 'das gelueftete geheimnis' auf %0}
}

#ALIAS {freund} {
    waffen trommel;
    #SEND {spiele 'band der freundschaft' auf %0}
}

#ALIAS {gast} {
    waffen trommel;
    #SEND {spiele 'sei hier gast'}
}

#ALIAS {gift} {
    waffen trommel;
    #SEND {spiele 'klarster lebenssaft da quillt'}
}

#ALIAS {held} {
    waffen trommel;
    #SEND {spiele 'ruf der helden' auf %0};
    #NOP #SEND {spiele 'der letzte der riesen' auf %0}
}

#ALIAS {lor} {
    waffen trommel;
    #SEND {spiele 'lorica der musen' auf %0};
    #NOP #SEND {spiele 'schuetzendes herz des berges' auf %0}
}

#ALIAS {ode} {
    waffen trommel;
    #SEND {spiele 'ode an die sterne'}
}

#ALIAS {sb} {
    waffen trommel;
    #SEND {spiele 'tanz der saebel' auf %0};
    #NOP #SEND {spiele 'sturm der klingen' auf %0}
}

#ALIAS {sch} {
    waffen trommel;
    #SEND {spiele 'der dinge wahrer hintergrund' auf %0}
}

#ALIAS {still} {
    waffen trommel;
    #SEND {spiele 'abendstille ueberall'}
}

#ALIAS {strasse} {
    waffen trommel;
    #SEND {spiele 'die strasse gleitet fort'}
}

#ALIAS {ton} {
    waffen trommel;
    #SEND {spiele 'ein ton in der stille'}
}

#ALIAS {tro} {
    waffen trommel;
    #SEND {spiele 'des todes drei trommelschlaege' auf %1}
}

#ALIAS {win} {
    waffen trommel;
    #SEND {spiele 'lied der winde' auf %0}
}

#ALIAS {winter} {
    waffen trommel;
    #SEND {spiele 'der winter'}
}

#ALIAS {wut} {
    waffen trommel;
    #SEND {spiele 'toetet das biest'}
}

#ALIAS {zau} {
    waffen trommel;
    #SEND {spiele 'lied der zauber'}
}

#NOP { /**
 *	@brief	Fertigkeiten anders anzeigen
 */};

#ACTION {^Artyrian sagt: %+ beherrscht Du %+.} {
    #ECHO {%s %s} {@dotfill{<ffd>%1<099>;40}} {@barden_fert{%2}<099>};
    #LINE GAG
}

#ACTION {Artyrian sagt: Du musizierst %+.} {
    #ECHO {%s %s} {@dotfill{<ffd>Musizieren<099>;40}} {@barden_fert{%1}<099>};
    #LINE GAG
}

#ACTION {Artyrian sagt: Und Du singst %+.} {
    #ECHO {%s %s} {@dotfill{<ffd>Singen<099>;40}} {@barden_fert{%1}<099>};
    #LINE GAG
}

#ACTION {Artyrian sagt: Den Helden %+ verkoerperst du  {[^\.]+}} {
    #ECHO {%s %s} {@dotfill{<ffd>Verkoerperung der Helden (%1)<099>;40}} {@barden_fert{%2}<099>};
    #LINE GAG
}

#ACTION {Artyrian sagt: Insgesamt betrachtet bist Du %+.} {
    #ECHO {%s %s} {@dotfill{<ffd>Insgesamt<099>;40}} {@barden_fert{%1}<099>};
    #LINE GAG
}

#VAR {bfort}
{
    {1} {zum Davonlaufen}
    {2} {absolut miserabel}
    {3} {elendiglich}
    {4} {sehr schlecht}
    {5} {schlecht}
    {6} {nicht besonders}
    {7} {einigermassen passabel}
    {8} {passabel}
    {9} {unterdurchschnittlich}
    {10}{durchschnittlich}
    {11}{ueberdurchschnittlich}
    {12}{gut}
    {13}{sehr gut}
    {14}{wirklich toll}
    {15}{hervorragend}
    {16}{ausgezeichnet}
    {17}{einfach wunderbar}
    {18}{exzellent}
    {19}{unglaublich gut}
    {20}{virtuos}
    {21}{unuebertreffbar}
}

#FUNCTION barden_fert {
    #LIST bfort FIND {%0} tmp;
    #IF {$tmp < 12} {
        #VAR tmp_color {<bba>}
    };
    #ELSEIF {$tmp < 15} {
        #VAR tmp_color {<bca>}
    };
    #ELSEIF {$tmp < 17} {
        #VAR tmp_color {<bdb>}
    };
    #ELSEIF {$tmp < 19} {
        #VAR tmp_color {<dea>}
    };
    #ELSEIF {$tmp < 21} {
        #VAR tmp_color {<bfa>}
    };
    #ELSE {
        #VAR tmp_color {<dfc><388>}
    };
    #RETURN ${tmp_color}%0
}

#ALIAS {bfort} {
    #VAR tmp_count {1};
    #SCREEN GET {SCROLL_COLS} {tmp_width};
    #VAR tmp_line {};
    #WHILE {$tmp_count <= &bfort[] } {
        #FORMAT {tmp_line_len} {%L} {$tmp_line};
        #FORMAT {tmp_item_len} {%L} {$bfort[$tmp_count]};
        #MATH {tmp_total_len} {$tmp_line_len + $tmp_item_len + 5};

        #VAR tmp_item @barden_fert{$bfort[$tmp_count]};

        #IF {$tmp_total_len > $tmp_width} {
            #ECHO {$tmp_line<099>};
            #VAR tmp_line {$tmp_item};
        };
        #ELSEIF {"$tmp_line" == ""} {
            #VAR tmp_line {$tmp_item};
        };
        #ELSE {
            #CAT tmp_line {     $tmp_item}
        };
        inc tmp_count
    };
    #ECHO {$tmp_line<099>}
} {5};

#NOP { /**
 *	@brief	whatever
 */};

#ALIAS {waffen set %1 %+} {
    #SWITCH { "%1" } {
        #CASE { "{w|waffe}" } {
            #VAR waff[waffe] {%2};
            #ECHO {Du kaempfst jetzt mit der Waffe: %n} {$waff[waffe]};
            #RETURN
        };
        #CASE { "{t|trommel}" } {
            #VAR waff[trommel] {%2};
            #ECHO {Du nutzt jetzt das Instrument: %n} {$waff[trommel]};
            #RETURN
        }
    }
} {4.9}

#ALIAS {waffen} {
    #IF { "$waff[waffe]" == "" } {
        #ECHO {Keine Waffe gesetzt.};
        #RETURN
    };
    #IF {"$waff[trommel]" == ""} {
        #ECHO {Keine Trommel gesetzt.};
        #RETURN
    };

    #IF { "%1" == "waffe" } {
        #IF { "$waff[aktiv]" == "waffe" } {
            #ECHO {Du kaempfst schon mit Deiner Waffe.};
            #RETURN
        };
        #VAR waff[aktiv] waffe;
        #NOP #SEND {ziehe $waff[trommel] aus};
        #SEND {stecke $waff[trommel] weg};
        #NOP #SEND {zuecke $waff[waffe]};
        #RETURN
    };
    #IF { "%1" == "trommel" } {
        #IF { "$waff[aktiv]" == "trommel" } {
            #ECHO {Du traegst schon Dein Instrument.};
            #RETURN
        };
        #VAR waff[aktiv] trommel;
        #NOP #SEND {stecke $waff[waffe] weg};
        #SEND {zuecke $waff[trommel]};
        #NOP #SEND {trage $waff[trommel]};
        #RETURN
    };

    #IF { "$waff[aktiv]" == "" } {
        #VAR waff[aktiv] waffe;
        #NOP #SEND {ziehe $waff[trommel] aus};
        #SEND {stecke $waff[trommel] weg};
        #NOP #SEND {zuecke $waff[waffe]};
        #RETURN
    };
    #IF { "$waff[aktiv]" == "waffe" } {
        #VAR waff[aktiv] trommel;
        #NOP #SEND {stecke $waff[waffe] weg};
        #SEND {zuecke $waff[trommel]};
        #NOP #SEND {trage $waff[trommel]};
        #RETURN
    };
    #IF { "$waff[aktiv]" == "trommel" } {
        #VAR waff[aktiv] waffe;
        #NOP #SEND {ziehe $waff[trommel] aus};
        #SEND {stecke $waff[trommel] weg};
        #NOP #SEND {zuecke $waff[waffe]};
        #RETURN
    }
} {5}

#EVENT {VARIABLE UPDATED waff} {
    update_var;
    #VAR status_waffe {@status_waffe{}};
    draw_status
}

#ALIAS {ww} {
    waffen
}

#ALIAS {ueben} {
    #IF { $barden_ueben == 1 } {
        #VAR barden_ueben 0;
        #CLASS barden_ueben KILL;
        #ECHO {Uebungsmodus ist deaktiviert.}
    };
    #ELSE {
        #VAR barden_ueben 1;
        #READ {gilden/barden_ueben.tin};
        #ECHO {Uebungsmodus ist aktiviert.}
    }
}

#ALIAS {freh} {
    freund $gruppe[partner]
}

#ALIAS {rabe_analyse} {
    #VAR angreifer          {@name_kuerzen{Du}};
    #VAR opfer              {@name_kuerzen{%1}};
    setze_angriff_spell rabe;
    #VAR angriff[art]       Geist;
    #VAR angriff[hoehe]     {%2};

    #FORMAT {rabe_schadenausgabe} {%s%s|%s%s %s%s%s %.5s %s%s}
        {@kampf_pad{9;1;l;$angriff[art]}}
        {$angriff[art]}
        {$angriff[spell]}
        {@kampf_pad{12;1;r;$angriff[spell]}}
        {$angreifer}
        {@kampf_pad{39;2;m;$angreifer;$opfer}}
        {$opfer}
        {@zeige_angriff_effekte{$angriff[effekte]}}
        {@kampf_pad{11;1;l;$angriff[hoehe]}}
        {$angriff[hoehe]};

    #NOP #SHOWME {$rabe_schadenausgabe};

    #NOP { /** Auf weitere Kampfrunde vorbereiten */};
    #UNVAR angriff;
    #VAR angriff[killer] Du;
    setze_angriff_spell;
    setze_angriff_effekte;
    #VAR roomspell {};

    #LINE GAG
}

#ACTION {%+ taumelt verwirrt umher!} {
    setze_angriff_effekte 5 T;
    rabe_analyse {$rabe_target} {$rabe_damage};
}

#ACTION {^  %+ widersteht deinem geistigen Angriff.} {
  #VAR rabe_target {%1};
  #VAR rabe_damage {<dff>widersteht<099>};
  rabe_analyse {$rabe_target} {$rabe_damage};
  #ECHO {$rabe_schadenausgabe}
}

#ACTION {^  %+ wirkt ganz nervoes.} {
  #VAR rabe_target {%1};
  #VAR rabe_damage {<dbc>nervoes<099>};
  rabe_analyse {$rabe_target} {$rabe_damage};
  #ECHO {$rabe_schadenausgabe}
}

#ACTION {^  %+ blickt sich unsicher um.} {
  #VAR rabe_target {%1};
  #VAR rabe_damage {<dbc>unsicher<099>};
  rabe_analyse {$rabe_target} {$rabe_damage};
  #ECHO {$rabe_schadenausgabe}
}

#ACTION {^  %+ blickt sich verwirrt um.} {
  #VAR rabe_target {%1};
  #VAR rabe_damage {<dbc>verwirrt<099>};
  rabe_analyse {$rabe_target} {$rabe_damage};
  #ECHO {$rabe_schadenausgabe}
}

#ACTION {^  %+ bekommt einen fiebrig-wahnsinnigen Blick.} {
  #VAR rabe_target {%1};
  #VAR rabe_damage {<fbc>wahnsinnig<099>};
  rabe_analyse {$rabe_target} {$rabe_damage};
  #ECHO {$rabe_schadenausgabe}
}

#ACTION {^  %+ Kopf rollt irre herum und %+ bruellt} {
  #GAG {Worte.};
  #VAR rabe_target {%2};
  #VAR rabe_damage {<fbc>irre<099>};
  rabe_analyse {$rabe_target} {$rabe_damage};
  #ECHO {$rabe_schadenausgabe};
  #DELAY 0.01 {
    #UNGAG {Worte.}
  }
} {4.9}

#ACTION {^  %+ Kopf rollt irre herum und %+} {
  #GAG {Worte.};
  #VAR rabe_target {%2};
  #VAR rabe_damage {<fbc>irre<099>};
  rabe_analyse {$rabe_target} {$rabe_damage};
  #ECHO {$rabe_schadenausgabe};
  #DELAY 0.01 {
    #UNGAG {Worte.}
  }
}

#ACTION {^  %+ verfaellt in wilde Zuckungen und spuckt unsaegliche} {
  #GAG {Schaum.};
  #VAR rabe_target {%1};
  #VAR rabe_damage {<158>Zuckungen<099>};
  rabe_analyse {$rabe_target} {$rabe_damage};
  #ECHO {$rabe_schadenausgabe};
  #DELAY 0.01 {
    #UNGAG {Schaum.}
  }
}

#ACTION {Dein Blick laesst deine Feinde in wilde Raserei verfallen!} {
    #IF { $biest_flag == 1 }     {
        #GAG {Dein Blick laesst deine Feinde in wilde Raserei verfallen!}
    };
    #VAR biest_flag 1;

    #DELAY 0.01 {
        #VAR biest_flag 0;
        #UNGAG {Dein Blick laesst deine Feinde in wilde Raserei verfallen!}
    }
}

#ALIAS {ablenken} {
    #herschel frag %1 nach wetter
}

#ALIAS {sieb} {
    ablenken %1;
    #DELAY 20 {
        ablenken %1
    };
    #SEND {si %1}
}

#ACTION {%+ sagt: Schoen, dass wir uns kennenlernen,} {
    #REGEXP {%1} {{([Dd]er|[Ee]ine?n?|[Dd]ie|[Dd]as|[Dd]en)(.*?)([A-Z][A-Za-z\- ]+)}} {
        #VAR begleiter {&4}
    } {
        #VAR begleiter {%1}
    };
    #IF { "$kampf[helfer]" == "" } {
        #VAR kampf[helfer] $begleiter
    };
    #ELSEIF { "$begleiter" != "{$kampf[helfer]}" } {
        #CAT kampf[helfer] {|$begleiter}
    };
    timer_start helfer
} {4.8}

#ACTION {Dein Zauberbann ueber %+ wird bald enden!} {
    timer_med helfer
}

#ACTION {%+ sagt: Es war schoen eine Zeit lang mit dir} {
    #REGEXP {%1} {{([Dd]er|[Ee]ine?n?|[Dd]ie|[Dd]as|[Dd]en)(.*?)([A-Z][A-Za-z\- ]+)}} {
        #VAR begleiter {&4}
    } {
        #VAR begleiter {%1}
    };
    #IF { "$begleiter" == "{$kampf[helfer]}" } {
        #REPLACE {kampf[helfer]} {$begleiter} {};
        #REPLACE {kampf[helfer]} {^|} {};
        #REPLACE {kampf[helfer]} {||} {|};
        #REPLACE {kampf[helfer]} {%*|$} {&1}
    };
    timer_end helfer
} {4.8}


#FUNCTION {flammara_analyse} {
    #VAR angreifer          {@name_kuerzen{Du}};
    #VAR opfer              {@name_kuerzen{%1}};
    setze_angriff_spell flammen;
    #VAR angriff[art]       Feuer;
    #VAR angriff[hoehe]     %2;

    #FORMAT {flammara_schadenausgabe} {%s%s|%s%s %s%s%s %.5s %s%s}
        {@kampf_pad{9;1;l;$angriff[art]}}
        {$angriff[art]}
        {$angriff[spell]}
        {@kampf_pad{12;1;r;$angriff[spell]}}
        {$angreifer}
        {@kampf_pad{39;2;m;$angreifer;$opfer}}
        {$opfer}
        {@zeige_angriff_effekte{$angriff[effekte]}}
        {@kampf_pad{11;1;l;$angriff[hoehe]}}
        {$angriff[hoehe]};

    #NOP #SHOWME {$flammara_schadenausgabe};

    #NOP { /** Auf weitere Kampfrunde vorbereiten */};
    #UNVAR angriff;
    #VAR angriff[killer] Du;
    setze_angriff_spell;
    setze_angriff_effekte;
    #VAR roomspell {};

    #RETURN {$flammara_schadenausgabe}
}

#ACTION {^  Die Flammen machen %+ nichts aus.} {
  #SHOWME {@flammara_analyse{%1;<dff>verfehlt<099>}};
  #LINE GAG
}

#ACTION {^  Kleine Flammen schlagen aus %+.} {
  #SHOWME {@flammara_analyse{%1;<dbc>kl. Flammen<099>}};
  #LINE GAG
}

#ACTION {^  Flammen schlagen aus %+.} {
  #SHOWME {@flammara_analyse{%1;<fbc>Flammen<099>}};
  #LINE GAG
}

#ACTION {^  Eine Stichflamme schlaegt aus %+.} {
  #SHOWME {@flammara_analyse{%1;<fbc>Stichflamme<099>}};
  #LINE GAG
} {4.5}

#ACTION {Die Wirkung der Zaubermelodie verfliegt...} {
    #VAR bard[zauber] {1};
    timer_end zauber
}

#NOP /* 25% */;
#ACTION {Die Wirkung von 'Lied der Zauber' wird bald enden.} {
    timer_med zauber;

    #IF { "$buff[modus]" == "blocked" || $bard[zauber] != 1 } {
        #RETURN
    };
    #FORMAT {tmp_target} {%n} {$info[SESSION][NAME]};
    #IF { "$buff[modus]" == "{gespenst|buff}" } {
        buff_add message $tmp_target {{1}{message_restzeit}};
        #RETURN
    };
    #ELSE {
        message_restzeit
    };
}

#SUBSTITUTE {Ein regenbogenfarbenes Netz schimmert kurz um dich.} {@timer_lorica{%0}}

#FUNCTION {timer_lorica} {
    timer_start lorica;
    #RETURN {%1}
}

#ACTION {Das regenbogenfarbene Netz um dich herum verblasst.} {
    timer_end lorica
}

#NOP /* 25% */;
#NOP #ACTION {Die Wirkung von 'Schuetzendes Herz des Berges' wird bald enden.}
#ACTION {Die Wirkung von 'Lorica der Musen' wird bald enden.} {
    timer_med lorica
}

#ACTION {Mit donnerndem Crescendo endet dein Lied.} {
    timer_start helden;
    #IF { "$buff[modus]" != "buff" } {
        #RETURN
    };
    tick_start
}

#ACTION {Deine heldenhafte Ausstrahlung verblasst.} {
    timer_end helden
}

#NOP /* 25% */;
#ACTION {Deine heldenhafte Ausstrahlung wird schwaecher.} {
    timer_med helden
}

#ACTION {Du beendest das Lied 'hymne der hoffnung'.} {
    #IF { "$buff[modus]" != "blocked" && "$bot_type" != "hymne" } {
        buff_insert titel Juergen {$bot_untitle};
    };
    timer_end hymne;
}

#ACTION {Ein Stern erscheint, dreht seine Bahnen um Dich und leuchtet fuer Dich.} {
    timer_start ode
}

#ACTION {Einige Sterne erscheinen, drehen ihre Bahnen und leuchtet fuer Dich.} {
    timer_start ode
}

#ACTION {Die Wirkung Deiner 'Ode an die Sterne' schwindet!} {
    timer_end ode
}

#VAR {port_ziele} {
    {nexus}             {1586}
    {tijara}            {761}
    {dschungelelfen}    {1726}
    {midas}             {59}
    {kyranda}           {3}
    {feen}              {2945}
    {arwenock}          {1063}
    {felonia}           {264}
    {paladine}          {1001}
    {trolle}            {2614}
    {bergwerk}          {308}
    {steinstadt}        {4776}
    {derlim}            {670}
    {holzfaeller}       {640}
    {druiden}           {490}
    {knarkh}            {1944}
    {fischerdorf}       {5022}
    {zombieinsel}       {1660}
    {felseninsel}       {5195}
    {mauritius}         {3803}
    {hypros}            {3996}
    {arrakeen}          {1382}
    {echsendorf}        {2900}
    {quaul}             {3791}
    {erg}               {141}
    {dhrkion}           {2413}
    {sternenwald}       {1908}
    {yaqmonoq}          {1827}
    {avir}              {1440}
    {ardhmir}           {2699}
    {barden}            {598}
    {elessar}           {605}
    {zentauren}         {1231}
    {yiqanuc}           {1567}
    {smoersky}          {1480}
}

#ALIAS {port} {
    #IF { &port_ziele[%1] > 0 } {
        #VAR port_ziel {%1};
        #SEND {port}
    };
    #ELSEIF { "%0" == "" } {
        #VAR port_ziel {};
        #SEND {port}
    };
    #ELSE {
        #VAR port_ziel {};
        #SEND {port %1}
    }
}

#ACTION {Schwingen, mit denen du dich als Regenbogenfalke vom Boden abstoesst...} {
    #IF { $barden_ueben == 1 } {
        #DELAY 10 {
            port midas
        }
    };
    #ACTION {{^  \* (.+)}} {
        #SWITCH { "%%2" } {
            #CASE { "Nexus" } {
                barden_astral_bewegung 5491
            };
            #CASE { "Tijara" } {
                barden_astral_bewegung 5493
            };
            #CASE { "Midas" } {
                barden_astral_bewegung 5485
            };
            #CASE { "Arwenock" } {
                barden_astral_bewegung 5486
            };
            #CASE { "Derlim" } {
                barden_astral_bewegung 5487
            };
            #CASE { "Avir" } {
                barden_astral_bewegung 5492
            };
            #CASE { "Smoersky" } {
                barden_astral_bewegung 5494
            };
            #CASE { "Dhrkion" } {
                barden_astral_bewegung 5490
            };
            #CASE { "Knarkh" } {
                barden_astral_bewegung 5488
            };
            #CASE { "Arrakeen" } {
                barden_astral_bewegung 5489
            }
        }
    }
}

#ALIAS {barden_astral_bewegung} {
    #MAP GOTO %1;
    #IF { "$port_ziel" != "" } {
        go $port_ziele[$port_ziel]
    }
}

#ACTION {Du erkennst folgende Orte, and denen du landen kannst:} {
    #NOP ;
}

#ACTION {Von hier aus kannst du in folgende Regionen weiterfliegen:} {
    #UNACTION {{^  \* (.+)}}
}

#NOP { /**
 *	@brief	Endtexte der Lieder
 */};

#ACTION {^Im Takt von Paukenschlaegen legen sich verhaertete Toene um %+ und} {
    #IF { "$buff[modus]" != "buff" } {
        #RETURN
    };
    #IF { "%1" == "dich" } {
        buff_message {fail};
        tick_skip -1;
    };
    tick_start
}

#ACTION {Von dir ausgehend bildet sich ein Kreis aus Licht,} {
    timer_start {hymne};
    #IF { "$buff[modus]" != "buff" } {
        #RETURN
    };
    tick_start
}

#ACTION {^%+ wird bereits geschuetzt.} {
    #IF { "$buff[modus]" != "buff" } {
        #RETURN
    };
    buff_message {schild_act};
    tick_skip;
    tick_start
}

#ACTION {^Du wirst bereits geschuetzt.} {
    #IF { "$buff[modus]" != "buff" } {
        #RETURN
    };
    buff_message {fail};
    tick_skip -1;
    tick_start
}

#ACTION {Du wirst bereits von den Musen geschuetzt} {
    #IF { "$buff[modus]" != "buff" } {
        #RETURN
    };
    buff_message {lorica_act};
    tick_skip;
    tick_start
}

#ACTION {^%+1..w wird bereits geschuetzt!} {
    #IF { "$buff[modus]" != "buff" } {
        #RETURN
    };
    buff_message {lorica_oth};
    tick_skip;
    tick_start
}

#ACTION {Ein regenbogenfarbenes Netz schimmert kurz um %+.} {
    #IF { "$buff[modus]" != "buff" } {
        #RETURN
    };
    #IF { "%1" == "dich" } {
    buff_message {fail};
    tick_skip -1;
    tick_start
    };
    #ELSEIF { "%1" == "$bot_target auf" } {
        tick_start
    }
}

#ACTION {Dieses Lied wirkt bereits auf %+.} {
    #IF { "$buff[modus]" != "buff" } {
        #RETURN
    };
    #NOP buff_message {helden_oth};
    tick_skip;
    tick_start
}

#ACTION {Du schmetterst: Schau, du Schmied - so schneidet Juergens Schwert!} {
    #IF { "$buff[modus]" != "buff" } {
        #RETURN
    };
    tick_start
}

#ACTION {Dein Lied verstaerkt die Magie deiner Lieder! %+} {
    timer_start zauber;
    #IF { "%1" == "Sie wirken nun staerker." } {
        #VAR bard[zauber] {1}
    };
    #ELSE {
        #LINE ONESHOT {
            #ACTION {laenger%.} {
                #IF { "%%1" == "." } {
                    #VAR bard[zauber] {2}
                };
                #ELSE {
                    #VAR bard[zauber] {3}
                }
            }
        }
    };
    #IF { "$buff[modus]" != "buff" } {
        #RETURN
    };
    tick_start
}

#ACTION {Diese Zaubermelodie wirkt noch auf Dich!} {
    #IF { "$buff[modus]" != "buff" } {
        #RETURN
    };
    tick_skip;
    tick_start
}

#ACTION {Hier ist niemand%*, den du mit deiner Hymne der Hoffnung inspirieren} {
    #IF { "$buff[modus]" != "buff" } {
        #RETURN
    };
    buff_message {fail};
    tick_skip -1;
    tick_start
}

#ALIAS {lied} {
    timer_start lied;
    #SEND {spiele 'lied der lieder'};
} {5};

#CLASS barden CLOSE

#NOP {
Das Band deiner Freundschaft zu Eldar beschwingt deine Angriffe!
Deine Freundschaft zu Eldar staerkt deine Verteidigung etwas.
Das Band deiner Freundschaft zu Eldar schliesst deine Wunden.
Das Band deiner Freundschaft zu Eldar regeneriert deine magische Kraft.
Die Gegenwart von Eldar staerkt deine Verteidigung.

  Der grosse Zwergenberserker widersteht deinem geistigen Angriff.
Der grosse Zwergenberserker taumelt verwirrt umher!
  Der grosse Zwergenberserker blickt sich unsicher um.
  Der Ork blickt sich verwirrt um.
  Der Ork bekommt einen fiebrig-wahnsinnigen Blick.
  Des Orks Kopf rollt irre herum und der Ork bruellt unverstaendliche Worte.

Die Stimmgabel zerbricht! Enttaeuscht wirfst du sie weg.

Dein Zauberbann ueber Den Mythar wird bald enden!

Die Tugenden ziehen sich aus Elysia zurueck!

Deine Finger tanzen ueber dein Isntrument,

Mit geschlossenen Augen taucsht

Juergen beginnt eine virtuouse 

Angara
Arcon
Flammara
Fraggin
Grimm
Khartak
Nogi
Mira
Rynoxa
Schlain
Xulgsch
Zafira

Lied   | Anzahl | Dauer | Min  | Max   | Avg
Freund | 10     | 138   | 96   | 140   | 116

;#NOP }
