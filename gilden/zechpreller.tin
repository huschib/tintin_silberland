#NOP { /**
 *  @class zechpreller.tin
 *  @brief  Alles, was mit den Spells der Zechpreller zu tun hat.
 *
 *  @todo   Spellschaeden aufschreiben
 *                        Gift  Laerm   Saeure  Luft    Magie   Feuer   Druck   Wasser
 *              Ruelpser:   X     X        -      -       -       -       -       -
 *         Ruelpser (WG):   -     -        -      X       -       -       -       -
 *             Anhauchen:   X     -        -      X       -       -       -       -
 *    Anhauchen (Fackel):   -     -        -      -       -       X       -       -
 *        Anhauchen (WG):   -     -        -      X       -       -       -       -
 * Anhauchen (WG/Fackel):   -     -        -      -       -       X       -       -
 *    Alkoholstrahl (WG):   -     -        -      -       -       -       -       X
 *         Alkoholisiere:   X     -        -      -       X       -       -       -
 *          Alkoholwolke:   X     -        X      -       -       -       -       -
 *             Rumkugeln:   -     -        -      -       -       -       X       -
 */}

#CLASS zechpreller KILL
#CLASS zechpreller OPEN

#ECHO {<029>-- <188>%s<288> geladen<099>} {gilden/zechpreller.tin}

#NOP /* Zusammensetzung der Statuszeile fuer Zechpreller */

#LIST status_items CREATE {status_session;status_hp;status_mp;status_alcohol;status_align;status_zech;status_weingeist;status_halsband}

#NOP #LIST {status_items} ADD {ampullen}
#NOP #LIST {status_items} ADD {test}
#LIST STATUS_ZECH_SPELLS CREATE
{
    dunstpanzer;torkeln;rumkugeln;verschmelzung;alkoholwolke;destilliere
}
#LIST CONST_ALKOHOL CREATE
{
    nuechtern;leicht angeheitert;feucht froehlich;etwas angetrunken;angetrunken;
    etwas blau;blau;extrem blau;zechprellerblau
}

#ACTION {{[0-9]+}/{[0-9]+} {.?[0-9]+} LP {[0-9]+}/{[0-9]+} {.?[0-9]+} MP {[ a-z]+} {.+}}
{
    #NOP { /**
     *  @brief  Trigger auf Anzeige von Report
     *
     *  Trigger erfasst die LP/MP Anzeige bei den Zechprellern im kurzen maximal
     *  Modus: zreport kurz 1, zreport max 1
     *  Bsp.:    266/266 0 LP 258/258 0 MP extrem blau 0
     */};
    #NOP #ECHO {z: $status[hp] &1};
    #VAR status[lasthp] $status[hp];
    #VAR status[maxhp] %2;
    #VAR status[hp] %1;
    #NOP #VAR diffhp &3;
    #VAR status[lastmp] $status[mp];
    #VAR status[maxmp] %5;
    #VAR status[mp] %4;
    #NOP #VAR diffmp &6;
    #VAR status[alcohol] %7;
    #MATH {status[diffhp]} {$status[hp] - $status[lasthp]};
    #MATH {status[diffmp]} {$status[mp] - $status[lastmp]};
    #IF {$status[diffmp] > 0}
    {
        shift <060>+$status[diffmp]<099>
    };
    #IF {$status[diffhp] > 0}
    {
        shift <020>+$status[diffhp]<099>
    };
    #IF {$status[diffmp] < 0}
    {
        shift <140>$status[diffmp]<099>
    };
    #IF {$status[diffhp] < 0}
    {
        shift <010>$status[diffhp]<099>
    };
    #IF {$status[diffhp] != 0 || $status[diffmp] != 0}
    {
        #NOP { /**
        *   @todo   Aufruf von scrolling nur über Funktion
        */};
        #SHOWME {$scrolling} {-4} {61}
    };
    #LINE GAG
} {4}

#ALIAS {init_zech_spells}
{
    #NOP { /**
     *  @fn init_zech_spells
     *  @brief intialisiert Zustandsausgabe fuer Zechpreller
     */};

    #LOCAL zech_item {};
    #LOCAL symbol_string {};
    #VAR zech_spells {};

    #FOREACH $STATUS_ZECH_SPELLS[%*] zech_item
    {
        #VAR zech_spells[$zech_item][aktiv] 0;
        #LIST symbol_string TOKENIZE $zech_item;
        #FORMAT zech_spells[$zech_item][symbol] {%u} {$symbol_string[1]}
    };

    #VAR zech_spells[destilliere][symbol] 0
}

init_zech_spells

#FUNCTION {status_zech}
{
    #NOP { /**
     *  @fn status_zech
     *  @brief  setzt Block mit aktiven Spells fuer die Statuszeile
     *
     *  @return     formatierter String
     */};

    #LOCAL ausgabe {};
    #LOCAL item {};

    #FOREACH $STATUS_ZECH_SPELLS[%*] item
    {
        #IF {$zech_spells[$item][aktiv]}
        {
            #CAT ausgabe {<099>$zech_spells[$item][symbol]}
        };
        #ELSE
        {
            #CAT ausgabe {<g02>$zech_spells[$item][symbol]}
        }
    };

    #FORMAT {result} {%-6s<099>|} {$ausgabe}
}

#FUNCTION {status_weingeist}
{
    #NOP { /**
     *  @fn status_weingeist
     *  @brief  Ausgabe Status der Weingeistbefehle
     *
     *  @return     formatierter String
     */};

    #LOCAL ausgabe {};

    #IF {$zech_spells[weingeist][aktiv]}
    {
        #CAT ausgabe {<099>WG:}
    };
    #ELSE
    {
        #CAT ausgabe {<g02>WG:}
    };
    #IF {$zech_spells[weingeist][folgen]}
    {
        #CAT ausgabe {<099>F}
    };
    #ELSE
    {
        #CAT ausgabe {<g02>F}
    };
    #IF {$zech_spells[weingeist][kampf]}
    {
        #CAT ausgabe {<099>K}
    };
    #ELSE
    {
        #CAT ausgabe {<g02>K}
    };
    #IF {$zech_spells[weingeist][magie]}
    {
        #CAT ausgabe {<099>M}
    };
    #ELSE
    {
        #CAT ausgabe {<g02>M}
    };

    #FORMAT {result} {%-6s<099>|} {$ausgabe}
}

#FUNCTION {status_alc}
{
    #NOP { /**
     *  @fn status_alc
     *  @brief Kurzanzeige fuer Alkoholwert
     *
     *  setzt die Kurzanzeige des Alkoholwertes in der Statuszeile
     *
     *  @return   formatierter String
     */};

    #LOCAL alc {};

    #SWITCH {"%0"}
    {
        #CASE {"$CONST_ALKOHOL[1]"}
        {
            #VAR alc .
        };
        #CASE {"$CONST_ALKOHOL[2]"}
        {
            #VAR alc +
        };
        #CASE {"$CONST_ALKOHOL[3]"}
        {
            #VAR alc #
        };
        #CASE {"$CONST_ALKOHOL[4]"}
        {
            #VAR alc #+
        };
        #CASE {"$CONST_ALKOHOL[5]"}
        {
            #VAR alc ##
        };
        #CASE {"$CONST_ALKOHOL[6]"}
        {
            #VAR alc ##+
        };
        #CASE {"$CONST_ALKOHOL[7]"}
        {
            #VAR alc ###
        };
        #CASE {"$CONST_ALKOHOL[8]"}
        {
            #VAR alc ###+
        };
        #CASE {"$CONST_ALKOHOL[9]"}
        {
            #VAR alc ####
        }
    };
    #FORMAT {result} {%-4s|} {<afc>$alc<078>}
}

#ALIAS {weingeist_einstellen}
{
    #NOP { /**
     *  @fn weingeist_einstellen
     *  @brief  intialisiert Grundvariablen nach Erschaffung von Weingeist
     */};

    #VAR zech_spells[weingeist][aktiv] 1;
    #VAR zech_spells[weingeist][magie] 1
}

#ALIAS {weingeist_entlassen}
{
    #NOP { /**
     *  @fn weingeist_entlassen
     *  @brief  setzt Grundvariablen eines Weingeists zurueck
     */};

    #VAR zech_spells[weingeist][aktiv] 0;
    #VAR zech_spells[weingeist][folgen] 0;
    #VAR zech_spells[weingeist][kampf] 0;
    #VAR zech_spells[weingeist][magie] 0;
    #UNVAR kampf[helfer]
}

#ALIAS {wgf}
{
    #NOP { /**
     *  @fn wgf
     *  @brief  Toggle fuer Folgen des Weingeists
     */};

    #IF {$zech_spells[weingeist][folgen]}
    {
        #SEND {emote sagt: weingeist bleib stehen};
        #VAR zech_spells[weingeist][folgen] 0
    };
    #ELSE
    {
        #SEND {emote sagt: weingeist folge mir};
        #VAR zech_spells[weingeist][folgen] 1
    };
    #NOP drawstatus
}

#ALIAS {wgk}
{
    #NOP { /**
     *  @fn wgk
     *  @brief  Toggle fuer Kampf des Weingeists
     */};

    #IF {$zech_spells[weingeist][kampf]}
    {
        #SEND {emote sagt: weingeist kaempfe nicht mit mir};
        #VAR zech_spells[weingeist][kampf] 0;
        #UNVAR kampf[helfer]
    };
    #ELSE
    {
        #SEND {emote sagt: weingeist kaempfe mit mir};
        #VAR zech_spells[weingeist][kampf] 1;
        #VAR kampf[helfer] Weingeist
    };
    #NOP drawstatus
}

#ALIAS {wgfk}
{
    #NOP { /**
     *  @fn wgfk
     *  @brief  Kuerzel fuer Funktionen wgf und wgk
     */};

    wgf;
    wgk
}

#ALIAS {wgm}
{
    #NOP { /**
     *  @fn wgm
     *  @brief  Toggle fuer Magieanwendung durch Weingeist
     */};

    #IF {$zech_spells[weingeist][magie]}
    {
        #SEND {emote sagt: weingeist benutze keine magie};
        #VAR zech_spells[weingeist][magie] 0
    };
    #ELSE
    {
        #SEND {emote sagt: weingeist benutze magie};
        #VAR zech_spells[weingeist][magie] 1
    };
    drawstatus
}

#ALIAS {zstat}
{
    #SEND {zstat};
    #LINE ONESHOT
    {
        #GAG {^> $};
        #GAG {^$}
    }
}

#ALIAS {tru}
{
    mm;
    tal;
    rum leise
}

#ALIAS {tgr}
{
    tal;
    gr
}

#ALIAS {ng}
{
    nf;
    ggf
}

#ACTION {Du inhalierst einen Teil der Wolke...}
{
    #NOP { /**
     *  @brief  Zechstatus bei Inhalation der Wolke ausfuehren
     */};

    zstat
}

#ACTION {Du bestellst so unauffaellig, dass Du nichts zu zahlen brauchst.}
{
    #NOP { /**
     *  @brief  Zechstatus bei Prellen ausfuehren
     */};

    zstat
}

#ACTION {Du nimmst einen tiefen Schluck aus Deinem Faesschen. *GLUCKGLUCK*}
{
    #NOP { /**
     *  @brief  Zechstatus bei Trinken aus Faesschen ausfuehren
     */};

    zstat
}

#ACTION {Der Weingeist verfluessigt sich und fliesst in Deinen Mund! Mmmmmh Alkohol!}
{
    #NOP { /**
     *  @brief  Weingeist verwandelt sich in Alkohol
     */};

    weingeist_entlassen;
    zstat
}

#ACTION {Du laesst Dich vom Alkohol durchfluten...}
{
    #NOP { /**
     *  @brief  Starttext fuer Alkoholisiere
     *  @todo   Prespell Timer einbauen   
     */};

    #NOP #LINE GAG;
}

#ACTION {Als sich genug Allohol in Deinem Hirn angesammelt hat uebertraegst Du ihn auf}
{
    #NOP { /**
     *  @brief  Spell Alkoholisiere
     */};

    #LINE GAG;
    setze_angriff_spell alkoholisier;
    #GAG {.};
    #ACTION {Jetsch fuehlscht Dich gleich nuechterner.}
    {
        #UNGAG {.};
        #LINE GAG
    }
}

#ACTION {{.+} scheint ploetzlich betrunkener.}
{
    #NOP { /**
     *  @brief  Effekt von Alkoholisiere
     */};

    #LINE GAG;
    setze_angriff_effekte 5 K
}

#ACTION {Du geraetst in Schwingung\!}
{
    #NOP { /**
     *  @brief  Spell Torkeln beginnt
     */};

    #VAR zech_spells[torkeln][aktiv] 1;
    #NOP drawstatus
}

#ACTION {Du hoerst auf herumzutorkeln.}
{
    #NOP { /**
     *  @brief  Spell Torkeln endet
     */};

    #VAR zech_spells[torkeln][aktiv] 0;
    #HIGHLIGHT {%0} {<ffa>};
    #NOP drawstatus
}

#ACTION {Du wankst aus der Reichweite der Attacke!}
{
    #NOP { /**
     *  @brief  Effekt durch den Spell Torkeln
     */};

    #LINE GAG;
    setze_angriff_effekte 1 A
}

#ACTION {Du ruelpst laut und eine Wolke aus wunderbarem Alkoholdunst huellt Dich ein}
{
    #NOP { /**
     *  @brief  Spell Dunstpanzer beginnt
     */};

    #VAR zech_spells[dunstpanzer][aktiv] 1;
    #NOP drawstatus
}

#ACTION {Der Panzer aus Alkoholdunst loest sich auf.}
{
    #NOP { /**
     *  @brief  Spell Dunstpanzer endet
     */};

    #VAR zech_spells[dunstpanzer][aktiv] 0;
    #HIGHLIGHT {%0} {<ffa>};
    #NOP drawstatus
}

#ACTION {Du atmest Alkoholdunst aus...}
{
    #NOP { /**
     *  @brief  Spell Alkoholwolke beginnt
     */};

    #ACTION {Eine Wolke aus reinem Alkohol bildet sich.}
    {
        #VAR zech_spells[alkoholwolke][aktiv] 1;
        #NOP drawstatus;
        #UNACTION {Eine Wolke aus reinem Alkohol bildet sich.}
    }
}
#ACTION {Die Alkoholwolke verfluechtigt sich.}
{
    #NOP { /**
     *  @brief  Spell Alkoholwolke endet
     */};

    #VAR zech_spells[alkoholwolke][aktiv] 0;
    #HIGHLIGHT {%0} {<ffa>};
    #NOP drawstatus
}
#ACTION {Die Alkoholwolke huellt{.+}}
{
    #NOP { /**
     *  @brief  Spell Alkoholwolke verursacht Schaden
     */};

    setze_angriff_spell alkwolke;
    #LINE GAG
}

#ACTION {Dasch warn Ruelpser, der einen Weingeischt anlocken koennt!}
{
    #NOP { /**
     *  @brief  Spell Weingeist beginnt
     */};

    weingeist_einstellen;
    #NOP drawstatus
}
#HIGHLIGHT {Der %w%?Weingeist loest sich in Luft auf.} {light yellow}
#ACTION {Der{ ?[a-z]*} Weingeist loest sich in Luft auf.}
{
    #NOP { /**
     *  @brief  Spell Weingeist endet
     */};

    weingeist_entlassen;
    #NOP #HIGHLIGHT {%0} {<ffa>};
    #NOP drawstatus
}
#ACTION {Der Weingeist loest sich auf und verbindet sich mit Dir.}
{
    #NOP { /**
     *  @brief  Spelleffekt Verschmelzung ausgeloest
     */};

    #VAR zech_spells[verschmelzung][aktiv] 1;
    weingeist_entlassen;
    #GAG {Alkoholdunst steigt von Dir auf.};
    #FORMAT {zech_spells[verschmelzung][beginn]} {%T};
    verschmelzung_func;
    #NOP drawstatus
}
#ACTION {Die Kraft des Weingeists verlaesst Dich.}
{
    #NOP { /**
     *  @brief  Spelleffekt Verschmelzung beendet
     */};

    #VAR zech_spells[verschmelzung][aktiv] 0;
    #HIGHLIGHT {%0} {<ffa>};
    #NOP drawstatus;
    #FORMAT {zech_spells[verschmelzung][ende]} {%T};
    #MATH {time_dur} {$zech_spells[verschmelzung][ende] - $zech_spells[verschmelzung][beginn]};
    #LINE GAG;
    #ECHO {%s (%s s)} {%0} {$time_dur};
    #UNGAG {Alkoholdunst steigt von Dir auf.}
}

#ALIAS {verschmelzung_func}
{
    #NOP { /**
     *  @brief  Setzen eines Timers in der Statuszeile fuer Verschmelzung
     */};

    #VAR zech_spells[verschmelzung][dauer] 300;
    #TICKER {verschmelzung}
    {
        #MATH {zech_spells[verschmelzung][dauer]} {$zech_spells[verschmelzung][dauer] - 1};
        #IF {!$zech_spells[verschmelzung][aktiv] || !$zech_spells[verschmelzung][dauer]}
        {   
            #VAR zech_spells[verschmelzung][symbol] {V};
            drawstatus;
            #UNTICKER {verschmelzung}
        };
        #ELSE
        {
            #FORMAT zech_spells[verschmelzung][symbol] {%+3sV} {$zech_spells[verschmelzung][dauer]};
            drawstatus;
        }
    } {1}
}

#ACTION {Der Weingeist faehrt in Dich ein.}
{
    #NOP { /**
     *  @brief  Spelleffekt Heilung durch Weingeist
     */};

    weingeist_entlassen;
    drawstatus
}

#ACTION {Du kugelst am Boden herum und rempelst die Umstehenden an. Du fuehlst Dich wie}
{
    #NOP { /**
     *  @brief  Spell Rumkugeln beginnt
     */};

    #ACTION {eine Rumkugel.}
    {
        #LINE GAG;
        #UNACTION {eine Rumkugel.};
    };
    #VAR zech_spells[rumkugeln][aktiv] 1;
    drawstatus;
    #LINE GAG
}

#ACTION {Du hoerst auf mit dem rumkugeln.}
{
    #NOP { /**
     *  @Spell Rumkugeln endet
     */};

    #VAR zech_spells[rumkugeln][aktiv] 0;
    #HIGHLIGHT {%0} {<ffa>};
    drawstatus
}

#GAG {*roll* *roll* *roll*}

#ACTION {{.+}faellt zu Boden.}
{
    #NOP { /**
     *  @brief  Spelleffekt durch Rumkugeln
     */};

    setze_angriff_effekte 5 B;
    #LINE GAG
}

#ACTION {Die Wirkung des Groehlen laesst nach!}
{
    #NOP { /**
     *  @brief  Spell Groehlen endet
     */};

    #IF {&{gruppe[aktiv]}}
    {
        #eldar #ECHO {<ffa>%0<099>};
    };
    #LINE GAG;
    #ECHO {<ffa>%0<099>}
}

#ACTION {^Du entwickelst regenerative Kraefte!$}
{
    #NOP { /**
     *  @brief  Spelleffekt durch Destilliere beginnt
     */};

    #IF {!$zech_spells[destilliere][aktiv]}
    {
        #VAR zech_spells[destilliere][symbol] 0;
    };
    #VAR zech_spells[destilliere][aktiv] 1;
    #LINE GAG;
    #MATH {zech_spells[destilliere][symbol]} {$zech_spells[destilliere][symbol] + 1};
    drawstatus;
    #FORMAT {time_begin} {%T}
}

#ACTION {Deine regenerativen Kraefte schwinden!}
{
    #NOP { /**
     *  @brief  Spelleffekt durch Destilliere endet
     */};

    #MATH {zech_spells[destilliere][symbol]} {$zech_spells[destilliere][symbol] - 1};
    #IF {$zech_spells[destilliere][symbol] == 0}
    {
        #VAR zech_spells[destilliere][aktiv] 0;
    };
    drawstatus;
    #FORMAT {time_end} {%T};
    #MATH {time_dur} {$time_end - $time_begin};
    #ECHO {%s (%s s)} {%0} {$time_dur};
    #LINE GAG
}

#ACTION {Du ruelpst {.+} mit einem ekelerregendem}
{
    #NOP { /**
     *  @brief  Spell Ruelpser
     */};

    setze_angriff_spell ruelpser;
    #LINE GAG;
    #ACTION {{.*}erleichtert.}
{
        #LINE GAG;
        #UNACTION {{.*}erleichtert.}
    }
}

#ACTION {{.+} ist nach diesem Ruelpser benommen!}
{
    #NOP { /**
     *  @brief  Spelleffekt durch Ruelpser
     */};

    setze_angriff_effekte 5 R;
    #LINE GAG
}

#ACTION {Wow, das ist Dir gut gelungen! {.+} wankt,}
{
    #NOP { /**
     *  @brief  Spell Anhauchen
     */};

    setze_angriff_spell anhauchen;
    #LINE GAG;
    #ACTION {{.*}einnebelst!}
    {
        #LINE GAG;
        #UNACTION {{.*}einnebelst!}
    }
}

#ACTION {Du nimmst Deine Fackel und hauchst {.+}}
{
    #NOP { /**
     *  @brief  Spell Anhauchen mit Fackel
     */};

    setze_angriff_spell anhauchen;
    #LINE GAG;
    #ACTION {{.*}entgegen.}
    {
        #LINE GAG;
        #UNACTION {{.*}entgegen.}
    }
}

#FUNCTION {feind_zielen}
{
    #IF {"%0" == "{[A-Za-z\-]+}"}
    {
        #NOP #ECHO {w: '%0'};
        #RETURN {%0}
    };
    #IF {("%0" == "{[0-9]+}") && (&feinde[%0]) }
    {
        #NOP #ECHO {z: '%0' '$feinde[%0]'};
        #RETURN {$feinde[%0]}
    };
    #ELSE
    {
        #NOP #ECHO {z: wos};
        #RETURN {}
    }
}

#ALIAS {ah}
{
    #NOP { /**
     *  @brief  Spell Anhauchen mit Alkoholtest
     */};
    m;
    #VAR spell anhauchen;
    #IF {@alktest{2}}
    {
        #SEND $spell @feind_zielen{%0}
    }
}

#ALIAS {rum}
{
    #NOP { /**
     *  @brief  Spell Rumkugeln mit Alkoholtest
     */};
    #IF {{%0} == {}}
    {
        mm
    };
    #VAR spell rumkugeln;
    #IF {@alktest{4}}
    {
        #SEND $spell
    }
}

#ALIAS {st}
{
    #NOP { /**
     *  @brief  Spell Staenkern mit Alkoholtest
     *  @todo   auf gefuellte feinde-liste überprufen
     */};

    #VAR spell staenkern;
    #IF {"%0" == "{[0-9]+}"}
    {
        #FORMAT {target} {%l} {$feinde[%0]}
    };
    #ELSE
    {
        #VAR target %0
    };
    #IF {@alktest{2}}
    {
        #SEND $spell $target;
        #IF {$gruppe[aktiv] == 1}
        {
            #{$gruppe[partner]} toete $target
        }
    }
}

#ALIAS {wg}
{
    #NOP { /**
     *  @brief  Spell Weingeist mit Alkoholtest
     */};

    #VAR spell weingeist;
    #IF {@alktest{4}}
    {
        #SEND $spell
    }
}

#ALIAS {al}
{
    #NOP { /**
     *  @brief  Spell Alkoholisiere mit Alkoholtest
     */};
    m;
    #VAR spell alkoholisiere;
    #IF {@alktest{4}}
    {
        #SEND $spell %0
    }
}

#ALIAS {tr}
{
    #NOP { /**
     *  @brief  Spell Trunkspruch mit Alkoholtest
     */};

    #VAR spell trunkspruch;
    #IF {@alktest{3}}
    {
        #SEND $spell
    }
}

#ALIAS {dp}
{
    #NOP { /**
     *  @brief  Spell Dunstpanzer mit Alkoholtest
     */};

    #VAR spell dunstpanzer;
    #IF {@alktest{2}}
    {
        #SEND $spell
    }
}

#ALIAS {tk}
{
    #NOP { /**
     *  @brief  Spell Torkeln mit Alkoholtest
     */};

    #VAR spell torkeln;
    #IF {@alktest{2}}
    {
        #SEND $spell
    }
}

#FUNCTION {alktest}
{
    #NOP { /**
     *  @brief  Alkoholtest um unnoetige Spellpause zu vermeiden
     *
     *  @param  1   Zahl
     *  @return     Boolean
     */};

    #LOCAL index {};
    #LOCAL regex {};
    #LIST CONST_ALKOHOL FIND $status[alcohol] index;
    #IF {%0 >= $index}
    {
        #ECHO {Nicht genug Alkohol um '%n' ausfuehren zu koennen.} {$spell};
        #RETURN 0
    };
    #ELSE
    {
        #RETURN 1
    }
}

#ACTION {{.+} wehrt Deinen Zauber ab.}
{
    #NOP { /**
     *  @brief  Keinen Spellangriff bei abgewehrtem Zauber setzen 
     */};

    setze_angriff_spell
}

#ACTION {Krachend fuehrst Du einen Thekenschmetterer gegen{.*}}
{
    #NOP { /**
     *  @brief  Zechpreller Spezialschlag Thekenschmetterer
     */};

    setze_angriff_effekte 4 @zechspezialschlag{%0;aus!;T}
}

#ACTION {Du laesst Deine Waffe mit einem Schaedelbrecher-Spezial auf{.*}}
{
    #NOP { /**
     *  @brief  Zechpreller Spezialschlag Schaedelbrecher
     */};

    setze_angriff_effekte 4 @zechspezialschlag{%0;niedersausen!;B}
}

#ACTION {Du wendest bei {.+}einen{.*}}
{
    #NOP { /**
     *  @brief  Zechpreller Spezialschlag Stuhlbeinschwinger
     */};

    setze_angriff_effekte 4 @zechspezialschlag{%0;an!;S}
}

#ACTION {Du schleuderst {.+} Deine Waffe{.*}}
{
    #NOP { /**
     *  @brief  Zechpreller Spezialschlag Hosenbodenheber
     */};

    setze_angriff_effekte 4 @zechspezialschlag{%0;entgegen!;H}
}

#ACTION {Du laesst {.+} Deinen{.*}}
{
    #NOP { /**
     *  @brief  Zechpreller Spezialschlag Bierhumpenmatscher
     */};

    setze_angriff_effekte 4 @zechspezialschlag{%0;spueren!;M}
}

#FUNCTION {zechspezialschlag}
{
    #NOP { /**
     *  @brief  Setzt Trigger fuer Zechpreller Spezialschlaege
     *
     *  @param  1   Erkennungstext fuer den Trigger
     *  @param  2   Endtext fuer Regexp
     *  @param  3   Zeichen
     *  @return     eingefaerbtes Zeichen fuer Effekt
     */};

    #REGEXP {%1} {%2}
    {
    }
    {
        #ACTION {%2}
        {
            #LINE GAG;
            #UNACTION {%2}
        }
    };
    #LINE GAG;
    #RETURN <ffa>%3<099>
}

#ALIAS {d}
{
    #IF {{%0} == {}}
    {
        dd
    };
    #ELSE
    {
        #SEND {d %0}
    }
}

#ALIAS {df}
{
    #IF {{%0} == {}}
    {
        dd fass
    };
    #ELSE
    {
        #SEND {df %0}
    }
}

#ALIAS {dd}
{
    #IF {&erkenne_liste[] == 0}
    {
        #ECHO {Alle Gegenstaende bearbeitet.};
        #RETURN
    };

    #ACTION {Du deschtillierst aus}
    {
        #LIST erkenne_liste DEL 1
    };
    #ACTION {verwandelt sich in klaren Allohol,}
    {
        #LIST erkenne_liste DEL 1
    };
    #ACTION {verwandelt sich nur in Wasser.}
    {
        #LIST erkenne_liste DEL 1
    };
    #DELAY {0.3}
    {
        #IF {&erkenne_liste[] == 1}
        {
            #ECHO {Du hast noch einen Gegenstand uebrig.}
        };
        #ELSEIF {&erkenne_liste[]}
        {
            #ECHO {Du hast noch %d Gegenstaende uebrig.} {&erkenne_liste[]}
        };
        #ELSE
        {
            #ECHO {Alle Gegenstaende verdestilliert.}
        };
        #UNACTION {Du deschtillierst aus};
        #UNACTION {verwandelt sich in klaren Allohol,};
        #UNACTION {verwandelt sich nur in Wasser.}
    };
    #IF {{%0} == {}}
    {
        #SEND {d $erkenne_liste[1]}
    };
    #ELSE
    {
        #SEND {df $erkenne_liste[1]}
    }
}
#CLASS zechpreller CLOSE

#NOP {
Messingarmschienen verwandelt sich in klaren Allohol,
Du deschtillierst aus einer orkischen Kriegsaxt Allohol

3.1     27      104     ork Kriegsaxt
1.3     7       36      Messingarmschienen
3.5     17      91      Eisenpanzer
1.8     15      59      Kampfschwert
1.1     35      87      Kometenhelm
1.2     15      46      Kometenschwert
0.2     8       19      Lederhandschuhe
4.0     17      100     Kometenpanzer
#NOP }