#NOP { /**
 *  @class zechpreller.tin
 *  @brief  Alles, was mit den Spells der Zechpreller zu tun hat.
 *
 *  @todo   Spellschaeden aufschreiben
 *                        Gift  Laerm   Saeure  Luft    Magie   Feuer   Druck   Wasser
 *              Ruelpser:   X     X        -      -       -       -       -       -
 *         Ruelpser (WG):   -     -        -      X       -       -       -       -
 *             Anhauchen:   X     -        -      X       -       -       -       -
 *    Anhauchen (Fackel):   -     -        -      -       -       X       -       -
 *        Anhauchen (WG):   -     -        -      X       -       -       -       -
 * Anhauchen (WG/Fackel):   -     -        -      -       -       X       -       -
 *    Alkoholstrahl (WG):   -     -        -      -       -       -       -       X
 *         Alkoholisiere:   X     -        -      -       X       -       -       -
 *          Alkoholwolke:   X     -        X      -       -       -       -       -
 *             Rumkugeln:   -     -        -      -       -       -       X       -
 */}

#CLASS zechpreller KILL
#CLASS zechpreller OPEN

#ECHO {<029>-- <188>%s<288> geladen<099>} {gilden/zechpreller.tin}

#VAR gilden_report_cmd {zstat}

#EVENT {VARIABLE UPDATED zech_spells} {
    #VAR status_zech {@status_zech{}};
    #LIST status_items FIND status_zech tmp_status_index;
    #VAR statusline_list[$tmp_status_index] {$status_zech};
    draw_status
}

#EVENT {VARIABLE UPDATED zech_weingeist} {
    #VAR status_weingeist {@status_weingeist{}};
    #LIST status_items FIND status_weingeist tmp_status_index;
    #VAR statusline_list[$tmp_status_index] {$status_weingeist};
    draw_status
}

#NOP /* Zusammensetzung der Statuszeile fuer Zechpreller */

#LIST status_items CREATE {status_session;status_hp;status_mp;status_alcohol;status_align;status_zech;status_weingeist;status_halsband}

#NOP #LIST {status_items} ADD {ampullen}
#NOP #LIST {status_items} ADD {test}

#LIST CONST_ALKOHOL CREATE {
    nuechtern;leicht angeheitert;feucht froehlich;etwas angetrunken;angetrunken;
    etwas blau;blau;extrem blau;zechprellerblau
}

#VAR zech_spells {
    {1} {<g02>D}
    {2} {<g02>T}
    {3} {<g02>R}
    {4} {<g02>V}
    {5} {<g02>A}
    {6} {<g02>0}
}

#VAR zech_weingeist {
    {1} {<g02>WG:}
    {2} {<g02>F}
    {3} {<g02>K}
    {4} {<g02>M}
}

#VAR color_inactive {<g02>};
#VAR color_active {<099>};

#NOP { /**
 *  @fn status_zech
 *  @brief  setzt Block mit aktiven Spells fuer die Statuszeile
 *
 *  @return     formatierter String
 */};
#FUNCTION {status_zech} {
    #LIST result COPY zech_spells;
    #LIST result COLLAPSE {};

    #FORMAT {result} {%s<099>|} {$result}
}

#NOP { /**
 *  @fn status_zech
 *  @brief  setzt Block mit aktiven Spells fuer die Statuszeile
 *
 *  @return     formatierter String
 */};
#FUNCTION {status_weingeist} {
    #LIST result COPY zech_weingeist;
    #LIST result COLLAPSE {};

    #FORMAT {result} {%s<099>|} {$result}
}

#NOP { /**
 *  @brief  Trigger auf Anzeige von Report
 *
 *  Trigger erfasst die LP/MP Anzeige bei den Zechprellern im kurzen maximal
 *  Modus: zreport kurz 1, zreport max 1
 *  Bsp.:    266/266 0 LP 258/258 0 MP extrem blau 0
 */};
#ACTION {{[0-9]+}/{[0-9]+} {.?[0-9]+} LP {[0-9]+}/{[0-9]+} {.?[0-9]+} MP {[ a-z]+} {.+}} {
    check_diff status[alcohol] {%7};

    #VAR check_diff {0};
    #IF {!@_an_aus{config[report]}} {
        #LINE GAG
    }
} {4}

#NOP { /**
 *  @fn status_alcohol
 *  @brief Kurzanzeige fuer Alkoholwert
 *
 *  setzt die Kurzanzeige des Alkoholwertes in der Statuszeile
 *    + # #+ ## ##+ ### ###+ ####
 *
 *  @return   formatierter String
 */};

#FUNCTION {status_alcohol}
{
    #LIST CONST_ALKOHOL FIND {%1} index;
    #IF { $index % 2 } {
        #MATH tmp_test {$index/2};
        #VAR result {@char_repeat{#;$tmp_test}};
    };
    #ELSE {
        #MATH tmp_test {$index/2 - 1};
        #VAR result {@char_repeat{#;$tmp_test}+}
    };
    #FORMAT {result} {%-4s|} {<afc>$result<078>}
} {5};

#NOP { /**
 *  @fn wgm
 *  @brief  Toggle fuer Magieanwendung durch Weingeist
 */};
#ALIAS {weingeist_einstellen} {
    #VAR zech_weingeist[1] {@swap_state{$zech_weingeist[1];on}};
    #VAR zech_weingeist[4] {@swap_state{$zech_weingeist[4];on}};
}

#NOP { /**
 *  @fn weingeist_entlassen
 *  @brief  setzt Grundvariablen eines Weingeists zurueck
 */};
#ALIAS {weingeist_entlassen} {
    #VAR zech_weingeist[1] {@swap_state{$zech_weingeist[1];off}};
    #VAR zech_weingeist[2] {@swap_state{$zech_weingeist[2];off}};
    #VAR zech_weingeist[3] {@swap_state{$zech_weingeist[3];off}};
    #VAR zech_weingeist[4] {@swap_state{$zech_weingeist[4];off}};

    #UNVAR kampf[helfer]
}


#ACTION {Zechus lallt: %+ kannscht Du %+\.} {
    #ECHO {%s %s} {@dotfill{<ffd>%1<099>;40}} {@zech_fert{%2}<099>};
    #LINE GAG
} {5};

#ACTION {Zechus lallt: Und Allohol vertraegscht Du %+\.} {
    #ECHO {%s %s} {@dotfill{<ffd>Allohol vertragen<099>;40}} {@zech_fert{%1}<099>};
    #LINE GAG
} {5};

#ACTION {Zechus lallt: Mit stumpfen Waffen kannscht {.+} umgehen.} {
    #ECHO {%s %s} {@dotfill{<ffd>Mit stumpfen Waffen kaempfen<099>;40}} {@zech_fert{%1}<099>};
    #LINE GAG
} {5};

#ACTION {Zechus lallt: Inschgeschamt betrachtet bist Du %+\.} {
    #ECHO {%s %s} {@dotfill{<ffd>Inschgeschamt<099>;40}} {@zech_fert{%1}<099>};
    #LINE GAG
} {5};

#FUNCTION {zech_fert} {
    #VAR tmp {%0};
    #REPLACE {tmp} {. } {};
    #SWITCH { "$tmp" } {
        #CASE { "{gut|sehr gut|wirklich toll|fast schon sehr gut|phantastisch gut}" } {
            #VAR result {<bca>}
        };
        #CASE { "{unglaublich gut|hervorragend|ausgezeichnet}" } {
            #VAR result {<bdb>}
        };
        #CASE {" {exzellent|meisterhaft}" } {
            #VAR result {<dea>}
        };
        #CASE { "{nahezu perfekt|perfekt}" } {
            #VAR result {<bfa>}
        };
        #CASE { "{abscholut perfekt|absolut perfekt}" } {
            #VAR result {<dfc><388>}
        };
        #DEFAULT {
            #VAR result {<bba>}
        }
    };
    #RETURN {$result$tmp}
}

#NOP { /**
 *  @fn wgf
 *  @brief  Toggle fuer Folgen des Weingeists
 */};
#ALIAS {wgf} {
    #IF {@check_state{$zech_weingeist[2]}} {
        #SEND {emote sagt: weingeist bleib stehen};
    };
    #ELSE {
        #SEND {emote sagt: weingeist folge mir};
    };
    #VAR zech_weingeist[2] {@swap_state{$zech_weingeist[2]}};
}

#NOP { /**
 *  @fn wgk
 *  @brief  Toggle fuer Kampf des Weingeists
 */};
#ALIAS {wgk} {
    #IF {@check_state{$zech_weingeist[3]}} {
        #SEND {emote sagt: weingeist kaempfe nicht mit mir};
        #UNVAR kampf[helfer]
    };
    #ELSE {
        #SEND {emote sagt: weingeist kaempfe mit mir};
        #VAR kampf[helfer] Weingeist
    };
    #VAR zech_weingeist[3] {@swap_state{$zech_weingeist[3]}};
}

#NOP { /**
 *  @fn wgfk
 *  @brief  Kuerzel fuer Funktionen wgf und wgk
 */};
#ALIAS {wgfk} {
    wgf;
    wgk
}

#NOP { /**
 *  @fn wgm
 *  @brief  Toggle fuer Magieanwendung durch Weingeist
 */};
#ALIAS {wgm} {
    #IF {@check_state{$zech_weingeist[4]}} {
        #SEND {emote sagt: weingeist benutze keine magie};
    };
    #ELSE {
        #SEND {emote sagt: weingeist benutze magie};
    };
    #VAR zech_weingeist[4] {@swap_state{$zech_weingeist[4]}};
}

#ALIAS {zstat} {
    #SEND {zstat};
    #LINE ONESHOT {
        #GAG {^> $};
        #GAG {^$}
    }
}

#ALIAS {tru} {
    mm;
    tal;
    rum leise
}

#ALIAS {tgr} {
    tal;
    gr
}

#ALIAS {ng} {
    nf;
    ggf
}

#NOP { /**
 *  @brief  Zechstatus bei Inhalation der Wolke ausfuehren
 */};
#ACTION {Du inhalierst einen Teil der Wolke...} {
    zstat
}

#NOP { /**
 *  @brief  Zechstatus bei Prellen ausfuehren
 */};
#ACTION {Du bestellst so unauffaellig, dass Du nichts zu zahlen brauchst.} {
    zstat
}

#NOP { /**
 *  @brief  Zechstatus bei Trinken aus Faesschen ausfuehren
 */};
#ACTION {Du nimmst einen tiefen Schluck aus Deinem Faesschen. *GLUCKGLUCK*} {
    zstat
}

#NOP { /**
 *  @brief  Weingeist verwandelt sich in Alkohol
 */};
#ACTION {Der Weingeist verfluessigt sich und fliesst in Deinen Mund! Mmmmmh Alkohol!} {
    weingeist_entlassen;
    zstat
}

#NOP { /**
 *  @brief  Starttext fuer Alkoholisiere
 *  @todo   Prespell Timer einbauen
 */};
#ACTION {Du laesst Dich vom Alkohol durchfluten...} {
    #NOP #LINE GAG;
}

#NOP { /**
 *  @brief  Spell Alkoholisiere
 */};
#ACTION {Als sich genug Allohol in Deinem Hirn angesammelt hat uebertraegst Du ihn auf} {
    #LINE GAG;
    setze_angriff_spell alkoholisier;
    #GAG {.};
    #ACTION {Jetsch fuehlscht Dich gleich nuechterner.} {
        #UNGAG {.};
        #LINE GAG
    }
}

#NOP { /**
 *  @brief  Effekt von Alkoholisiere
 */};
#ACTION {{.+} scheint ploetzlich betrunkener.} {
    #LINE GAG;
    setze_angriff_effekte 5 K
}

#NOP { /**
 *  @brief  Spell Torkeln beginnt
 */};
#ACTION {Du geraetst in Schwingung\!} {
    #VAR zech_spells[2] {@swap_state{$zech_spells[2];on}};
}

#NOP { /**
 *  @brief  Spell Torkeln endet
 */};
#ACTION {Du hoerst auf herumzutorkeln.} {
    #VAR zech_spells[2] {@swap_state{$zech_spells[2];off}};
    #HIGHLIGHT {%0} {<ffa>}
}

#NOP { /**
 *  @brief  Effekt durch den Spell Torkeln
 */};
#ACTION {Du wankst aus der Reichweite der Attacke!} {
    #LINE GAG;
    setze_angriff_effekte 1 A
}

#NOP { /**
 *  @brief  Spell Dunstpanzer beginnt
 */};
#ACTION {Du ruelpst laut und eine Wolke aus wunderbarem Alkoholdunst huellt Dich ein} {
    #VAR zech_spells[1] {@swap_state{$zech_spells[1];on}};
}

#NOP { /**
 *  @brief  Spell Dunstpanzer endet
 */};
#ACTION {Der Panzer aus Alkoholdunst loest sich auf.} {
    #VAR zech_spells[1] {@swap_state{$zech_spells[1];off}};
    #HIGHLIGHT {%0} {<ffa>}
}

#NOP { /**
 *  @brief  Spell Alkoholwolke beginnt
 */};
#ACTION {Du atmest Alkoholdunst aus...} {
    #ACTION {Eine Wolke aus reinem Alkohol bildet sich.} {
        #VAR zech_spells[5] {@swap_state{$zech_spells[5];on}};
        #UNACTION {Eine Wolke aus reinem Alkohol bildet sich.}
    }
}

#NOP { /**
 *  @brief  Spell Alkoholwolke endet
 */};
#ACTION {Die Alkoholwolke verfluechtigt sich.} {
    #VAR zech_spells[5] {@swap_state{$zech_spells[5];off}};
    #HIGHLIGHT {%0} {<ffa>}
}

#NOP { /**
 *  @brief  Spell Alkoholwolke verursacht Schaden
 */};
#ACTION {Die Alkoholwolke huellt{.+}} {
    setze_angriff_spell alkwolke;
    #LINE GAG
}

#NOP { /**
 *  @brief  Spell Weingeist beginnt
 */};
#ACTION {Dasch warn Ruelpser, der einen Weingeischt anlocken koennt!} {
    weingeist_einstellen
}

#NOP { /**
 *  @brief  Spell Weingeist endet
 */};
#ACTION {Der %w%?Weingeist loest sich in Luft auf.} {
    weingeist_entlassen
}
#HIGHLIGHT {Der %w%?Weingeist loest sich in Luft auf.} {light yellow}

#NOP { /**
 *  @brief  Spelleffekt Verschmelzung ausgeloest
 */};
#ACTION {Der Weingeist loest sich auf und verbindet sich mit Dir.} {
    #VAR zech_spells[4] {@swap_state{$zech_spells[4];on}};
    weingeist_entlassen;
    #GAG {Alkoholdunst steigt von Dir auf.};
    timer_start verschmelzung
}

#NOP { /**
 *  @brief  Spelleffekt Verschmelzung beendet
 */};
#ACTION {Die Kraft des Weingeists verlaesst Dich.} {
    #VAR zech_spells[4] {@swap_state{$zech_spells[4];off}};
    #UNGAG {Alkoholdunst steigt von Dir auf.};
    timer_end verschmelzung
}
#HIGHLIGHT {Die Kraft des Weingeists verlaesst Dich.} {<ffa>};

#NOP { /**
 *  @brief  Spelleffekt Heilung durch Weingeist
 */};
#ACTION {Der Weingeist faehrt in Dich ein.} {
    weingeist_entlassen
}

#NOP { /**
 *  @brief  Spell Rumkugeln beginnt
 */};
#ACTION {Du kugelst am Boden herum und rempelst die Umstehenden an. Du fuehlst Dich wie} {
    #VAR zech_spells[3] {@swap_state{$zech_spells[3];on}};

    #LINE GAG 2
}

#NOP { /**
 *  @Spell Rumkugeln endet
 */};
#ACTION {Du hoerst auf mit dem rumkugeln.} {
    #VAR zech_spells[3] {@swap_state{$zech_spells[3];off}};
}
#HIGHLIGHT {Du hoerst auf mit dem rumkugeln.} {<ffa>};

#GAG {*roll* *roll* *roll*}

#NOP { /**
 *  @brief  Spelleffekt durch Rumkugeln
 */};
#ACTION {%+ faellt zu Boden.} {
    setze_angriff_effekte 5 B;
    #LINE GAG
}
#NOP { /**
 *  @brief  Spell Groehlen endet
 */};
#ACTION {Die Wirkung des Groehlen laesst nach!} {
    #IF {&{gruppe[aktiv]}} {
        #${gruppe[partner]} #ECHO {<ffa>%0<099>};
    };
    #LINE GAG;
    #ECHO {<ffa>%0<099>}
}

#NOP { /**
 *  @brief  Spelleffekt durch Destilliere beginnt
 */};
#ACTION {^Du entwickelst regenerative Kraefte!$} {
    #NOP #VAR zech_spells[6] {@swap_state{$zech_spells[6];on}};
    #REGEXP {$zech_spells[6]} {<%+>%+1..d} {
        #MATH tmp_destill {&2 + 1};
        #VAR zech_spells[6] {$color_active$tmp_destill};
    };
    #LINE GAG;
}

#NOP { /**
 *  @brief  Spelleffekt durch Destilliere endet
 */};
#ACTION {Deine regenerativen Kraefte schwinden!} {
    #REGEXP {$zech_spells[6]} {<%+>%+1..d} {
        #MATH tmp_destill {&2 - 1};
        #VAR zech_spells[6] {$color_active$tmp_destill};
        #IF { $tmp_destill == 0}
        {
            #VAR zech_spells[6] {@swap_state{$zech_spells[6];off}};
        };
    };
    #LINE GAG
}

#NOP { /**
 *  @brief  Spell Ruelpser
 */};
#ACTION {Du ruelpst %+ mit einem ekelerregendem%*} {
    setze_angriff_spell ruelpser;
    #LINE GAG @line_gag_check{%2;\.}
}

#NOP { /**
 *  @brief  Spelleffekt durch Ruelpser
 */};
#ACTION {%+ ist nach diesem Ruelpser benommen!} {
    setze_angriff_effekte 5 R;
    #LINE GAG
}

#NOP { /**
 *  @brief  Spell Anhauchen
 */};
#ACTION {Wow, das ist Dir gut gelungen! %+ wankt,%*} {
    setze_angriff_spell anhauchen;
    #LINE GAG @line_gag_check{%2;\!}
}

#NOP { /**
 *  @brief  Spell Anhauchen mit Fackel
 */};
#ACTION {Du nimmst Deine Fackel und hauchst %+} {
    setze_angriff_spell anhauchen;
    #LINE GAG @line_gag_check{%1;\.}
}

#FUNCTION {feind_zielen} {
    #VAR result {};
    #IF { "%0" == "{[A-Za-z\-]+}" } {
        #VAR result {%0}
    };
    #ELSEIF { ("%0" == "{[0-9]+}") && (&{feinde[%0]}) } {
        #VAR result {$feinde[%0]}
    };
    #RETURN {$result}
}

#NOP { /**
 *  @brief  Spell Anhauchen mit Alkoholtest
 */};
#ALIAS {ah} {
    m;
    #VAR spell {anhauchen};
    #IF { @alktest{2} } {
        #SEND $spell @feind_zielen{%0}
    }
}

#NOP { /**
 *  @brief  Spell Rumkugeln mit Alkoholtest
 */};
#ALIAS {rum} {
    #IF { {%0} == {} }
    {
        mm
    };
    #VAR spell {rumkugeln};
    #IF { @alktest{4} } {
        #SEND $spell
    }
}

#NOP { /**
 *  @brief  Spell Staenkern mit Alkoholtest
 *  @todo   auf gefuellte feinde-liste Ã¼berprufen
 */};
#ALIAS {st} {
    #VAR spell {staenkern};
    #IF { "%0" == "{[0-9]+}" } {
        #FORMAT {target} {%l} {$feinde[%0]}
    };
    #ELSE {
        #VAR target {%0}
    };
    #IF { @alktest{2} } {
        #SEND $spell $target;
        #IF { $gruppe[aktiv] == 1 } {
            #{$gruppe[partner]} toete $target
        }
    }
}

#NOP { /**
 *  @brief  Spell Weingeist mit Alkoholtest
 */};
#ALIAS {wg} {
    #VAR spell {weingeist};
    #IF { @alktest{4} } {
        #SEND $spell
    }
}

#NOP { /**
 *  @brief  Spell Alkoholisiere mit Alkoholtest
 */};
#ALIAS {al} {
    m;
    #VAR spell {alkoholisiere};
    #IF { @alktest{4} } {
        #SEND $spell %0
    }
}

#NOP { /**
 *  @brief  Spell Trunkspruch mit Alkoholtest
 */};
#ALIAS {tr} {
    #VAR spell {trunkspruch};
    #IF { @alktest{3} } {
        #SEND $spell
    }
}

#NOP { /**
 *  @brief  Spell Dunstpanzer mit Alkoholtest
 */};
#ALIAS {dp} {
    #VAR spell {dunstpanzer};
    #IF { @alktest{2} } {
        #SEND $spell
    }
}

#NOP { /**
 *  @brief  Spell Torkeln mit Alkoholtest
 */};
#ALIAS {tk} {
    #VAR spell {torkeln};
    #IF { @alktest{2} } {
        #SEND $spell
    }
}

#NOP { /**
 *  @brief  Alkoholtest um unnoetige Spellpause zu vermeiden
 *
 *  @param  1   Zahl
 *  @return     Boolean
 */};
#FUNCTION {alktest} {
    #LOCAL index {};

    #LIST CONST_ALKOHOL FIND $status[alcohol] index;
    #IF { %0 >= $index } {
        #ECHO {Nicht genug Alkohol um '%n' ausfuehren zu koennen.} {$spell};
        #RETURN 0
    };
    #ELSE {
        #RETURN 1
    }
}

#NOP { /**
 *  @brief  Keinen Spellangriff bei abgewehrtem Zauber setzen
 */};
#ACTION {{.+} wehrt Deinen Zauber ab.} {
    setze_angriff_spell
}

#NOP { /**
 *  @brief  Zechpreller Spezialschlag Thekenschmetterer
 */};
#ACTION {Krachend fuehrst Du einen Thekenschmetterer gegen%*} {
    setze_angriff_effekte 4 T;
    #LINE GAG @line_gag_check{%2;\!}
}

#NOP { /**
 *  @brief  Zechpreller Spezialschlag Schaedelbrecher
 */};
#ACTION {Du laesst Deine Waffe mit einem Schaedelbrecher-Spezial auf%*} {
    setze_angriff_effekte 4 B;
    #LINE GAG @line_gag_check{%2;\!}
}

#NOP { /**
 *  @brief  Zechpreller Spezialschlag Stuhlbeinschwinger
 */};
#ACTION {Du wendest bei %+ einen%*} {
    setze_angriff_effekte 4 S;
    #LINE GAG @line_gag_check{%2;\!}
}

#NOP { /**
 *  @brief  Zechpreller Spezialschlag Hosenbodenheber
 */};
#ACTION {Du schleuderst %+ Deine Waffe%*} {
    setze_angriff_effekte 4 H;
    #LINE GAG @line_gag_check{%2;\!}
}

#NOP { /**
 *  @brief  Zechpreller Spezialschlag Bierhumpenmatscher
 */};
#ACTION {Du laesst %+ Deinen%*} {
    setze_angriff_effekte 4 M;
    #LINE GAG @line_gag_check{%2;\!}
}

#NOP { /**
 *  @brief  Setzt Trigger fuer Zechpreller Spezialschlaege
 *
 *  @param  1   Erkennungstext fuer den Trigger
 *  @param  2   Endtext fuer Regexp
 *  @param  3   Zeichen
 *  @return     eingefaerbtes Zeichen fuer Effekt
 */};
#FUNCTION {zechspezialschlag} {
    #REGEXP {%1} {%2} {
        #NOP
    }
    {
        #ACTION {%2} {
            #LINE GAG;
            #UNACTION {%2}
        }
    };
    #LINE GAG;
    #RETURN <ffa>%3<099>
}

#ALIAS {d} {
    #IF { {%0} == {} } {
        dd
    };
    #ELSE {
        #SEND {d %0}
    }
}

#ALIAS {df} {
    #IF { {%0} == {} } {
        dd fass
    };
    #ELSE {
        #SEND {df %0}
    }
}

#ALIAS {dd} {
    #IF { &erkenne_liste[] == 0 } {
        #ECHO {Alle Gegenstaende bearbeitet.};
        #RETURN
    };

    #ACTION {Du deschtillierst aus} {
        #LIST erkenne_liste DEL 1
    };
    #ACTION {verwandelt sich in klaren Allohol,} {
        #LIST erkenne_liste DEL 1
    };
    #ACTION {verwandelt sich nur in Wasser.} {
        #LIST erkenne_liste DEL 1
    };
    #DELAY {0.3} {
        #IF { &{erkenne_liste[]} == 1 } {
            #ECHO {Du hast noch einen Gegenstand uebrig.}
        };
        #ELSEIF { &{erkenne_liste[]} } {
            #ECHO {Du hast noch %d Gegenstaende uebrig.} {&erkenne_liste[]}
        };
        #ELSE {
            #ECHO {Alle Gegenstaende verdestilliert.}
        };
        #UNACTION {Du deschtillierst aus};
        #UNACTION {verwandelt sich in klaren Allohol,};
        #UNACTION {verwandelt sich nur in Wasser.}
    };
    #IF { {%0} == {} } {
        #SEND {d $erkenne_liste[1]}
    };
    #ELSE {
        #SEND {df $erkenne_liste[1]}
    }
}

#CLASS zechpreller CLOSE

#NOP {
Messingarmschienen verwandelt sich in klaren Allohol,
Du deschtillierst aus einer orkischen Kriegsaxt Allohol

3.1     27      104     ork Kriegsaxt
1.3     7       36      Messingarmschienen
3.5     17      91      Eisenpanzer
1.8     15      59      Kampfschwert
1.1     35      87      Kometenhelm
1.2     15      46      Kometenschwert
0.2     8       19      Lederhandschuhe
4.0     17      100     Kometenpanzer
;
#NOP }